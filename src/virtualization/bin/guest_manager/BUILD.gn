# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("packages") {
  deps = [
    ":debian_guest_manager",
    ":guest_manager",
    ":termina_guest_manager",
    ":zircon_guest_manager",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":guest_manager_test" ]
}

source_set("lib") {
  visibility = [ ":*" ]

  sources = [
    "guest_component.cc",
    "guest_component.h",
    "guest_services.cc",
    "guest_services.h",
    "manager_impl.cc",
    "manager_impl.h",
    "realm_impl.cc",
    "realm_impl.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.sys",
    "//sdk/fidl/fuchsia.virtualization",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl",
    "//src/modular/lib/deprecated_svc",
    "//src/virtualization/bin/host_vsock:lib",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/bitmap",
  ]
}

executable("bin") {
  output_name = "guest_manager"

  sources = [ "main.cc" ]

  deps = [ ":lib" ]
}

fuchsia_package_with_single_component("guest_manager") {
  manifest = "meta/guest_manager.cmx"
  deps = [ ":bin" ]
}

source_set("lib_v2") {
  visibility = [ ":*" ]

  sources = [
    "guest_manager.cc",
    "guest_manager.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.virtualization",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl",
    "//src/virtualization/lib/guest_config",
  ]
}

executable("bin_v2") {
  output_name = "guest_manager_v2"

  sources = [ "guest_manager_main.cc" ]

  deps = [
    ":lib_v2",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_package("zircon_guest_manager") {
  deps = [ ":zircon_guest_manager_cmp" ]
}

fuchsia_component("zircon_guest_manager_cmp") {
  manifest = "meta/zircon_guest_manager.cml"
  deps = [ ":bin_v2" ]
  component_name = "zircon_guest_manager"
}

fuchsia_package("debian_guest_manager") {
  deps = [ ":debian_guest_manager_cmp" ]
}

fuchsia_component("debian_guest_manager_cmp") {
  manifest = "meta/debian_guest_manager.cml"
  deps = [ ":bin_v2" ]
  component_name = "debian_guest_manager"
}

fuchsia_package("termina_guest_manager") {
  deps = [ ":termina_guest_manager_cmp" ]
}

fuchsia_component("termina_guest_manager_cmp") {
  manifest = "meta/termina_guest_manager.cml"
  deps = [ ":bin_v2" ]
  component_name = "termina_guest_manager"
}

executable("guest_manager_test_bin") {
  testonly = true

  sources = [ "guest_manager_test.cc" ]

  deps = [
    ":lib_v2",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/testing/fidl:placeholders",
  ]
}

resource("test_configs") {
  sources = [
    "test_files/bad_schema_invalid_field.cfg",
    "test_files/valid_guest.cfg",
  ]

  outputs = [ "data/configs/{{source_file_part}}" ]
}

fuchsia_unittest_package("guest_manager_test") {
  deps = [
    ":guest_manager_test_bin",
    ":test_configs",
  ]

  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
