# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

group("tests") {
  testonly = true
  deps = [ ":virtio_gpu_unittests" ]
}

rustc_binary("virtio_gpu_bin") {
  name = "virtio_gpu_bin"
  edition = "2021"
  with_unit_tests = true

  sources = [
    "src/gpu_device.rs",
    "src/main.rs",
    "src/resource.rs",
    "src/wire.rs",
  ]

  deps = [
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition-rustc",
    "//sdk/fidl/fuchsia.virtualization.hardware:fuchsia.virtualization.hardware-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/mapped-vmo",
    "//src/lib/zerocopy",
    "//src/virtualization/lib/machina-virtio-device",
    "//src/virtualization/lib/virtio-device",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]

  deps += [
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem-rustc",
    "//src/lib/ui/fuchsia-framebuffer",
    "//src/lib/ui/fuchsia-scenic",
    "//src/lib/zircon/rust:fuchsia-zircon",
  ]

  # TODO(https://fxbug.dev/104019): the group above is only used in not(test),
  # can't express when `with_unit_tests = true` is present.
  configs += [ "//build/config/rust/lints:allow_unused_crate_dependencies" ]
}

fuchsia_unittest_package("virtio_gpu_unittests") {
  deps = [ ":virtio_gpu_bin_test" ]
}

fuchsia_package("virtio_gpu_rs") {
  deps = [ ":virtio_gpu_component" ]
}

fuchsia_component("virtio_gpu_component") {
  manifest = "meta/virtio_gpu.cml"
  deps = [ ":virtio_gpu_bin" ]
  component_name = "virtio_gpu_rs"
}
