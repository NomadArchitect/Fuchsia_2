# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/components/fuchsia_unittest_package.gni")
import("//src/graphics/lib/magma/include/virtio/virtmagma_debug.gni")

group("tests") {
  testonly = true
  deps = [
    ":device_tests_package",
    ":device_unittests_package",
    "virtio_balloon:tests",
    "virtio_block:tests",
    "virtio_console:tests",
    "virtio_gpu:tests",
    "virtio_input:tests",
    "virtio_sound:tests",
    "virtio_vsock:tests",
  ]
  public_deps = [
    "//src/cobalt/bin/testing/mock_cobalt",
    "//src/ui/scenic:scenic_pkg",
  ]
}

group("packages") {
  deps = [
    ":virtio_magma",
    ":virtio_wl",
    "virtio_balloon",
    "virtio_block",
    "virtio_console",
    "virtio_gpu",
    "virtio_input",
    "virtio_net",
    "virtio_rng",
    "virtio_sound",
    "virtio_vsock",
  ]
}

source_set("lib") {
  sources = [
    "block.h",
    "config.h",
    "input.h",
    "phys_mem.cc",
    "phys_mem.h",
    "virtio_queue.cc",
    "virtio_queue.h",
  ]
  configs += [ "//build/config:all_source" ]
  public_deps = [
    "//sdk/fidl/fuchsia.virtualization.hardware:fuchsia.virtualization.hardware_hlcpp",
    "//sdk/lib/fit",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl",
    "//third_party/mesa/include:drm-uapi",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/virtio",
  ]
}

source_set("virtio") {
  visibility = [ "./*" ]

  sources = [
    "device_base.h",
    "stream_base.h",
  ]
  public_deps = [
    ":lib",
    "//sdk/fidl/fuchsia.virtualization.hardware:fuchsia.virtualization.hardware_hlcpp",
    "//sdk/lib/sys/cpp",
    "//src/lib/fsl",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/trace-provider",
  ]
}

source_set("magma_image") {
  visibility = [ ":*" ]

  sources = [
    "magma_image.cc",
    "magma_image.h",
  ]
  deps = [
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_hlcpp",
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_cpp_wire",
    "//sdk/lib/sys/component/cpp",
    "//src/graphics/lib/magma/include:magma",
    "//src/lib/fsl",
    "//src/lib/vulkan",
    "//third_party/mesa/include:drm-uapi",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/image-format:image-format-llcpp",
    "//zircon/system/ulib/syslog",
    "//zircon/system/ulib/zx",
  ]

  # TODO(fxbug.dev/94768): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]

  # TODO(fxbug.dev/95833): This target uses the deprecated C bindings.
  # Consider switching to the C++ bindings. See linked bug for details.
  configs += [ "//build/c:fidl-deprecated-c-bindings" ]
}

source_set("virtio_magma_lib") {
  visibility = [ ":*" ]

  sources = [
    "../bits.h",
    "virtio_magma.cc",
    "virtio_magma.h",
  ]
  deps = [
    ":magma_image",
    ":virtio",
    "//src/graphics/lib/magma/include:magma",
    "//src/graphics/lib/magma/include/virtio",
    "//zircon/system/ulib/syslog",
  ]
  public_deps = [ ":virtio_magma_generic" ]

  if (virtmagma_debug) {
    defines = [ "VIRTMAGMA_DEBUG=1" ]
  }
}

executable("virtio_magma_bin") {
  visibility = [ ":*" ]
  output_name = "virtio_magma"
  deps = [
    ":virtio_magma_lib",
    "//src/graphics/lib/magma/src/libmagma",
  ]
}

executable("virtio_magma_bin_mock_system") {
  visibility = [ ":*" ]
  testonly = true
  output_dir = "$target_out_dir/virtio_magma_mock_system"
  output_name = "virtio_magma"
  deps = [
    ":virtio_magma_lib",
    "//src/graphics/lib/magma/tests/mock:magma_system",
  ]
}

resource("virtio_magma_resources") {
  sources = [ "${root_build_dir}/linux_x64-shared/libvulkan_intel.so" ]
  outputs = [ "drivers/libvulkan_intel.so" ]
}

fuchsia_package_with_single_component("virtio_magma") {
  manifest = "../meta/virtio_magma.cml"
  deps = [
    # TODO(fxbug.dev/13128): don't hard-code the driver
    ":virtio_magma_bin",
    ":virtio_magma_resources",
    "//third_party/mesa/src/intel/vulkan:vulkan_intel(//build/toolchain:linux_x64-shared)",
  ]
}

fuchsia_package_with_single_component("virtio_magma_mock_system") {
  testonly = true
  manifest = "../meta/virtio_magma.cml"
  deps = [ ":virtio_magma_bin_mock_system" ]
}

executable("virtio_wl_bin") {
  visibility = [ ":*" ]

  output_name = "virtio_wl"
  sources = [
    "../bits.h",
    "virtio_wl.cc",
    "virtio_wl.h",
  ]

  deps = [
    ":virtio",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_hlcpp",
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_hlcpp",
    "//src/lib/fsl",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_package_with_single_component("virtio_wl") {
  manifest = "../meta/virtio_wl.cml"
  deps = [ ":virtio_wl_bin" ]
}

executable("device_tests") {
  visibility = [ ":*" ]
  testonly = true

  sources = [
    "magma_image_test.cc",
    "virtio_queue_test.cc",
  ]

  deps = [
    ":lib",
    ":magma_image",
    "tests:device_tests",
    "tests:test_with_device",
    "tests:virtio_queue_fake",
    "//sdk/fidl/fuchsia.element:fuchsia.element_hlcpp",
    "//sdk/fidl/fuchsia.hardware.network:fuchsia.hardware.network_hlcpp",
    "//sdk/fidl/fuchsia.input:fuchsia.input_hlcpp",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_hlcpp",
    "//sdk/fidl/fuchsia.media:fuchsia.media_hlcpp",
    "//sdk/fidl/fuchsia.net.interfaces:fuchsia.net.interfaces_hlcpp",
    "//sdk/fidl/fuchsia.net.virtualization:fuchsia.net.virtualization_hlcpp",
    "//sdk/fidl/fuchsia.netstack:fuchsia.netstack_hlcpp",
    "//sdk/fidl/fuchsia.scheduler:fuchsia.scheduler_hlcpp",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_hlcpp",
    "//sdk/fidl/fuchsia.tracing.provider:fuchsia.tracing.provider_hlcpp",
    "//sdk/fidl/fuchsia.ui.app:fuchsia.ui.app_hlcpp",
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_hlcpp",
    "//sdk/fidl/fuchsia.ui.scenic:fuchsia.ui.scenic_hlcpp",
    "//sdk/fidl/fuchsia.virtualization.hardware:fuchsia.virtualization.hardware_hlcpp",
    "//sdk/fidl/fuchsia.vulkan.loader:fuchsia.vulkan.loader_hlcpp",
    "//sdk/lib/sys/component/cpp/testing:cpp",
    "//sdk/lib/sys/cpp/testing:integration_fixture",
    "//src/connectivity/lib/network-device/cpp",
    "//src/graphics/drivers/msd-intel-gen/include:include",
    "//src/graphics/lib/magma/include:magma",
    "//src/graphics/lib/magma/include/virtio:headers",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//src/ui/testing/ui_test_manager",
    "//third_party/googletest:gmock",
    "//third_party/mesa/include:drm-uapi",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/trace-provider",
    "//zircon/system/ulib/virtio",
  ]

  configs += [ "//build/config:all_source" ]
}

fuchsia_unittest_package("device_tests_package") {
  package_name = "device_tests"
  manifest = "meta/device_tests.cml"
  deps = [
    ":device_tests",
    "//src/ui/bin/hardware_display_controller_provider:fake-hardware-display-controller-provider-cmv2-component",
    "//src/ui/scenic:component_v2_with_supplied_display_provider",
    "//src/ui/testing/test_graphical_presenter",
  ]
  test_specs = {
    environments = guest_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

source_set("qcow_test_data") {
  visibility = [ "//src/virtualization/*" ]
  sources = [ "qcow_test_data.h" ]
}

executable("device_unittests") {
  visibility = [ ":*" ]
  testonly = true

  sources = [ "phys_mem_unittest.cc" ]

  deps = [
    ":lib",
    ":qcow_test_data",
    "//sdk/lib/fit-promise",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_unittest_package("device_unittests_package") {
  manifest = "meta/device_unittests.cml"
  package_name = "device_unittests"
  deps = [ ":device_unittests" ]
  test_specs = {
    log_settings = {
      max_severity = "FATAL"
    }
  }
}

action("virtio_magma_generic") {
  out_file = "$target_gen_dir/virtio_magma_generic.h"
  script = "virtio_magma_generic_h_gen.py"
  args = [
    rebase_path("//src/graphics/lib/magma/include/magma/magma.json",
                root_build_dir),
    rebase_path(out_file, root_build_dir),
  ]
  sources = [ "$script" ]
  inputs = [ "//src/graphics/lib/magma/include/magma/magma.json" ]
  outputs = [ "$out_file" ]
}
