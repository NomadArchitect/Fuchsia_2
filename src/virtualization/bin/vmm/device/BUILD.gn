# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//src/graphics/lib/magma/include/virtio/virtmagma_debug.gni")

group("packages") {
  deps = [
    ":virtio_balloon",
    ":virtio_block",
    ":virtio_console",
    ":virtio_gpu",
    ":virtio_input",
    ":virtio_magma",
    ":virtio_net",
    ":virtio_rng",
    ":virtio_wl",
  ]
}

source_set("device") {
  sources = [
    "block.h",
    "config.h",
    "input.h",
    "phys_mem.cc",
    "phys_mem.h",
    "virtio_queue.cc",
    "virtio_queue.h",
  ]
  configs += [ "//build/config:all_source" ]
  public_deps = [
    "//sdk/fidl/fuchsia.virtualization.hardware",
    "//sdk/lib/fit",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl",
    "//third_party/mesa/include:drm-uapi",
    "//zircon/public/lib/async-cpp",
    "//zircon/system/ulib/virtio",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

source_set("virtio") {
  visibility = [ ":*" ]

  sources = [
    "device_base.h",
    "stream_base.h",
  ]
  public_deps = [
    ":device",
    "//sdk/fidl/fuchsia.virtualization.hardware",
    "//sdk/lib/sys/cpp",
    "//src/lib/fsl",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/trace-provider:trace-provider-with-fdio",
  ]
}

executable("virtio_balloon_bin") {
  visibility = [ ":*" ]

  output_name = "virtio_balloon"
  sources = [ "virtio_balloon.cc" ]
  deps = [ ":virtio" ]
}

package("virtio_balloon") {
  binaries = [
    {
      name = "virtio_balloon"
    },
  ]
  deps = [ ":virtio_balloon_bin" ]
  meta = [
    {
      path = "../meta/virtio_balloon.cmx"
      dest = "virtio_balloon.cmx"
    },
  ]
}

source_set("block") {
  visibility = [ ":*" ]

  sources = [
    "block_dispatcher.cc",
    "block_dispatcher.h",
    "qcow.cc",
    "qcow.h",
  ]
  public_deps = [
    ":device",
    "//sdk/fidl/fuchsia.io",
    "//sdk/lib/syslog/cpp",
    "//zircon/public/lib/bitmap",
    "//zircon/system/ulib/trace",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

executable("virtio_block_bin") {
  visibility = [ ":*" ]

  output_name = "virtio_block"
  sources = [ "virtio_block.cc" ]
  deps = [
    ":block",
    ":virtio",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

package("virtio_block") {
  binaries = [
    {
      name = "virtio_block"
    },
  ]
  deps = [ ":virtio_block_bin" ]
  meta = [
    {
      path = "../meta/virtio_block.cmx"
      dest = "virtio_block.cmx"
    },
  ]
}

executable("virtio_console_bin") {
  visibility = [ ":*" ]

  output_name = "virtio_console"
  sources = [ "virtio_console.cc" ]
  deps = [ ":virtio" ]
}

package("virtio_console") {
  binaries = [
    {
      name = "virtio_console"
    },
  ]
  deps = [ ":virtio_console_bin" ]
  meta = [
    {
      path = "../meta/virtio_console.cmx"
      dest = "virtio_console.cmx"
    },
  ]
}

executable("virtio_input_bin") {
  visibility = [ ":*" ]

  output_name = "virtio_input"
  sources = [ "virtio_input.cc" ]
  deps = [ ":virtio" ]
}

package("virtio_input") {
  binaries = [
    {
      name = "virtio_input"
    },
  ]
  deps = [ ":virtio_input_bin" ]
  meta = [
    {
      path = "../meta/virtio_input.cmx"
      dest = "virtio_input.cmx"
    },
  ]
}

executable("virtio_gpu_bin") {
  visibility = [ ":*" ]

  output_name = "virtio_gpu"
  sources = [
    "gpu_resource.cc",
    "gpu_resource.h",
    "gpu_scanout.cc",
    "gpu_scanout.h",
    "guest_view.cc",
    "guest_view.h",
    "virtio_gpu.cc",
  ]
  deps = [
    ":virtio",
    "//sdk/fidl/fuchsia.ui.app",
    "//sdk/fidl/fuchsia.ui.policy",
    "//sdk/fidl/fuchsia.ui.scenic",
    "//sdk/fidl/fuchsia.ui.views",
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/ui/base_view",
  ]

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

package("virtio_gpu") {
  binaries = [
    {
      name = "virtio_gpu"
    },
  ]
  deps = [ ":virtio_gpu_bin" ]
  meta = [
    {
      path = "../meta/virtio_gpu.cmx"
      dest = "virtio_gpu.cmx"
    },
  ]
}

source_set("magma_image") {
  visibility = [ ":*" ]

  sources = [
    "magma_image.cc",
    "magma_image.h",
  ]
  deps = [
    "//sdk/fidl/fuchsia.sysmem",
    "//src/graphics/lib/magma/include:magma_abi",
    "//src/lib/fsl",
    "//src/lib/vulkan",
    "//third_party/mesa/include:drm-uapi",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/image-format:image-format-llcpp",
    "//zircon/system/ulib/service:service-llcpp",
    "//zircon/system/ulib/syslog",
  ]
}

source_set("virtio_magma_lib") {
  visibility = [ ":*" ]

  sources = [
    "../bits.h",
    "virtio_magma.cc",
    "virtio_magma.h",
  ]
  deps = [
    ":magma_image",
    ":virtio",
    "//src/graphics/lib/magma/include:magma_abi",
    "//src/graphics/lib/magma/include/virtio",
    "//zircon/system/ulib/syslog",
  ]
  public_deps = [ ":virtio_magma_generic" ]

  if (virtmagma_debug) {
    defines = [ "VIRTMAGMA_DEBUG=1" ]
  }

  # TODO(fxbug.dev/66436): Fix instances of undefined behavior.
  configs += [ "//build/config:temporarily_disable_ubsan_do_not_use" ]
}

executable("virtio_magma_bin") {
  visibility = [ ":*" ]
  output_name = "virtio_magma"
  deps = [
    ":virtio_magma_lib",
    "//src/graphics/lib/magma/src/libmagma",
  ]
}

executable("virtio_magma_bin_mock_system") {
  visibility = [ ":*" ]
  testonly = true
  output_name = "virtio_magma_mock_system"
  deps = [
    ":virtio_magma_lib",
    "//src/graphics/lib/magma/tests/mock:magma_system",
  ]
}

package("virtio_magma") {
  binaries = [
    {
      name = "virtio_magma"
    },
  ]
  deps = [
    ":virtio_magma_bin",
    "//third_party/mesa/src/intel/vulkan:vulkan_intel(//build/toolchain:linux_x64-shared)",
  ]

  # TODO(fxbug.dev/13128): don't hard-code the driver
  resources = [
    {
      path =
          rebase_path("${root_build_dir}/linux_x64-shared/libvulkan_intel.so")
      dest = "drivers/libvulkan_intel.so"
    },
  ]
  meta = [
    {
      path = "../meta/virtio_magma.cmx"
      dest = "virtio_magma.cmx"
    },
  ]
}

package("virtio_magma_mock_system") {
  testonly = true
  binaries = [
    {
      name = "virtio_magma_mock_system"
      dest = "virtio_magma"
    },
  ]
  deps = [ ":virtio_magma_bin_mock_system" ]

  meta = [
    {
      path = "../meta/virtio_magma.cmx"
      dest = "virtio_magma_mock_system.cmx"
    },
  ]
}

executable("virtio_net_bin") {
  visibility = [ ":*" ]

  output_name = "virtio_net"
  sources = [
    "guest_ethernet.cc",
    "guest_ethernet.h",
    "virtio_net.cc",
  ]
  deps = [
    ":virtio",
    "//sdk/fidl/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet",
    "//sdk/fidl/fuchsia.netstack",
    "//sdk/lib/fit-promise",
    "//zircon/public/lib/async-cpp",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

package("virtio_net") {
  binaries = [
    {
      name = "virtio_net"
    },
  ]
  deps = [ ":virtio_net_bin" ]
  meta = [
    {
      path = "../meta/virtio_net.cmx"
      dest = "virtio_net.cmx"
    },
  ]
}

executable("virtio_rng_bin") {
  visibility = [ ":*" ]

  output_name = "virtio_rng"
  sources = [ "virtio_rng.cc" ]
  deps = [ ":virtio" ]
}

package("virtio_rng") {
  binaries = [
    {
      name = "virtio_rng"
    },
  ]
  deps = [ ":virtio_rng_bin" ]
  meta = [
    {
      path = "../meta/virtio_rng.cmx"
      dest = "virtio_rng.cmx"
    },
  ]
}

executable("virtio_wl_bin") {
  visibility = [ ":*" ]

  output_name = "virtio_wl"
  sources = [
    "../bits.h",
    "virtio_wl.cc",
    "virtio_wl.h",
  ]

  deps = [
    ":virtio",
    "//sdk/fidl/fuchsia.scenic.allocation",
    "//sdk/fidl/fuchsia.sysmem",
    "//zircon/system/ulib/fbl",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

package("virtio_wl") {
  binaries = [
    {
      name = "virtio_wl"
    },
  ]
  deps = [ ":virtio_wl_bin" ]

  meta = [
    {
      path = "../meta/virtio_wl.cmx"
      dest = "virtio_wl.cmx"
    },
  ]
}

executable("device_tests") {
  visibility = [ "//src/virtualization/bin/vmm:*" ]
  testonly = true

  sources = [
    "magma_image_test.cc",
    "test_with_device.cc",
    "test_with_device.h",
    "virtio_balloon_test.cc",
    "virtio_block_test.cc",
    "virtio_console_test.cc",
    "virtio_gpu_test.cc",
    "virtio_input_test.cc",
    "virtio_magma_test.cc",
    "virtio_net_test.cc",
    "virtio_queue_fake.cc",
    "virtio_queue_fake.h",
    "virtio_queue_test.cc",
    "virtio_rng_test.cc",
    "virtio_wl_unittest.cc",
  ]

  deps = [
    ":device",
    ":magma_image",
    "//sdk/fidl/fuchsia.netstack",
    "//sdk/fidl/fuchsia.ui.scenic",
    "//sdk/fidl/fuchsia.virtualization.hardware",
    "//sdk/lib/sys/cpp/testing:integration",
    "//src/graphics/lib/magma/include:magma_abi",
    "//src/graphics/lib/magma/include/virtio:headers",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/mesa/include:drm-uapi",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/trace-provider:trace-provider-with-fdio",
    "//zircon/system/ulib/virtio",
  ]

  configs += [ "//build/config:all_source" ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

source_set("qcow_test_data") {
  visibility = [ "//src/virtualization/*" ]
  sources = [ "qcow_test_data.h" ]
  public_deps = [ ":block" ]
}

executable("device_unittests") {
  visibility = [ "//src/virtualization/bin/vmm:*" ]
  testonly = true

  sources = [
    "qcow_unittest.cc",
    "volatile_write_block_dispatcher_unittest.cc",
  ]

  deps = [
    ":qcow_test_data",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//zircon/public/lib/fbl",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

action("virtio_magma_generic") {
  out_file = "$target_gen_dir/virtio_magma_generic.h"
  script = "virtio_magma_generic_h_gen.py"
  args = [
    rebase_path("//src/graphics/lib/magma/include/magma_abi/magma.json",
                root_build_dir),
    rebase_path(out_file, root_build_dir),
  ]
  sources = [ "$script" ]
  inputs = [ "//src/graphics/lib/magma/include/magma_abi/magma.json" ]
  outputs = [ "$out_file" ]
}
