# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/components/fuchsia_unittest_package.gni")

group("tests") {
  testonly = true
  deps = [
    ":device_unittests_package",
    "tests",
    "virtio_balloon:tests",
    "virtio_block:tests",
    "virtio_console:tests",
    "virtio_gpu:tests",
    "virtio_input:tests",
    "virtio_magma:tests",
    "virtio_net_rs:tests",
    "virtio_sound:tests",
    "virtio_vsock:tests",
  ]
}

group("packages") {
  deps = [
    ":virtio_wl",
    "virtio_balloon",
    "virtio_block",
    "virtio_console",
    "virtio_gpu",
    "virtio_input",
    "virtio_magma",
    "virtio_net",
    "virtio_net_rs",
    "virtio_rng",
    "virtio_sound",
    "virtio_vsock",
  ]
}

source_set("lib") {
  sources = [
    "block.h",
    "config.h",
    "input.h",
    "phys_mem.cc",
    "phys_mem.h",
    "virtio_queue.cc",
    "virtio_queue.h",
  ]
  configs += [ "//build/config:all_source" ]
  public_deps = [
    "//sdk/fidl/fuchsia.virtualization.hardware:fuchsia.virtualization.hardware_hlcpp",
    "//sdk/lib/fit",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl",
    "//third_party/mesa/include:drm-uapi",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/virtio",
  ]
}

source_set("virtio") {
  visibility = [ "./*" ]

  sources = [
    "device_base.h",
    "stream_base.h",
  ]
  public_deps = [
    ":lib",
    "//sdk/fidl/fuchsia.virtualization.hardware:fuchsia.virtualization.hardware_hlcpp",
    "//sdk/lib/sys/cpp",
    "//src/lib/fsl",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/trace-provider",
  ]
}

executable("virtio_wl_bin") {
  visibility = [ ":*" ]

  output_name = "virtio_wl"
  sources = [
    "../bits.h",
    "virtio_wl.cc",
    "virtio_wl.h",
  ]

  deps = [
    ":virtio",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_hlcpp",
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_hlcpp",
    "//src/lib/fsl",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_component("virtio_wl_component") {
  component_name = "virtio_wl"
  manifest = "../meta/virtio_wl.cml"
  deps = [ ":virtio_wl_bin" ]
}

fuchsia_package("virtio_wl") {
  deps = [ ":virtio_wl_component" ]
}

source_set("qcow_test_data") {
  visibility = [ "//src/virtualization/*" ]
  sources = [ "qcow_test_data.h" ]
}

executable("device_unittests") {
  visibility = [ ":*" ]
  testonly = true

  sources = [
    "phys_mem_unittest.cc",
    "virtio_queue_unittest.cc",
  ]

  deps = [
    ":lib",
    "tests:virtio_queue_fake",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
  ]
}

fuchsia_unittest_package("device_unittests_package") {
  manifest = "meta/device_unittests.cml"
  package_name = "device_unittests"
  deps = [ ":device_unittests" ]
  test_specs = {
    log_settings = {
      max_severity = "FATAL"
    }
  }
}
