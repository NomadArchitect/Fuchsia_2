# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_macro.gni")
import("//src/developer/ffx/build/services.gni")

template("ffx_services") {
  output_name = target_name
  if (defined(invoker.name)) {
    output_name = invoker.name
  }

  service_version = "0.1.0"
  if (defined(invoker.version)) {
    service_version = invoker.version
  }

  service_deps = []
  if (defined(invoker.deps)) {
    service_deps += invoker.deps
  }

  service_sources = []
  if (defined(invoker.sources)) {
    service_sources += invoker.sources
  }

  services_gen = output_name + "_gen"
  services(services_gen) {
    output_name = "macros.rs"
    deps = service_deps
  }

  service_deps += [ ":" + services_gen ]

  services_macro_lib = output_name + "_macros"
  rustc_macro(services_macro_lib) {
    output_name = services_macro_lib
    version = service_version
    edition = "2018"
    source_root = "$target_gen_dir/macros.rs"
    sources = [ "$target_gen_dir/macros.rs" ]
    deps = service_deps + [
             "//third_party/rust_crates:proc-macro2",
             "//third_party/rust_crates:quote",
             "//third_party/rust_crates:syn",
             "//src/developer/ffx/lib/services:lib",
             "//src/developer/ffx/lib/services/dependencies:lib",
             "//src/lib/fidl/rust/fidl",
           ]
  }

  rustc_library(output_name + "_lib") {
    name = output_name
    version = service_version
    edition = "2018"
    with_unit_tests = true
    deps =
        service_deps + [ ":" + services_macro_lib ] + [
          "//sdk/fidl/fuchsia.developer.bridge:fuchsia.developer.bridge-rustc",
          "//src/developer/ffx/lib/services:lib",
          "//src/lib/fidl/rust/fidl",
          "//third_party/rust_crates:anyhow",
          "//third_party/rust_crates:async-trait",
          "//third_party/rust_crates:futures",
        ]

    sources = service_sources
  }
}
