# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

visibility = [ "//src/developer/forensics/feedback_data/*" ]

###############################################################################
# SOURCES
###############################################################################
source_set("annotation_providers") {
  public = [
    "annotation_provider.h",
    "annotation_provider_factory.h",
  ]

  sources = [
    "annotation_provider.h",
    "annotation_provider_factory.cc",
    "annotation_provider_factory.h",
    "device_id_provider.cc",
    "device_id_provider.h",
  ]

  deps = [
    ":types",
    ":utils",
    "//sdk/lib/fit-promise",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/developer/forensics/feedback:device_id_provider",
    "//src/developer/forensics/feedback_data:constants",
    "//src/developer/forensics/utils:errors",
    "//src/developer/forensics/utils:time",
    "//src/lib/fxl",
    "//src/lib/timekeeper",
    "//src/lib/timekeeper:system",
  ]
}

source_set("static_annotations") {
  public = [ "static_annotations.h" ]

  sources = [
    "static_annotations.cc",
    "static_annotations.h",
  ]

  public_deps = [
    ":types",
    "//src/developer/forensics/utils:previous_boot_file",
  ]

  deps = [
    ":utils",
    "//sdk/fidl/fuchsia.sysinfo",
    "//sdk/lib/fdio",
    "//sdk/lib/syslog/cpp",
    "//src/developer/forensics/feedback_data:constants",
    "//src/developer/forensics/utils:errors",
    "//src/lib/files",
    "//src/lib/fxl",
  ]
}

source_set("types") {
  sources = [ "types.h" ]

  public_deps = [
    "//sdk/lib/syslog/cpp",
    "//src/developer/forensics/utils:errors",
  ]

  visibility += [ "//src/developer/forensics/testing:gpretty_printers" ]
}

source_set("utils") {
  sources = [
    "utils.cc",
    "utils.h",
  ]

  public_deps = [
    ":types",
    "//sdk/fidl/fuchsia.feedback",
  ]

  deps = [ "//third_party/rapidjson" ]
}
