# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/components/fuchsia_package_with_single_component.gni")
import("//build/config.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

fuchsia_system_package("msd-intel-gen") {
  deps = [ "//src/graphics/drivers/msd-intel-gen:msd_intel" ]
}

fuchsia_system_package("msd-intel-gen-test") {
  testonly = true
  deps = [ "//src/graphics/drivers/msd-intel-gen:msd_intel_test" ]
}

group("test-driver") {
  testonly = true
  deps = [
    ":msd-intel-gen-test",
    "//src/graphics/lib/magma/tests/unit_tests:testing_firmware_file",
  ]
}

fuchsia_test_component("msd_intel_gen_nonhardware_tests") {
  deps = [ "//src/graphics/drivers/msd-intel-gen:tests" ]
  manifest = "meta/msd_intel_gen_nonhardware_tests.cmx"
}

fuchsia_test_component("msd_intel_gen_integration_tests") {
  deps = [ "//src/graphics/drivers/msd-intel-gen/tests/integration" ]
  manifest = "meta/msd_intel_gen_integration_tests.cmx"
}

# Mesa Intel Vulkan driver requires x64 build
if (target_cpu == "x64") {
  resource("vulkan_metadata") {
    sources = [ "metadata.json" ]
    outputs = [ "meta/metadata/metadata.json" ]
  }

  if (bootfs_only) {
    # Don't package ICD on bringup builds, because it wastes space and the loader service doesn't
    # support loading it from bootfs.
    group("libvulkan_intel_gen") {
    }
  } else {
    fuchsia_package_with_single_component("libvulkan_intel_gen") {
      component_name = "vulkan"
      deps = [
        ":libvulkan_intel_config_resource",
        ":vulkan_metadata",
        "//third_party/mesa:magma_vulkan",
      ]
      manifest = "meta/vulkan.cml"
    }
  }

  icd_config_data("libvulkan_intel_config_resource") {
    icds = [
      {
        lib = "libvulkan_intel.so"
        manifest = "$target_gen_dir/vulkan/icd.d/libvulkan_intel.json"
      },
    ]
    resource_path = "meta/icd.d"
  }

  fuchsia_test_component("mesa_unit_tests") {
    deps = [ "//third_party/mesa/tests:mesa_unit_tests" ]
    manifest = "meta/mesa_unit_tests.cmx"
  }

  fuchsia_test_component("state_pool") {
    deps = [ "//third_party/mesa/src/intel/vulkan/tests:state_pool" ]
    manifest = "meta/state_pool.cmx"
  }

  fuchsia_test_component("state_pool_free_list_only") {
    deps = [
      "//third_party/mesa/src/intel/vulkan/tests:state_pool_free_list_only",
    ]
    manifest = "meta/state_pool_free_list_only.cmx"
  }

  fuchsia_test_component("state_pool_no_free") {
    deps = [ "//third_party/mesa/src/intel/vulkan/tests:state_pool_no_free" ]
    manifest = "meta/state_pool_no_free.cmx"
  }

  fuchsia_test_component("block_pool_no_free") {
    deps = [ "//third_party/mesa/src/intel/vulkan/tests:block_pool_no_free" ]
    manifest = "meta/block_pool_no_free.cmx"
  }

  fuchsia_test_package("magma_intel_gen_nonhardware_tests") {
    test_components = [
      ":msd_intel_gen_nonhardware_tests",
      ":mesa_unit_tests",
      ":state_pool",
      ":state_pool_free_list_only",
      ":state_pool_no_free",
      ":block_pool_no_free",
    ]
  }

  fuchsia_test_component("mesa_integration_tests") {
    deps = [ "//third_party/mesa:tests" ]
    manifest = "meta/mesa_integration_tests.cmx"
  }

  fuchsia_test_package("magma_intel_gen_integration_tests") {
    test_components = [
      ":msd_intel_gen_integration_tests",
      ":mesa_integration_tests",
    ]
    test_specs = {
      environments = intel_gpu_envs
    }
  }
}
