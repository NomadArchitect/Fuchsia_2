# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/components/fuchsia_package_with_single_component.gni")
import("//build/config.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

# Mesa Intel Vulkan driver requires x64 build
if (target_cpu == "x64") {
  group("magma-vulkan-system") {
    if (!magma_enable_developer_build) {
      public_deps = [ ":magma-intel-gen" ]
    }
  }

  group("magma-vulkan-config") {
    if (!magma_enable_developer_build) {
      public_deps = [ ":magma-intel-gen-config" ]
    }
  }

  if (magma_enable_developer_build) {
    group("magma-dev") {
      testonly = true
      public_deps = [
        ":magma-intel-gen",
        ":magma-intel-gen-config",
      ]
      deps =
          [ "//src/graphics/lib/magma/tests/unit_tests:testing_firmware_file" ]
    }
  }

  fuchsia_system_package("magma-intel-gen") {
    if (magma_enable_developer_build) {
      testonly = true
    }

    deps = []

    if (magma_enable_developer_build) {
      deps += [ "$msd_intel_gen_build_root:msd_intel_test" ]
    } else {
      deps += [ "$msd_intel_gen_build_root:msd_intel" ]
    }

    # This package is reachable from //garnet/packages/prod:all
    allowed_in_extra_deps = true
  }

  resource("vulkan_metadata") {
    sources = [ "metadata.json" ]
    outputs = [ "data/metadata.json" ]
  }

  fuchsia_package_with_single_component("libvulkan_intel_gen") {
    component_name = "vulkan"
    deps = [
      ":vulkan_metadata",
      "//third_party/mesa:magma_vulkan",
    ]
    manifest = "meta/vulkan.cml"
  }

  icd_config_data("magma-intel-gen-config") {
    icds = [
      {
        lib = "libvulkan_intel.so"
        manifest = "$target_gen_dir/vulkan/icd.d/libvulkan_intel.json"
      },
    ]
  }

  fuchsia_test_component("msd_intel_gen_nonhardware_tests") {
    deps = [ "$msd_intel_gen_build_root:tests" ]
    manifest = "meta/msd_intel_gen_nonhardware_tests.cmx"
  }

  fuchsia_test_component("mesa_unit_tests") {
    deps = [ "//third_party/mesa/tests:mesa_unit_tests" ]
    manifest = "meta/mesa_unit_tests.cmx"
  }

  fuchsia_test_component("state_pool") {
    deps = [ "//third_party/mesa/src/intel/vulkan/tests:state_pool" ]
    manifest = "meta/state_pool.cmx"
  }

  fuchsia_test_component("state_pool_free_list_only") {
    deps = [
      "//third_party/mesa/src/intel/vulkan/tests:state_pool_free_list_only",
    ]
    manifest = "meta/state_pool_free_list_only.cmx"
  }

  fuchsia_test_component("state_pool_no_free") {
    deps = [ "//third_party/mesa/src/intel/vulkan/tests:state_pool_no_free" ]
    manifest = "meta/state_pool_no_free.cmx"
  }

  fuchsia_test_component("block_pool_no_free") {
    deps = [ "//third_party/mesa/src/intel/vulkan/tests:block_pool_no_free" ]
    manifest = "meta/block_pool_no_free.cmx"
  }

  fuchsia_test_package("magma_intel_gen_nonhardware_tests") {
    test_components = [
      ":msd_intel_gen_nonhardware_tests",
      ":mesa_unit_tests",
      ":state_pool",
      ":state_pool_free_list_only",
      ":state_pool_no_free",
      ":block_pool_no_free",
    ]
  }

  fuchsia_test_component("msd_intel_gen_integration_tests") {
    deps = [ "//src/graphics/drivers/msd-intel-gen/tests/integration" ]
    manifest = "meta/msd_intel_gen_integration_tests.cmx"
  }

  fuchsia_test_component("mesa_integration_tests") {
    deps = [ "//third_party/mesa:tests" ]
    manifest = "meta/mesa_integration_tests.cmx"
  }

  fuchsia_test_package("magma_intel_gen_integration_tests") {
    test_components = [
      ":msd_intel_gen_integration_tests",
      ":mesa_integration_tests",
    ]
    test_specs = {
      environments = intel_gpu_envs
    }
  }
}
