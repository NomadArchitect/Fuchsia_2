# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")
import("//src/sys/build/components.gni")
import("//src/sys/build/fuchsia_system_package.gni")

group("magma-vulkan") {
  if (!magma_enable_developer_build) {
    public_deps = [ ":magma-qcom-adreno" ]
    if (build_libvulkan_qcom_adreno != []) {
      public_deps += [ ":magma-qcom-adreno-config" ]
    }
  }
}

if (magma_enable_developer_build) {
  group("magma-dev") {
    testonly = true
    public_deps = [ ":magma-qcom-adreno" ]
    if (build_libvulkan_qcom_adreno != []) {
      public_deps += [ ":magma-qcom-adreno-config" ]
    }
    deps = [ "//src/graphics/lib/magma/tests/unit_tests:testing_firmware_file" ]
  }
}

fuchsia_system_package("magma-qcom-adreno") {
  if (magma_enable_developer_build) {
    testonly = true
  }

  deps = []

  if (build_libvulkan_qcom_adreno != []) {
    deps += build_libvulkan_qcom_adreno
  }

  if (magma_enable_developer_build) {
    deps += [ "//src/graphics/drivers/msd-qcom-adreno:msd_qcom_test" ]
  } else {
    deps += [ "//src/graphics/drivers/msd-qcom-adreno:msd_qcom" ]
  }

  # This package is reachable from //garnet/packages/prod
  allowed_in_extra_deps = true
}

if (build_libvulkan_qcom_adreno != []) {
  icd_config_data("magma-qcom-adreno-config") {
    icds = [
      {
        lib = "libvulkan_adreno.so"
        manifest = "$target_gen_dir/vulkan/icd.d/libvulkan_adreno.json"
      },
    ]
  }
}
