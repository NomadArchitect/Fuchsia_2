# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/driver_package.gni")
import("//build/test.gni")
import("//src/sys/build/components.gni")

bind_rules("goldfish_control_composite-bind") {
  rules = "goldfish_control_composite.bind"
  output = "goldfish_control_composite-bind.h"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.google.platform" ]
}

common_deps = [
  ":goldfish_control_composite-bind",
  "//sdk/banjo/fuchsia.hardware.goldfish.addressspace",
  "//sdk/banjo/fuchsia.hardware.goldfish.control",
  "//sdk/banjo/fuchsia.hardware.goldfish.pipe",
  "//sdk/banjo/fuchsia.hardware.goldfish.sync",
  "//sdk/fidl/fuchsia.hardware.goldfish:fuchsia.hardware.goldfish_llcpp",
  "//sdk/fidl/fuchsia.sysmem2:fuchsia.sysmem2_llcpp",
  "//sdk/lib/fit-promise",
  "//sdk/lib/syslog/cpp:backend_legacy",
  "//src/devices/lib/driver",
  "//src/lib/ddk",
  "//src/lib/ddktl",
  "//src/lib/fsl",
  "//zircon/public/lib/fbl",
  "//zircon/public/lib/fidl-llcpp",
  "//zircon/public/lib/zircon-internal",
  "//zircon/public/lib/zx",
  "//zircon/system/ulib/async-loop:async-loop-cpp",
  "//zircon/system/ulib/async-loop:async-loop-default",
  "//zircon/system/ulib/trace",
  "//zircon/system/ulib/trace:trace-driver",
]

driver_module("control_driver") {
  output_name = "goldfish_control"

  sources = [
    "control_device.cc",
    "control_device.h",
    "device_local_heap.cc",
    "device_local_heap.h",
    "heap.cc",
    "heap.h",
    "host_visible_heap.cc",
    "host_visible_heap.h",
  ]

  deps = common_deps

  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]

  configs += [ "//build/config:all_source" ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

driver_package("goldfish_control") {
  deps = [ ":control_driver" ]

  drivers = [
    {
      name = "goldfish_control.so"
    },
  ]
}

test("goldfish-control-device-test") {
  output_name = "goldfish-control-device-test"
  sources = [
    "control_device.cc",
    "control_device.h",
    "device_local_heap.cc",
    "device_local_heap.h",
    "heap.cc",
    "heap.h",
    "host_visible_heap.cc",
    "host_visible_heap.h",
    "tests/control_device_test.cc",
  ]
  deps = common_deps + [
           "//sdk/fidl/fuchsia.sysmem2:fuchsia.sysmem2_llcpp",
           "//src/devices/testing/fake_ddk",
           "//third_party/googletest:gtest",
           "//third_party/googletest:gtest_main",
           "//zircon/system/ulib/mmio-ptr:mmio-ptr-fake",
           "//src/devices/testing/fake-bti",
           "//src/devices/testing/fake-object",
           "//zircon/public/lib/mock-function",
           "//zircon/public/lib/sync",
           "//zircon/public/lib/zircon-internal",
           "//zircon/public/lib/zx",
         ]

  configs += [ "//build/config:all_source" ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

fuchsia_unittest_package("goldfish-control-device-test-package") {
  package_name = "goldfish-control-device-test"
  deps = [ ":goldfish-control-device-test" ]
}

group("tests") {
  testonly = true
  deps = [ ":goldfish_control_composite-bind_test" ]

  if (target_cpu == "x64") {
    deps += [ ":goldfish-control-device-test-package" ]
  }
}
