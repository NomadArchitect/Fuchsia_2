# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")

bind_rules("goldfish-bind") {
  rules = "goldfish.bind"
  output = "goldfish-bind.h"
  tests = "tests.json"
  deps = [ "//src/devices/bind/fuchsia.acpi" ]
}

common_deps = [
  ":goldfish-bind",
  "//sdk/banjo/fuchsia.hardware.acpi:fuchsia.hardware.acpi_banjo_cpp",
  "//sdk/banjo/fuchsia.hardware.goldfish.pipe",
  "//sdk/fidl/fuchsia.hardware.goldfish:fuchsia.hardware.goldfish_llcpp",
  "//src/devices/lib/driver",
  "//src/devices/lib/mmio",
  "//src/lib/ddk",
  "//src/lib/ddktl",
  "//zircon/public/lib/fbl",
  "//zircon/public/lib/fidl-llcpp",
  "//zircon/public/lib/zircon-internal",
  "//zircon/public/lib/zx",
  "//zircon/system/ulib/async-loop:async-loop-cpp",
  "//zircon/system/ulib/async-loop:async-loop-default",
  "//zircon/system/ulib/trace",
  "//zircon/system/ulib/trace:trace-driver",
]

driver_module("goldfish_driver") {
  output_name = "goldfish"

  sources = [
    "instance.cc",
    "instance.h",
    "pipe.cc",
    "pipe.h",
    "pipe_device.cc",
    "pipe_device.h",
  ]

  deps = common_deps
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]

  configs += [ "//build/config:all_source" ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

fuchsia_system_package("goldfish") {
  deps = [ ":goldfish_driver" ]
  allowed_in_extra_deps = true
}

test("goldfish-pipe-device-test") {
  output_name = "goldfish-pipe-device-test"
  sources = [
    "instance.cc",
    "instance_test.cc",
    "pipe.cc",
    "pipe_device.cc",
    "pipe_device_test.cc",
  ]
  deps = common_deps + [
           "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_llcpp",
           "//sdk/banjo/fuchsia.hardware.acpi:fuchsia.hardware.acpi_banjo_cpp_mock",
           "//sdk/banjo/fuchsia.hardware.goldfish.pipe:fuchsia.hardware.goldfish.pipe_mock",
           "//src/devices/testing/fake-bti",
           "//src/devices/testing/fake_ddk",
           "//zircon/system/ulib/mmio-ptr:mmio-ptr-fake",
           "//zircon/system/ulib/fidl-async:fidl-async-cpp",
           "//zircon/public/lib/mock-function",
           "//zircon/public/lib/sync",
           "//zircon/public/lib/zircon-internal",
           "//zircon/public/lib/zx",
           "//zircon/public/lib/zxtest",
         ]

  configs += [ "//build/config:all_source" ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

fuchsia_unittest_package("goldfish-pipe-device-test-package") {
  package_name = "goldfish-pipe-device-test"
  deps = [ ":goldfish-pipe-device-test" ]
}

group("tests") {
  testonly = true
  deps = [ ":goldfish-bind_test" ]

  if (target_cpu == "x64") {
    deps += [ ":goldfish-pipe-device-test-package" ]
  }
}
