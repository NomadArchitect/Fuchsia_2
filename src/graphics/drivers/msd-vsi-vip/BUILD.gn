# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")
import("//src/sys/build/fuchsia_system_package.gni")

fuchsia_system_package("msd-vsi-vip") {
  deps = [ ":msd_vsi" ]
}

fuchsia_system_package("msd-vsi-vip-test") {
  testonly = true
  deps = [ ":msd_vsi_test" ]
}

driver_module("msd_vsi") {
  output_name = "libmsd_vsi"
  deps = [
    ":msd_vsi_entry",
    "$magma_build_root/src/magma_util/platform:logger_for_system_drivers",
    "src",
  ]
}

driver_module("msd_vsi_test") {
  testonly = true
  output_name = "libmsd_vsi_test"
  deps = [
    ":msd_vsi_test_entry",
    "$magma_build_root/src/magma_util/platform:logger_for_system_drivers",
    "src",
  ]
}

bind_rules("msd_vsi_bind") {
  rules = "msd_vsi.bind"
  output = "bind.h"
  tests = "tests/bind_tests.json"
  deps = [
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.verisilicon.platform",
  ]
}

bind_rules("msd_vsi_test_bind") {
  rules = "msd_vsi.bind"
  output = "test_bind.h"
  disable_autobind = true
  deps = [
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.verisilicon.platform",
  ]
}

magma_pdev_entry("msd_vsi_entry") {
  deps = [ ":msd_vsi_bind" ]
}

magma_pdev_entry("msd_vsi_test_entry") {
  test_entry = true
  deps = [
    ":msd_vsi_test_bind",
    "tests/unit_tests:hardware_tests",
    "//src/graphics/lib/magma/tests/unit_tests:magma_platform_hardware_tests",
    "//src/graphics/lib/magma/tests/unit_tests:msd_abi_conformance_tests",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":msd_vsi_bind_test" ]
}
