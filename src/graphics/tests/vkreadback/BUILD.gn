# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
#
import("//build/components.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

source_set("src") {
  testonly = true

  sources = [
    "vkreadback.cc",
    "vkreadback.h",
  ]

  deps = [
    "//src/graphics/tests/common",
    "//zircon/system/ulib/zx",
  ]

  public_deps = [
    "//src/lib/vulkan",
    "//third_party/googletest:gtest",
  ]
}

executable("vkreadback_bin") {
  testonly = true
  output_name = "vkreadback"

  sources = [ "main.cc" ]

  deps = [
    ":src",
    "//src/lib/fxl/test:gtest_main",
  ]
}

group("linux_arm64") {
  testonly = true
  deps = [ ":vkreadback_bin(//build/toolchain:linux_arm64)" ]
}

fuchsia_component("vkreadback_cmp") {
  testonly = true
  component_name = "vkreadback"
  deps = [
    ":vkreadback_bin",
    "//src/lib/vulkan/validation_layers",
  ]

  manifest = "meta/vkreadback.cml"
}

fuchsia_test_package("vkreadback-pkg") {
  package_name = "vkreadback"
  test_components = [ ":vkreadback_cmp" ]
  test_specs = {
    # TODO(fxbug.dev/47373): Restore astro_env when fxbug.dev/44570 is fixed.
    environments = [ aemu_env ] + magma_libvulkan_hardware_envs +
                   [ astro_env ] - [ astro_env ]
  }
}

group("vkreadback") {
  testonly = true
  deps = [ ":vkreadback-pkg" ]
}
