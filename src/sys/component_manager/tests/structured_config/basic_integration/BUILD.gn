# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/host.gni")
import("//build/rust/rustc_test.gni")

group("basic_integration") {
  testonly = true
  deps = [ ":basic_structured_config_integration_tests" ]
}

fuchsia_component("basic_config_receiver") {
  testonly = true
  manifest = "meta/receiver.cml"
  restricted_features = [ "structured_config" ]
  check_references = false  # we don't intend to execute this manifest
}

fuchsia_structured_config_values("basic_config_values") {
  testonly = true
  component = ":basic_config_receiver"
  values = "config/basic_config_values.json5"
}

rustc_test("basic_integration_tests") {
  testonly = true
  sources = [ "src/lib.rs" ]

  deps = [
    "//sdk/fidl/fuchsia.component.config:fuchsia.component.config-rustc",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/sys/lib/cm_rust",
  ]
}

fuchsia_unittest_package("basic_structured_config_integration_tests") {
  deps = [
    ":basic_config_receiver",
    ":basic_config_values",
    ":basic_integration_tests",
  ]
}
