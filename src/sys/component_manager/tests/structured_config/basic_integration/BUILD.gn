# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/host.gni")
import("//build/rust/rustc_test.gni")

group("basic_integration") {
  testonly = true
  deps = [
    ":basic_structured_config_tests_with_false",
    ":basic_structured_config_tests_with_true",
  ]
}

fuchsia_component("basic_config_receiver") {
  testonly = true
  manifest = "meta/receiver.cml"
  restricted_features = [ "structured_config" ]
  check_references = false  # we don't intend to execute this manifest
}

rustc_test("basic_integration_tests") {
  testonly = true
  sources = [ "src/lib.rs" ]

  deps = [
    "//sdk/fidl/fuchsia.component.config:fuchsia.component.config-rustc",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/sys/lib/cm_rust",
  ]
}

# this template gives us a clear boundary around what varies in the different test executions
template("my_flag_test") {
  values_target = "${target_name}_values_${invoker.my_flag}"

  fuchsia_structured_config_values(values_target) {
    testonly = true
    component = ":basic_config_receiver"
    values = "config/basic_config_values_with_${invoker.my_flag}.json5"
  }

  fuchsia_unittest_package(target_name) {
    deps = [
      ":$values_target",
      ":basic_config_receiver",
      ":basic_integration_tests",
    ]
  }
}

my_flag_test("basic_structured_config_tests_with_true") {
  my_flag = true
}

my_flag_test("basic_structured_config_tests_with_false") {
  my_flag = false
}
