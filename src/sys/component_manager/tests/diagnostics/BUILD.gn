# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")

rustc_binary("reporter_bin") {
  name = "reporter"
  edition = "2018"
  source_root = "reporter.rs"
  testonly = true
  deps = [
    "//examples/components/routing/fidl:echo-rustc",
    "//garnet/lib/rust/io_util",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//src/lib/diagnostics/reader/rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
  ]

  sources = [ "reporter.rs" ]
}

rustc_test("bin") {
  name = "diagnostics_integration_test"
  edition = "2018"
  source_root = "integration_test.rs"
  deps = [
    "//src/lib/fuchsia-async",
    "//src/sys/component_manager/testing:test_utils_lib",
    "//src/sys/lib/component-events",
  ]

  sources = [ "integration_test.rs" ]
}

fuchsia_component("reporter") {
  testonly = true
  deps = [ ":reporter_bin" ]
  manifest = "meta/reporter.cml"
}

fuchsia_component("component-manager-inspect") {
  testonly = true
  manifest = "meta/component-manager-inspect.cml"
}

fuchsia_unittest_package("diagnostics-integration-test") {
  package_deps_for_infra_only =
      [ "//src/sys/component_manager:component-manager" ]
  manifest = "meta/diagnostics-integration-test.cmx"
  deps = [
    ":bin",
    ":component-manager-inspect",
    ":reporter",
    "//src/diagnostics/archivist:archivist-for-embedding-v2",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":diagnostics-integration-test" ]
}
