# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")

rustc_test("collections_integration_test_bin") {
  name = "collections_integration_test"
  edition = "2018"
  source_root = "integration_test.rs"
  deps = [
    "//garnet/lib/rust/io_util",
    "//sdk/fidl/fuchsia.component:fuchsia.component-rustc",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl-rustc",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.process:fuchsia.process-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-runtime",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/component_manager/tests/fidl:components-rustc",
    "//third_party/rust_crates:anyhow",
  ]

  sources = [ "integration_test.rs" ]
}

rustc_binary("write_startup_socket_bin") {
  testonly = true
  name = "write_startup_socket"
  edition = "2018"
  source_root = "write_startup_socket.rs"
  deps = [
    "//src/lib/fuchsia-runtime",
    "//src/lib/zircon/rust:fuchsia-zircon",
  ]

  sources = [ "write_startup_socket.rs" ]
}

fuchsia_component("write_startup_socket") {
  testonly = true
  deps = [ ":write_startup_socket_bin" ]
  manifest = "meta/write_startup_socket.cml"
}

fuchsia_component("trigger_a") {
  testonly = true
  deps = [ "//src/sys/component_manager/tests:trigger_bin" ]
  manifest = "meta/trigger_a.cml"
}

fuchsia_component("trigger_b") {
  testonly = true
  deps = [ "//src/sys/component_manager/tests:trigger_bin" ]
  manifest = "meta/trigger_b.cml"
}

fuchsia_component("trigger_realm") {
  testonly = true
  manifest = "meta/trigger_realm.cml"
}

fuchsia_unittest_package("collections_integration_test") {
  manifest = "meta/collections_integration_test.cml"
  deps = [
    ":collections_integration_test_bin",
    ":trigger_a",
    ":trigger_b",
    ":trigger_realm",
    ":write_startup_socket",
    "//src/sys/component_manager/testing/echo_server",
  ]
}
