# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")

rustc_test("driver") {
  name = "isolated-ota-integration-test"
  edition = "2021"

  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.paver:fuchsia.paver_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-url",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/lib/fidl-fuchsia-pkg-ext",
    "//src/sys/pkg/lib/fuchsia-merkle",
    "//src/sys/pkg/lib/fuchsia-pkg-testing",
    "//src/sys/pkg/lib/isolated-ota",
    "//src/sys/pkg/testing/blobfs-ramdisk",
    "//src/sys/pkg/testing/mock-omaha-server",
    "//src/sys/pkg/testing/mock-paver",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:http",
    "//third_party/rust_crates:parking_lot",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:tempfile",
  ]

  sources = [ "src/lib.rs" ]
}

fuchsia_test_component("isolated-ota-integration-test") {
  testonly = true
  manifest = "meta/isolated-ota-integration-test.cml"
  deps = [ ":driver" ]
}

fuchsia_test_package("isolated-ota-integration-tests") {
  test_components = [ ":isolated-ota-integration-test" ]
  deps = [
    "//src/connectivity/network/dns:component",
    "//src/connectivity/network/netstack:component",

    # For ramdisk devices, needed to back blobfs (which in turn backs pkg-cache)
    "//src/lib/storage/ramdevice_client:ramdisk-isolated-devmgr-component",
    "//src/storage/bin/blobfs",
    "//src/storage/testing:storage_driver_test_realm",
    "//src/sys/pkg/bin/pm:pm_bin",
    "//src/sys/pkg/lib/fuchsia-pkg-testing/certs",
    "//src/sys/pkg/tests/pkg-resolver:empty-repo",
  ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  public_deps = [
    ":isolated-ota-integration-tests",
    "//src/sys/pkg/lib/isolated-swd:isolated-swd-components",
  ]
}
