# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")
import("//src/lib/isolated_devmgr/v2_component/isolated_devmgr.gni")

rustc_library("blobfs-ramdisk") {
  version = "0.1.0"
  edition = "2018"
  with_unit_tests = true

  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-runtime",
    "//src/lib/scoped_task",
    "//src/lib/storage/ramdevice_client",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/pkg/lib/blobfs",
    "//src/sys/pkg/lib/fuchsia-merkle",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:openat",
    "//third_party/rust_crates:thiserror",
  ]

  test_deps = [
    "//src/lib/storage/ramdevice_client",
    "//third_party/rust_crates:maplit",
    "//third_party/rust_crates:matches",
  ]

  sources = [
    "src/lib.rs",
    "src/test.rs",
  ]
}

fuchsia_component("unit-tests-root") {
  testonly = true
  component_name = "test_root"
  manifest = "meta/test_root.cml"
}

fuchsia_component("unit-tests-driver") {
  testonly = true
  component_name = "test_driver"
  manifest = "meta/test_driver.cml"
  deps = [
    ":blobfs-ramdisk_test",
    "//src/storage/bin/blobfs",
    "//src/storage/blobfs-corrupt",
  ]
}

isolated_devmgr_v2_component("isolated-devmgr-component") {
  package_name = "blobfs-ramdisk-unit-tests"
}

fuchsia_test_package("unit-tests-package") {
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  package_name = "blobfs-ramdisk-unit-tests"
  test_components = [ ":unit-tests-root" ]
  deps = [
    ":isolated-devmgr-component",
    ":unit-tests-driver",

    # For isolated-devmgr-component:
    "//src/storage/testing:storage_drivers",
  ]
}

group("tests") {
  testonly = true
  public_deps = [ ":unit-tests-package" ]
}
