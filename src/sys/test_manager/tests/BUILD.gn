# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")

rustc_test("test_manager_legacy_test_bin") {
  edition = "2018"
  name = "test_manager_legacy_test"
  source_root = "src/legacy_tests.rs"
  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2-rustc",
    "//sdk/fidl/fuchsia.test:fuchsia.test-rustc",
    "//sdk/fidl/fuchsia.test.manager:fuchsia.test.manager-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/test_executor/rust:test_executor",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:maplit",
    "//third_party/rust_crates:pretty_assertions",
    "//third_party/rust_crates:thiserror",
  ]

  sources = [ "src/legacy_tests.rs" ]

  inputs = [ "//src/sys/test_runners/gtest/test_data/sample_tests_golden_events_legacy.rsf" ]
}

rustc_test("test_manager_test_bin") {
  edition = "2018"
  name = "test_manager_test"
  source_root = "src/tests.rs"
  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2-rustc",
    "//sdk/fidl/fuchsia.test.manager:fuchsia.test.manager-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/test_executor/rust:test_executor",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:maplit",
    "//third_party/rust_crates:pretty_assertions",
    "//third_party/rust_crates:thiserror",
  ]

  sources = [ "src/tests.rs" ]

  inputs = [
    "//src/sys/test_runners/gtest/test_data/sample_tests_golden_events.rsf",
  ]
}

fuchsia_unittest_package("test_manager_test_legacy") {
  manifest = "meta/test_manager_legacy_test.cml"
  deps = [
    ":test_manager_legacy_test_bin",
    "test_data",
  ]

  # There's expected error logs that happen during the test such as not resolving an instance when
  # testing that we fail to execute a test with an unknown URL.
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fuchsia_unittest_package("test_manager_test") {
  manifest = "meta/test_manager_test.cml"
  deps = [
    ":test_manager_test_bin",
    "test_data",
  ]

  # There's expected error logs that happen during the test such as not resolving an instance when
  # testing that we fail to execute a test with an unknown URL.
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

rustc_test("debug_data_test_bin") {
  testonly = true
  name = "debug_data_test"
  source_root = "src/debug_data_test.rs"

  deps = [
    "//sdk/fidl/fuchsia.debugdata:fuchsia.debugdata-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon-status",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:matches",
    "//third_party/rust_crates:thiserror",
  ]

  sources = [ "src/debug_data_test.rs" ]
}

fuchsia_component("debug_data_test") {
  testonly = true
  manifest = "meta/debug_data_test.cml"
  deps = [ ":debug_data_test_bin" ]
}

fuchsia_test_package("debug-data-test") {
  test_components = [ ":debug_data_test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":debug-data-test",
    ":test_manager_test",
    ":test_manager_test_legacy",
    "diagnostics_tests:tests",
    "//examples/tests:example-tests",
    "//src/sys/test_manager",
    "//src/sys/test_runners/gtest/test_data:gtest-runner-example-tests",
  ]
}
