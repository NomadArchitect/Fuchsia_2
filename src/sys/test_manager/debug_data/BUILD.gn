# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

source_set("lib") {
  testonly = true

  sources = [
    "abstract_data_processor.h",
    "cache.h",
    "common.h",
    "data_processor.cc",
    "data_processor.h",
    "data_processor_fidl.cc",
    "data_processor_fidl.h",
    "debug_data.cc",
    "debug_data.h",
    "event_stream.cc",
    "event_stream.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.debugdata",
    "//sdk/fidl/fuchsia.sys2",
    "//sdk/fidl/fuchsia.test.internal",
    "//sdk/lib/fdio",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl",
    "//src/lib/json_parser",
    "//src/sys/test_manager/fidl/fuchsia.test.debug",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/debugdata",
  ]
}

rustc_binary("rust_bin") {
  name = "debug_data_rust_bin"
  with_unit_tests = true
  edition = "2018"
  source_root = "src/main.rs"

  deps = [
    "//garnet/lib/rust/files_async",
    "//garnet/lib/rust/io_util",
    "//sdk/fidl/fuchsia.debugdata:fuchsia.debugdata-rustc",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2-rustc",
    "//sdk/fidl/fuchsia.test.internal:fuchsia.test.internal-rustc",
    "//sdk/fidl/fuchsia.test.manager:fuchsia.test.manager-rustc",
    "//src/lib/fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/lib/moniker",
    "//src/sys/test_manager/fidl/fuchsia.test.debug:fuchsia.test.debug-rustc",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:parking_lot",
  ]

  test_deps = [
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:maplit",
    "//third_party/rust_crates:tempfile",
  ]

  sources = [
    "src/data_processor.rs",
    "src/debug_data_server.rs",
    "src/debug_data_set.rs",
    "src/iterator.rs",
    "src/main.rs",
    "src/message.rs",
  ]
}

fuchsia_component("debug_data_rust") {
  testonly = true
  deps = [ ":rust_bin" ]
  manifest = "meta/debug_data_rust.cml"
}

fuchsia_unittest_package("debug-data-rust-unittests") {
  deps = [ ":rust_bin_test" ]
  manifest = "meta/debug_data_rust_unittests.cml"
}

executable("bin") {
  testonly = true
  output_name = "debug_data"
  sources = [ "main.cc" ]

  deps = [
    ":lib",
    "//sdk/fidl/fuchsia.sys2",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

executable("debug_data_processor_bin") {
  testonly = true
  output_name = "debug_data_processor"
  sources = [ "processor_main.cc" ]

  deps = [
    ":lib",
    "//sdk/fidl/fuchsia.sys2",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/sys/test_manager/fidl/fuchsia.test.debug",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_component("debug_data") {
  testonly = true
  deps = [ ":bin" ]
  manifest = "meta/debug_data.cml"
}

fuchsia_component("debug_data_processor") {
  testonly = true
  deps = [ ":debug_data_processor_bin" ]
  manifest = "meta/debug_data_processor.cml"
}

executable("test_bin") {
  testonly = true
  output_name = "debug_data_unittests"

  sources = [
    "cache_unittests.cc",
    "data_processor_fidl_unittests.cc",
    "data_processor_unittests.cc",
    "debug_data_unittests.cc",
    "event_stream_unittests.cc",
    "test_data_processor.h",
  ]

  deps = [
    ":lib",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_component("debug_data_unittests_component") {
  testonly = true
  component_name = "debug_data_unittests"
  deps = [ ":test_bin" ]
  manifest = "meta/debug_data_unittests.cml"
}

fuchsia_test_package("debug_data_unittests") {
  test_components = [ ":debug_data_unittests_component" ]
}

group("tests") {
  testonly = true
  deps = [
    ":debug-data-rust-unittests",
    ":debug_data_unittests",
  ]
}
