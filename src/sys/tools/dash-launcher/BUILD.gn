# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  output_name = "dash-launcher"
  edition = "2018"
  with_unit_tests = true

  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component-rustc",
    "//sdk/fidl/fuchsia.dash:fuchsia.dash-rustc",
    "//sdk/fidl/fuchsia.device:fuchsia.device-rustc",
    "//sdk/fidl/fuchsia.hardware.pty:fuchsia.hardware.pty-rustc",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.process:fuchsia.process-rustc",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2-rustc",
    "//src/lib/diagnostics/inspect/runtime/rust",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/lib/fuchsia-runtime",
    "//src/lib/storage/vfs/rust:vfs",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/lib/library_loader",
    "//src/sys/lib/moniker",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]

  sources = [
    "src/launch.rs",
    "src/main.rs",
    "src/socket.rs",
  ]
}

fuchsia_component("component") {
  component_name = "dash_launcher"
  manifest = "meta/dash_launcher.cml"
  deps = [ ":bin" ]
}

fuchsia_unittest_package("dash-launcher-unittests") {
  manifest = "meta/dash_launcher_unittests.cml"
  deps = [
    ":bin_test",
    "//src/bringup/bin/ptysvc:component",
  ]
}

fuchsia_package("dash-launcher") {
  deps = [
    ":component",
    "//src/sys/tools/chroot",
    "//third_party/sbase:bins",
    "//zircon/third_party/uapp/dash",
  ]
  visibility = [ "//bundles/assembly/*" ]
}

group("tests") {
  testonly = true
  deps = [
    ":dash-launcher-unittests",
    "integration_test",
  ]
}
