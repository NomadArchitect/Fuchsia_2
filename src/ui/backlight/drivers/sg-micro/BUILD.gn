# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")
import("//build/test.gni")

driver_bind_rules("sgm37603a-bind") {
  rules = "sgm37603a.bind"
  header_output = "sgm37603a-bind.h"
  tests = "sgm37603a-bind-test.json"
  deps = [ "//src/devices/bind/fuchsia.platform" ]
}

driver_module("sg-micro-driver") {
  output_name = "sg-micro"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "sgm37603a.cc" ]
  deps = [
    ":sgm37603a-bind",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.i2c:fuchsia.hardware.i2c_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.backlight:fuchsia.hardware.backlight_llcpp",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/i2c/lib/device-protocol-i2c",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fidl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_driver_component("sg-micro") {
  component_name = "sg-micro"
  deps = [ ":sg-micro-driver" ]
}

test("sgm37603a-test-bin") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "sgm37603a-test-bin"
  sources = [
    "sgm37603a-test.cc",
    "sgm37603a.cc",
  ]
  deps = [
    ":sgm37603a-bind",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.backlight:fuchsia.hardware.backlight_llcpp",
    "//sdk/lib/fdio",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/i2c/lib/device-protocol-i2c",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/i2c/testing/mock-i2c",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fidl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("sgm37603a-test") {
  deps = [ ":sgm37603a-test-bin" ]
}

group("tests") {
  testonly = true
  deps = [
    ":sgm37603a-bind_test",
    ":sgm37603a-test",
  ]
}
