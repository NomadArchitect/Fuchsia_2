# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("tests") {
  testonly = true
  public_deps = [ ":view_tests" ]
}

executable("view_tests") {
  output_name = "view_tests"

  testonly = true

  sources = [
    "a11y_view_semantics_test.cc",
    "gfx_accessibility_view_test.cc",
    "view_manager_test.cc",
    "view_wrapper_test.cc",
  ]
  public_deps = [ "//sdk/fidl/fuchsia.accessibility.semantics" ]
  deps = [
    "//sdk/fidl/fuchsia.accessibility",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:unit",
    "//sdk/lib/syslog/cpp",
    "//sdk/lib/vfs/cpp",
    "//src/lib/fsl",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/json_parser",
    "//src/lib/testing/loop_fixture",
    "//src/ui/a11y/bin/a11y_manager/tests/util",
    "//src/ui/a11y/lib/annotation",
    "//src/ui/a11y/lib/annotation/tests/mocks",
    "//src/ui/a11y/lib/semantics",
    "//src/ui/a11y/lib/semantics/tests/mocks",
    "//src/ui/a11y/lib/view",
    "//src/ui/a11y/lib/view/tests/mocks",
    "//src/ui/input/lib/injector/tests/mocks",
    "//third_party/glm",
    "//third_party/googletest:gmock",
    "//third_party/rapidjson",
  ]
}

executable("gfx-a11y-view-test-bin") {
  testonly = true
  sources = [ "gfx_a11y_view_test.cc" ]
  output_name = "gfx-a11y-view-test"
  deps = [
    "//sdk/fidl/fuchsia.accessibility.semantics",
    "//sdk/fidl/fuchsia.logger",
    "//sdk/fidl/fuchsia.tracing.provider",
    "//sdk/fidl/fuchsia.ui.accessibility.view",
    "//sdk/fidl/fuchsia.ui.scenic",
    "//sdk/lib/sys/component/cpp/testing:cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/ui/testing/ui_test_manager",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_test_component("gfx-a11y-view-test-component") {
  testonly = true
  manifest = "meta/gfx-a11y-view-test.cml"
  deps = [ ":gfx-a11y-view-test-bin" ]
}

fuchsia_test_package("gfx-a11y-view-test") {
  test_components = [ ":gfx-a11y-view-test-component" ]
  test_specs = {
    log_settings = {
      # TODO(fxbug.dev/93081): Investigate flakes and remove allowing ERROR severity.
      max_severity = "ERROR"
    }
    environments = [
      {
        dimensions = {
          # Ensure the device has Vulkan.
          device_type = "AEMU"
        }
      },
    ]
  }
}
