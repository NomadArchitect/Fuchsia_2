# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/testing/environments.gni")

group("unit") {
  testonly = true
  public_deps = [ ":semantics_tests" ]
}

group("integration") {
  testonly = true
  public_deps = [
    ":semantics-integration-tests",
    "//src/ui/a11y/bin/demo:a11y-demo",
    "//src/ui/bin/hardware_display_controller_provider:fake-hardware-display-controller-provider",
  ]

  data_deps = [
    "//garnet/packages/prod:scenic",
    "//src/chromium",
    "//src/ui/a11y/bin/demo:a11y-demo",
  ]
}

executable("semantics_tests") {
  output_name = "semantics_tests"
  testonly = true
  sources = [
    "a11y_semantics_event_manager_test.cc",
    "semantic_tree_parser.cc",
    "semantic_tree_parser.h",
    "semantic_tree_parser_test.cc",
    "semantic_tree_service_unittest.cc",
    "tree_unittest.cc",
  ]
  public_deps = [ "//sdk/fidl/fuchsia.accessibility.semantics" ]
  deps = [
    "//garnet/public/lib/gtest",
    "//garnet/public/lib/json",
    "//sdk/fidl/fuchsia.accessibility",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/inspect/testing/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:unit",
    "//sdk/lib/syslog/cpp",
    "//sdk/lib/ui/scenic/cpp",
    "//sdk/lib/vfs/cpp",
    "//src/lib/files",
    "//src/lib/fsl",
    "//src/lib/fxl/test:gtest_main",
    "//src/ui/a11y/bin/a11y_manager/tests/util",
    "//src/ui/a11y/lib/screen_reader/util",
    "//src/ui/a11y/lib/semantics",
    "//src/ui/a11y/lib/semantics/tests/mocks",
    "//third_party/googletest:gmock",
    "//third_party/rapidjson",
  ]

  # This is a dependency of the semantic tree to perform certain matrix transform operations.
  include_dirs = [ "//third_party/glm" ]
}

fuchsia_component("flutter_semantics_tests") {
  testonly = true
  deps = [ ":flutter_semantics_tests_bin" ]
  manifest = "meta/flutter_semantics_tests.cmx"
}

fuchsia_component("web_semantics_tests") {
  testonly = true
  deps = [ ":web_semantics_tests_bin" ]
  manifest = "meta/web_semantics_tests.cmx"
}

resource("big_list") {
  sources = [ "testdata/big_list.html" ]
  outputs = [ "data/big_list.html" ]
}

resource("dynamic_button") {
  sources = [ "testdata/dynamic_button.html" ]
  outputs = [ "data/dynamic_button.html" ]
}

resource("static") {
  sources = [ "testdata/static.html" ]
  outputs = [ "data/static.html" ]
}

fuchsia_test_package("semantics-integration-tests") {
  test_components = [ ":web_semantics_tests" ]

  # TODO(https://fxbug.dev/73200): Re-enable this test in coverage build when it
  # passes consistently.
  if (!is_coverage) {
    test_components += [ ":flutter_semantics_tests" ]
  }

  deps = [
    ":big_list",
    ":dynamic_button",
    ":static",
  ]

  test_specs = {
    environments = with_display_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

executable("flutter_semantics_tests_bin") {
  testonly = true

  sources = [ "flutter_semantics_tests.cc" ]

  deps = [
    ":semantics_integration_test_fixture",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/ui/base_view",
    "//src/ui/testing/views",
    "//third_party/googletest:gtest",
  ]
}

executable("web_semantics_tests_bin") {
  testonly = true
  sources = [ "web_semantics_tests.cc" ]
  deps = [
    ":semantics_integration_test_fixture",
    "//sdk/lib/syslog/cpp",
    "//src/chromium/web_engine/meta/shards:web_engine_feature_network",
    "//src/chromium/web_engine/meta/shards:web_engine_feature_vulkan",
    "//src/chromium/web_engine/meta/shards:web_engine_view",
    "//src/chromium/web_runner_tests:mock_server",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/ui/base_view",
    "//src/ui/a11y/lib/view",
    "//src/ui/testing/views",
    "//third_party/googletest:gtest",
  ]
}

source_set("semantics_integration_test_fixture") {
  testonly = true
  sources = [
    "semantics_integration_test_fixture.cc",
    "semantics_integration_test_fixture.h",
  ]
  public_deps = [
    # TODO(fxbug.dev/82655): Remove this after migrating to RealmBuilder.
    "//sdk/fidl/fuchsia.ui.lifecycle",
    "//sdk/fidl/fuchsia.ui.scenic",
    "//sdk/fidl/fuchsia.ui.views",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:integration_fixture",
    "//src/ui/a11y/lib/annotation/tests/mocks",
    "//src/ui/a11y/lib/semantics",
    "//src/ui/a11y/lib/semantics/util",
    "//src/ui/a11y/lib/view",
    "//src/ui/a11y/lib/view/tests/mocks",
  ]
  deps = [
    "//sdk/fidl/fuchsia.ui.policy",
    "//sdk/fidl/fuchsia.ui.scenic",
    "//sdk/lib/fdio",
    "//sdk/lib/sys/cpp/testing:unit",
    "//sdk/lib/syslog/cpp",
    "//sdk/lib/ui/scenic/cpp",
    "//zircon/public/lib/zx",
  ]
  data_deps = [
    "//src/fonts:downstream_test_deps($default_toolchain)",
    "//src/intl/intl_services:pkg_small",
  ]
}
