# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_binary.gni")
import("//src/sys/build/components.gni")

group("tests") {
  testonly = true

  deps = [ ":default_hardware_ime_tests" ]
}

rustc_binary("bin") {
  name = "default_hardware_ime"
  with_unit_tests = true
  edition = "2018"

  deps = [
    "//sdk/fidl/fuchsia.ui.input:fuchsia.ui.input-rustc",
    "//sdk/fidl/fuchsia.ui.input2:fuchsia.ui.input2-rustc",
    "//sdk/fidl/fuchsia.ui.text:fuchsia.ui.text-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/ui/lib/text",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:thiserror",
  ]

  sources = [
    "src/keymap.rs",
    "src/main.rs",
  ]
}

resource("assets") {
  sources = [
    "assets/us-legacy.json",
    "assets/us.json",
  ]
  outputs = [ "data/{{source_file_part}}" ]
}

fuchsia_package_with_single_component("default_hardware_ime") {
  manifest = "meta/default_hardware_ime.cmx"
  deps = [
    ":assets",
    ":bin",
  ]
}

fuchsia_unittest_package("default_hardware_ime_tests") {
  deps = [
    ":assets",
    ":bin_test",
  ]
}
