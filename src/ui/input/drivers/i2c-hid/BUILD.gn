# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")

bind_rules("i2c_hid_bind") {
  rules = "i2c_hid.bind"
  output = "i2c_hid_bind.h"
  tests = "tests.json"
  deps = [ "//src/devices/bind/fuchsia.i2c" ]
}

driver_module("i2c-hid") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "i2c-hid.cc" ]
  deps = [
    ":i2c_hid_bind",
    "//sdk/banjo/fuchsia.hardware.hidbus:fuchsia.hardware.hidbus_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.i2c",
    "//src/devices/i2c/lib/device-protocol-i2c",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/sync",
    "//zircon/system/ulib/hid",
    "//zircon/system/ulib/trace:trace-driver",
  ]
}

test("i2c-hid-test") {
  configs += [
    "//build/unification/config:zircon-migrated",
    "//build/config:all_source",
  ]
  sources = [
    "i2c-hid-test.cc",
    "i2c-hid.cc",
  ]
  deps = [
    ":i2c_hid_bind",
    "//sdk/banjo/fuchsia.hardware.hidbus:fuchsia.hardware.hidbus_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.i2c",
    "//src/devices/i2c/lib/device-protocol-i2c",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/i2c/testing/fake-i2c",
    "//src/devices/testing/fake_ddk",
    "//src/lib/ddk",
    "//src/ui/input/testing/fake-hidbus-ifc",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/hid",
    "//zircon/system/ulib/trace:trace-driver",
  ]
}

fuchsia_unittest_package("i2c-hid-test-package") {
  package_name = "i2c-hid-test"
  deps = [ ":i2c-hid-test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":i2c-hid-test-package",
    ":i2c_hid_bind_test",
  ]
}
