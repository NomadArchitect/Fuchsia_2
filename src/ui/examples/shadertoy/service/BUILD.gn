# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//src/lib/vulkan/compact_image.gni")
import("//src/lib/vulkan/layers.gni")

executable("bin") {
  output_name = "shadertoy_service"

  sources = [
    "app.cc",
    "app.h",
    "compiler.cc",
    "compiler.h",
    "imagepipe_shadertoy.cc",
    "imagepipe_shadertoy.h",
    "main.cc",
    "pipeline.cc",
    "pipeline.h",
    "renderer.cc",
    "renderer.h",
    "shadertoy_impl.cc",
    "shadertoy_impl.h",
    "shadertoy_state.cc",
    "shadertoy_state.h",
    "view_shadertoy.cc",
    "view_shadertoy.h",
  ]

  deps = [
    "services",
    "//sdk/fidl/fuchsia.ui.gfx",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//src/lib/vulkan",
    "//src/ui/examples/escher/common",

    # Shadertoy service should always use glslang.
    "//src/ui/lib/escher:escher_with_glslang",
    "//src/ui/lib/glm_workaround",
    "//third_party/shaderc:libshaderc",
    "//zircon/public/lib/fit",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/trace-provider:trace-provider-with-fdio",
  ]

  include_dirs = [
    "//lib",
    "//src/ui/lib/escher",
  ]

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

group("service") {
  deps = [ ":shadertoy_service" ]
}

package("shadertoy_service") {
  deps = [ ":bin" ]

  binaries = [
    {
      name = "shadertoy_service"
    },
  ]
  meta = [
    {
      path = rebase_path("meta/shadertoy_service.cmx")
      dest = "shadertoy_service.cmx"
    },
  ]

  public_deps = vulkan_validation_layers.public_deps
  loadable_modules = vulkan_validation_layers.loadable_modules
  resources = vulkan_validation_layers.resources
}
