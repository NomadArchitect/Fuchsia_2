# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")
import("//build/test.gni")

driver_bind_rules("aml_light_bind") {
  rules = "aml_light.bind"
  header_output = "aml_light_bind.h"
  bind_output = "aml-light.bindbc"
  tests = "bind_tests.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_module("aml-light-driver") {
  output_name = "aml-light"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "aml-light.cc" ]
  deps = [
    ":aml_light_bind",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.pwm:fuchsia.hardware.pwm_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.light:fuchsia.hardware.light_llcpp",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/ddktl",
    "//zircon/public/lib/fidl",
    "//zircon/public/lib/sync",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver_component("aml-light") {
  component_name = "aml-light"
  deps = [ ":aml-light-driver" ]
}

test("aml-light-test") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "aml-light-test"
  sources = [
    "aml-light-test.cc",
    "aml-light.cc",
  ]
  deps = [
    ":aml_light_bind",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp_mock",
    "//sdk/banjo/fuchsia.hardware.pwm:fuchsia.hardware.pwm_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.pwm:fuchsia.hardware.pwm_banjo_cpp_mock",
    "//sdk/fidl/fuchsia.hardware.light:fuchsia.hardware.light_llcpp",
    "//src/devices/lib/amlogic",
    "//src/devices/testing/no_ddk",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/ddktl",
    "//zircon/public/lib/fidl",
    "//zircon/public/lib/mock-function",
    "//zircon/public/lib/sync",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

fuchsia_unittest_package("aml-light-test-package") {
  package_name = "aml-light-test"
  deps = [ ":aml-light-test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":aml-light-test-package",
    ":aml_light_bind_test",
  ]
}
