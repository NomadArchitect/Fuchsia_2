# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

group("integration_tests") {
  testonly = true
  public_deps = [ ":gfx_integration_tests" ]
}

source_set("common_deps") {
  testonly = true
  sources = [
    "utils.cc",
    "utils.h",
  ]
  public_deps = [
    "//sdk/lib/sys/cpp/testing:integration",
    "//sdk/lib/syslog/cpp",
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//third_party/googletest:gtest",
  ]
  deps = [ "//garnet/public/lib/fostr/fidl/fuchsia.ui.input:fuchsia.ui.input" ]
}

executable("gfx_focus_integration_test_bin") {
  testonly = true
  sources = [ "gfx_focus_integration_test.cc" ]
  deps = [
    ":common_deps",
    "//sdk/fidl/fuchsia.ui.focus",
    "//src/ui/scenic/lib/utils",
  ]
}

fuchsia_test_component("gfx_focus_integration_test") {
  testonly = true
  component_name = "gfx_focus_integration_test"
  manifest = "meta/gfx_focus_integration_test.cmx"
  deps = [ ":gfx_focus_integration_test_bin" ]
}

executable("gfx_legacy_coordinate_transform_test_bin") {
  testonly = true
  sources = [ "gfx_legacy_coordinate_transform_test.cc" ]
  deps = [
    ":common_deps",
    "//src/ui/lib/glm_workaround",
  ]
}

fuchsia_test_component("gfx_legacy_coordinate_transform_test") {
  testonly = true
  component_name = "gfx_legacy_coordinate_transform_test"
  manifest = "meta/gfx_legacy_coordinate_transform_test.cmx"
  deps = [ ":gfx_legacy_coordinate_transform_test_bin" ]
}

executable("gfx_view_ref_installed_integration_test_bin") {
  testonly = true
  sources = [ "gfx_view_ref_installed_integration_test.cc" ]
  deps = [
    ":common_deps",
    "//sdk/fidl/fuchsia.ui.views",
  ]
}

fuchsia_test_component("gfx_view_ref_installed_integration_test") {
  testonly = true
  component_name = "gfx_view_ref_installed_integration_test"
  manifest = "meta/gfx_view_ref_installed_integration_test.cmx"
  deps = [ ":gfx_view_ref_installed_integration_test_bin" ]
}

fuchsia_test_package("gfx_integration_tests") {
  test_components = [
    ":gfx_focus_integration_test",
    ":gfx_legacy_coordinate_transform_test",
    ":gfx_view_ref_installed_integration_test",
  ]
  test_specs = {
    environments = [ aemu_env ] + magma_libvulkan_hardware_envs
  }
}
