# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")
import("//src/diagnostics/archivist/build/pipeline.gni")

rustc_binary("stub_inspect_component_bin") {
  output_name = "stub_inspect_component"
  edition = "2018"
  source_root = "components/stub_inspect_component.rs"
  deps = [
    "//src/lib/diagnostics/inspect/runtime/rust",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]

  sources = [ "components/stub_inspect_component.rs" ]
}

fuchsia_component("stub_inspect_component") {
  deps = [ ":stub_inspect_component_bin" ]
  manifest = "meta/stub_inspect_component.cml"
}

fuchsia_component("component_with_children") {
  deps = [ ":stub_inspect_component_bin" ]
  manifest = "meta/component_with_children.cml"
}

rustc_test("integration_tests_bin") {
  output_name = "archivist_integration_tests"
  edition = "2018"
  deps = [
    "//garnet/lib/rust/files_async",
    "//garnet/lib/rust/io_util",
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics-rustc",
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics-rustc",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.io2:fuchsia.io2-rustc",
    "//src/lib/diagnostics/data/rust",
    "//src/lib/diagnostics/hierarchy/rust",
    "//src/lib/diagnostics/reader/rust",
    "//src/lib/diagnostics/testing/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component-test",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/lib/cm_rust",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:difference",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:lazy_static",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:serde_json",
  ]

  sources = [
    "src/constants.rs",
    "src/lib.rs",
    "src/reader.rs",
    "src/test_topology.rs",
  ]
  inputs = [
    "test_data/unified_reader_all_golden.json",
    "test_data/feedback_reader_all_golden.json",
    "test_data/unified_reader_full_filter_golden.json",
    "test_data/feedback_reader_nonoverlapping_selectors_golden.json",
    "test_data/unified_reader_single_value_golden.json",
    "test_data/feedback_reader_single_value_golden.json",
  ]
}

fuchsia_component("archivist_for_integration") {
  component_name = "archivist"
  testonly = true
  deps = [ "//src/diagnostics/archivist:bin" ]
  manifest = "meta/archivist_for_integration.cml"
}

fuchsia_component("archivist_integration_tests") {
  testonly = true
  deps = [ ":integration_tests_bin" ]
  manifest = "meta/archivist_integration_tests.cml"
}

resource("test_archivist_config") {
  sources = [ "configs/archivist_config.json" ]
  outputs = [ "data/config/archivist_config.json" ]
}

resource("filter_feedback") {
  sources = [ "configs/static_selectors.cfg" ]
  outputs = [ "data/config/pipelines/feedback/static_selectors.cfg" ]
}

resource("do_not_filter_feedback") {
  sources = [ "configs/DISABLE_FILTERING.txt" ]
  outputs = [ "data/config/pipelines/feedback/DISABLE_FILTERING.txt" ]
}

fuchsia_test_package("archivist-integration-tests-v2") {
  test_components = [ ":archivist_integration_tests" ]
  deps = [
    ":archivist_for_integration",
    ":component_with_children",
    ":stub_inspect_component",
    ":test_archivist_config",
    "//src/diagnostics/iquery/test/test_component",
  ]
}

fuchsia_package("archivist-with-feedback-filtering") {
  testonly = true
  deps = [
    ":archivist_for_integration",
    ":filter_feedback",
    ":test_archivist_config",
  ]
}

fuchsia_package("archivist-with-feedback-filtering-disabled") {
  testonly = true
  deps = [
    ":archivist_for_integration",
    ":do_not_filter_feedback",
    ":test_archivist_config",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":archivist-integration-tests-v2",
    ":archivist-with-feedback-filtering",
    ":archivist-with-feedback-filtering-disabled",
  ]
}
