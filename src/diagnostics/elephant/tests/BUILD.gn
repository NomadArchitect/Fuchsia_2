# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/package.gni")
import("//build/rust/rustc_library.gni")
import("//build/test/test_package.gni")
import("//src/sys/build/components.gni")

# Integration test binary for Elephant service.
rustc_test("test-bin") {
  testonly = true
  name = "canonical-elephant-test-bin"
  deps = [
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
    "//src/diagnostics/elephant/fidl:persist-rustc",
    "//src/diagnostics/sampler/tests/fidl-for-test:fuchsia.samplertestcontroller-rustc",
    "//src/lib/diagnostics/reader/rust",
    "//src/lib/diagnostics/testing/rust",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:lazy_static",
    "//third_party/rust_crates:serde_json",
  ]

  data_deps = [ "//src/diagnostics/elephant:lib" ]

  source_root = "integration.rs"
  sources = [ "integration.rs" ]
}

# Test component for elephant service integration test.
fuchsia_component("canonical-elephant-test") {
  testonly = true
  manifest = "meta/canonical-elephant-test.cmx"
  deps = [
    ":test-bin",
    "//src/diagnostics/elephant:elephant-v1",
    "//src/diagnostics/iquery/test/test_component",
  ]
}

# Config-data for the integration test. Whatever is in this file should map to the
# test component you are using to perturbe inspect state.
config_data("test-config") {
  for_pkg = "elephant-integration-tests"
  sources = [ "configs/test_config.persist" ]

  # You need to map this config to whereever/whatever elephant is expecting it as.
  outputs = [ "test_config.persist" ]
}

fuchsia_test_package("elephant-integration-tests") {
  test_components = [ ":canonical-elephant-test" ]
  deps = [ ":test-config" ]
}
