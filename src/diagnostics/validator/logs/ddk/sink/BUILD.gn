# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//src/lib/isolated_devmgr/isolated_devmgr.gni")

rustc_binary("bin") {
  output_name = "accessor_validator"
  edition = "2018"
  with_unit_tests = true
  deps = [
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics-rustc",
    "//sdk/fidl/fuchsia.diagnostics.stream:fuchsia.diagnostics.stream-rustc",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger-rustc",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
    "//src/diagnostics/validator/logs/sink/fidl:sink-puppet-rustc",
    "//src/lib/diagnostics/log/encoding/rust",
    "//src/lib/diagnostics/reader/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]

  sources = [ "src/main.rs" ]
}

isolated_devmgr_component("log-test-devmgr") {
  args = [
    "--svc_name=fuchsia.validate.logs.IsolatedDevmgr",
    "--device_vid_pid_did=11:3:0",
  ]

  deps = [
    "//src/devices/tests/sysdev",
    "//src/diagnostics/validator/logs/ddk/log-test-driver",
  ]
}

fuchsia_unittest_package("accessor-validator-tests") {
  deps = [ ":bin_test" ]
}

### C++ ###

fuchsia_component("accessor-validator-for-ddk") {
  manifest = "meta/validator-ddk.cmx"
  deps = [ ":bin" ]
}

fuchsia_test_package("accessor-validator-ddk") {
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  deps = [
    ":log-test-devmgr",
    "puppet/cpp:component",
  ]
  test_components = [ ":accessor-validator-for-ddk" ]
}

group("tests") {
  testonly = true

  deps = [
    ":accessor-validator-ddk",
    ":accessor-validator-tests",
  ]
}
