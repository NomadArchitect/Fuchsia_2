# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

static_library("perfmon") {
  sources = [
    "buffer_reader.cc",
    "buffer_reader.h",
    "event-registry.cc",
    "event-registry.h",
    "events.cc",
    "events.h",
    "file_reader.cc",
    "file_reader.h",
    "reader.cc",
    "reader.h",
    "records.cc",
    "records.h",
    "types.cc",
    "types.h",
    "writer.h",
  ]

  deps = []

  public_deps = [
    "//src/lib/fxl",
    "//zircon/system/ulib/zircon-internal",
  ]

  if (is_fuchsia) {
    sources += [
      "config.cc",
      "config.h",
      "config_impl.h",
      "controller.cc",
      "controller.h",
      "controller_impl.cc",
      "controller_impl.h",
      "device_reader.cc",
      "device_reader.h",
      "properties.cc",
      "properties.h",
      "properties_impl.h",
    ]

    if (current_cpu == "arm64") {
      sources += [ "arm64-events.cc" ]
    }
    if (current_cpu == "x64") {
      sources += [ "intel-events.cc" ]
    }

    deps += [
      "//sdk/lib/fdio",
      "//zircon/system/ulib/fbl",
      "//zircon/system/ulib/fzl",
    ]

    public_deps += [
      "//sdk/fidl/fuchsia.perfmon.cpu",
      "//zircon/system/public",
      "//zircon/system/ulib/zx",
      "//zircon/system/ulib/zxc",
    ]
  } else {
    sources += [
      "arm64-events.cc",
      "intel-events.cc",
    ]
  }

  public_configs = [
    # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
    "//build/config:Wno-conversion",
  ]
}

executable("perfmon_tests_app") {
  testonly = true
  output_name = "perfmon_tests"

  sources = [ "config_unittest.cc" ]

  deps = [
    ":perfmon",
    "//sdk/lib/fdio",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/zx",
  ]

  deps += [ "//src/zircon/lib/zircon" ]
}

fuchsia_unittest_package("perfmon_tests") {
  deps = [ ":perfmon_tests_app" ]
}

executable("perfmon_integration_tests_bin") {
  testonly = true
  output_name = "perfmon_integration_tests"

  sources = [ "perfmon_integration_tests.cc" ]

  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//src/performance/lib/perfmon",
    "//third_party/googletest:gtest",
  ]
}

fuchsia_test_component("perfmon_integration_tests_component") {
  manifest = "meta/perfmon_integration_tests.cml"
  deps = [ ":perfmon_integration_tests_bin" ]
}

fuchsia_test_package("perfmon_integration_tests") {
  test_components = [ ":perfmon_integration_tests_component" ]
  test_specs = {
    # Each tests needs full pmu ownership.
    parallel = 1

    # Run this both on real hardware to make sure it works and emus to make
    # sure we fail gracefully.
    environments = all_fuchsia_envs

    # On devices that don't have a pmu, we check that starting perfmon fails
    # gracefully.
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  public_deps = [
    ":perfmon_integration_tests",
    ":perfmon_tests",
  ]
}
