# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/dist/component_manifest_resources.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//src/bringup/bin/virtcon2/virtcon_args.gni")

group("bootfs") {
  deps = [
    ":bin",
    ":meta",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":virtual_console_tests" ]
}

component_manifest_resources("meta") {
  sources = [ "meta/virtual-console.cml" ]
}

rustc_library("lib") {
  name = "virtual_console_lib"
  edition = "2018"
  with_unit_tests = true
  sources = [
    "src/app.rs",
    "src/args.rs",
    "src/colors.rs",
    "src/lib.rs",
    "src/session_manager.rs",
    "src/terminal.rs",
    "src/text_grid.rs",
    "src/view.rs",
  ]
  deps = [
    "//garnet/bin/terminal/third_party/term-model",
    "//sdk/fidl/fuchsia.boot:fuchsia.boot-rustc",
    "//sdk/fidl/fuchsia.hardware.display:fuchsia.hardware.display-rustc",
    "//sdk/fidl/fuchsia.hardware.pty:fuchsia.hardware.pty-rustc",
    "//sdk/fidl/fuchsia.virtualconsole:fuchsia.virtualconsole-rustc",
    "//src/graphics/lib/compute/rive-rs",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/pty",
    "//src/lib/trace/rust:trace-provider",
    "//src/lib/ui/carnelian",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/lib/stdout-to-debuglog/rust",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:euclid",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:libc",
  ]
}

resource("font") {
  sources = [ virtcon_font_path ]
  outputs = [ "data/font.ttf" ]
}

resource("logo") {
  sources = [ virtcon_logo_path ]
  outputs = [ "data/logo.riv" ]
}

rustc_binary("bin") {
  name = "virtual_console"
  edition = "2018"
  source_root = "src/main.rs"
  sources = [ "src/main.rs" ]
  deps = [
    ":font",
    ":lib",
    ":logo",
    "//sdk/fidl/fuchsia.boot:fuchsia.boot-rustc",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/trace/rust:trace-provider",
    "//src/lib/ui/carnelian",
    "//src/lib/ui/carnelian:virtcon_config",
    "//src/sys/lib/stdout-to-debuglog/rust",
    "//third_party/rust_crates:anyhow",
  ]
}

resource("font_test") {
  sources = [ "data/test.ttf" ]
  outputs = [ "data/font.ttf" ]
}

resource("logo_test") {
  sources = [ "data/test.riv" ]
  outputs = [ "data/logo.riv" ]
}

fuchsia_test_component("virtual_console_tests_component") {
  deps = [
    ":font_test",
    ":lib_test",
    ":logo_test",
  ]
  manifest = "meta/virtual-console-tests.cmx"
}

fuchsia_test_package("virtual_console_tests") {
  test_components = [ ":virtual_console_tests_component" ]
}
