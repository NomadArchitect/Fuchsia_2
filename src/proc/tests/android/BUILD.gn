# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//src/proc/tests/starnix_test_package.gni")

group("tests") {
  testonly = true
  if (target_cpu == "x64") {
    deps = [
      ":test_android_distro",
      ":test_android_distro_with_error_logs",
      "benchmarks",
      "gvisor",
      "wayland",
    ]
  }
}

resource("binder_smoke_test_bin") {
  sources = [
    "//prebuilt/starnix/tests/syscalls/linux-amd64/android/binder_smoke_test",
  ]
  outputs = [ "data/tests/binder_smoke_test" ]
}

fuchsia_test_component("binder_smoke_test") {
  manifest = "meta/binder_smoke_test.cml"
  deps = [
    ":binder_smoke_test_bin",
    "//src/proc/tests:starnix_test",
  ]
}

resource("binder_test_script") {
  sources = [ "binderLibTestLauncher.sh" ]
  outputs = [ "data/tests/binderLibTestLauncher.sh" ]
}

fuchsia_test_component("binder_test") {
  manifest = "meta/binder_test.cml"
  deps = [
    ":binder_test_script",
    "//src/proc/tests:starnix_test",
  ]
}

fuchsia_test_component("libutils_test") {
  manifest = "meta/libutils_test.cml"
  deps = [ "//src/proc/tests:starnix_test" ]
}

fuchsia_test_component("java_hello_world") {
  manifest = "meta/java_hello_world.cml"
  deps = [ "//src/proc/tests:starnix_test" ]
}

fuchsia_component("sh_test") {
  testonly = true
  check_references = false
  manifest = "meta/sh_test.cml"
  deps = [ "//src/proc/tests:starnix_test" ]
}

fuchsia_component("boringssl_self_test64") {
  testonly = true
  check_references = false
  manifest = "meta/boringssl_self_test64.cml"
}

stardroid_test_package("test_android_distro") {
  test_components = [
    ":boringssl_self_test64",
    ":java_hello_world",
    ":sh_test",
    ":binder_smoke_test",
    ":libutils_test",
  ]
}

stardroid_test_package("test_android_distro_with_error_logs") {
  test_components = [ ":binder_test" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
