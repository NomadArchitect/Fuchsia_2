# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/config.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")

rustc_test("bin") {
  name = "volume_change_earcons_integration_test"
  edition = "2021"
  source_root = "src/volume_change_earcons_tests.rs"
  sources = [
    "src/common.rs",
    "src/mock_audio_core_service.rs",
    "src/mock_sound_player_service.rs",
    "src/volume_change_earcons_tests.rs",
  ]
  deps = [
    "//sdk/fidl/fuchsia.media:fuchsia.media_rust",
    "//sdk/fidl/fuchsia.media.sounds:fuchsia.media.sounds_rust",
    "//sdk/fidl/fuchsia.settings:fuchsia.settings_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/settings/service/integration_tests/utils",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
  ]

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

fuchsia_component("volume_change_earcons_integration_test") {
  testonly = true
  manifest = "meta/volume_change_earcons_integration_test.cml"
  deps = [ ":bin" ]
}

fuchsia_test_package("volume_change_earcons_integration_test_pkg") {
  package_name = "volume_change_earcons_integration_test"
  test_components = [ ":volume_change_earcons_integration_test" ]
  deps = [
    ":tests_config",
    ":volume_change_earcons_integration_test",
    "//src/settings/service:setui_service_v2",
    "//src/sys/stash:pkg",
  ]

  # test_media_sounds_system_source is expected to panic and emit Error logs.
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

config_data("tests_config") {
  for_pkg = "volume_change_earcons_integration_test_pkg"
  sources = [
    "testdata/agent_configuration.json",
    "testdata/interface_configuration.json",
    "testdata/volume-changed.wav",
    "testdata/volume-max.wav",
  ]
}

group("volume_change_earcons_tests") {
  testonly = true
  public_deps = [ ":volume_change_earcons_integration_test_pkg" ]
}
