# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

source_set("analysis") {
  sources = [
    "analysis.cc",
    "analysis.h",
    "generators.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.media",
    "//sdk/lib/syslog/cpp",
    "//src/media/audio/lib/format",
  ]

  deps = [
    "//src/lib/fxl",
    "//src/media/audio/lib/wav",
    "//zircon/public/lib/fbl",
  ]
}

executable("unittest-bin") {
  visibility = [ ":*" ]
  testonly = true
  output_name = "audio-libanalysis-unittests"

  sources = [
    "analysis_unittest.cc",
    "generators_unittest.cc",
  ]

  deps = [
    ":analysis",
    "//src/lib/fxl/test:gtest_main",
    "//src/zircon/lib/zircon",
    "//third_party/googletest:gmock",
    "//zircon/public/lib/fbl",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_unittest_package("audio-libanalysis-unittests") {
  deps = [ ":unittest-bin" ]

  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [ ":audio-libanalysis-unittests" ]
}
