# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be found in the LICENSE file.

# Clock-related utility functions, for audio tests, tools or drivers

import("//build/components.gni")

source_set("clock") {
  public_deps = [
    ":clone_mono",
    ":pid",
    ":utils",
  ]
}

source_set("clone_mono") {
  sources = [
    "clone_mono.cc",
    "clone_mono.h",
  ]

  deps = [ "//sdk/lib/syslog/cpp" ]

  public_deps = [ "//zircon/public/lib/zx" ]
}

source_set("pid") {
  sources = [
    "pid_control.cc",
    "pid_control.h",
  ]

  deps = [
    "//sdk/lib/syslog/cpp",
    "//zircon/public/lib/zx",
  ]
}

source_set("utils") {
  sources = [
    "utils.cc",
    "utils.h",
  ]

  deps = [ "//sdk/lib/syslog/cpp" ]

  public_deps = [
    "//sdk/lib/fit-promise",
    "//src/media/audio/lib/timeline",
    "//zircon/public/lib/affine",
    "//zircon/public/lib/zx",
  ]
}

executable("unittest_bin") {
  visibility = [ ":*" ]
  testonly = true
  output_name = "audio-libclock-unittests"

  sources = [
    "clone_mono_unittest.cc",
    "pid_control_unittest.cc",
    "utils_unittest.cc",
  ]

  deps = [
    ":clock",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/media/audio/lib/clock/testing",
    "//src/zircon/lib/zircon",
  ]
}

fuchsia_unittest_package("audio-libclock-unittests") {
  deps = [ ":unittest_bin" ]
  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [ ":audio-libclock-unittests" ]
}
