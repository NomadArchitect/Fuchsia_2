# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("tests") {
  testonly = true
  deps = [ ":audio_services_mixer_fidl_realtime_unittests" ]
}

source_set("fidl_realtime") {
  visibility = [
    "./*",
    "//src/media/audio/services/mixer/fidl/*",
  ]

  sources = [
    "stream_sink_client.cc",
    "stream_sink_client.h",
    "stream_sink_server.cc",
    "stream_sink_server.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.audio:fuchsia.audio_cpp_wire",
    "//sdk/fidl/fuchsia.audio.mixer:fuchsia.audio.mixer_cpp_wire",
    "//sdk/fidl/fuchsia.mediastreams:fuchsia.mediastreams_cpp_wire",
    "//sdk/lib/fit",
    "//sdk/lib/fit-promise",
    "//src/media/audio/services/common",
    "//src/media/audio/services/mixer/common",
    "//src/media/audio/services/mixer/mix",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/trace",
  ]

  deps = [ "//sdk/lib/syslog/cpp" ]
}

executable("fidl_realtime_unittests") {
  output_name = "audio_services_mixer_fidl_realtime_unittests"
  testonly = true

  sources = [
    "stream_sink_client_unittest.cc",
    "stream_sink_server_unittest.cc",
  ]

  deps = [
    ":fidl_realtime",
    "//src/lib/fxl/test:gtest_main",
    "//src/media/audio/lib/processing:prebuilt_coefficient_tables",
    "//src/media/audio/services/common/testing",
    "//src/media/audio/services/mixer/mix",
    "//src/media/audio/services/mixer/mix/testing",
    "//third_party/googletest:gmock",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/async-loop/testing:async-loop-testing",
    "//zircon/system/ulib/sync:sync-cpp",
  ]
}

fuchsia_unittest_package("audio_services_mixer_fidl_realtime_unittests") {
  deps = [ ":fidl_realtime_unittests" ]
}
