# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("tests") {
  testonly = true
  deps = [ ":audio_mixer_service_fidl_unittests" ]
}

source_set("fidl") {
  visibility = [
    ":*",
    "//src/media/audio/mixer_service:*",
    "//src/media/audio/mixer_service/fidl/testing:*",
  ]

  sources = [
    "fidl_graph.cc",
    "fidl_graph.h",
    "fidl_graph_creator.cc",
    "fidl_graph_creator.h",
    "node.cc",
    "node.h",
    "ptr_decls.h",
    "reachability.cc",
    "reachability.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.audio.mixer:fuchsia.audio.mixer_llcpp",
    "//sdk/fidl/fuchsia.media2:fuchsia.media2_llcpp",
    "//sdk/fidl/fuchsia.mediastreams:fuchsia.mediastreams_llcpp",
    "//sdk/lib/fit",
    "//sdk/lib/fit-promise",
    "//src/media/audio/mixer_service/common",
    "//src/media/audio/mixer_service/mix",
  ]

  deps = [
    "//sdk/lib/syslog/cpp",
    "//zircon/system/ulib/trace",
  ]
}

executable("fidl_unittests") {
  testonly = true

  sources = [
    "node_unittest.cc",
    "reachability_unittest.cc",
  ]

  deps = [
    ":fidl",
    "//src/lib/fxl/test:gtest_main",
    "//src/media/audio/mixer_service/fidl/testing",
    "//third_party/googletest:gmock",
  ]
}

fuchsia_unittest_package("audio_mixer_service_fidl_unittests") {
  deps = [ ":fidl_unittests" ]
}
