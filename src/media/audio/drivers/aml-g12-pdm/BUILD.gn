# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")

driver_bind_rules("aml_g12_pdm_bind") {
  rules = "aml_g12_pdm.bind"
  header_output = "aml_g12_pdm_bind.h"
  bind_output = "aml-g12-pdm.bindbc"
  tests = "tests.json"
  deps = [ "//src/devices/bind/amlogic.platform" ]
}

driver_module("aml-g12-pdm-driver") {
  output_name = "aml-g12-pdm"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "audio-stream-in.cc" ]

  deps = [
    ":aml_g12_pdm_bind",
    "//sdk/banjo/fuchsia.hardware.clock:fuchsia.hardware.clock_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.i2c:fuchsia.hardware.i2c_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_banjo_cpp",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/media/audio/drivers/lib/audio-driver-proto",
    "//src/media/audio/drivers/lib/audio-proto-utils",
    "//src/media/audio/lib/simple-audio-stream",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/inspect",
  ]
}

fuchsia_driver_component("aml-g12-pdm") {
  component_name = "aml-g12-pdm"
  deps = [ ":aml-g12-pdm-driver" ]
}

group("tests") {
  testonly = true
  deps = [
    ":aml_g12_pdm_bind_test",
    "test:test-package",
  ]
}
