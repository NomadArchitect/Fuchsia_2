# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")
import("//build/zbi/kernel_cmdline.gni")

fuchsia_system_package("virtual_audio") {
  deps = [ ":virtual_audio_driver" ]
  allowed_in_extra_deps = true
}

driver_bind_rules("virtual_audio_bind") {
  rules = "virtual_audio.bind"
  header_output = "virtual_audio_bind.h"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.platform" ]
}

group("tests") {
  testonly = true
  deps = [ ":virtual_audio_bind_test" ]
}
driver_module("virtual_audio_driver-driver") {
  output_name = "virtual_audio_driver"

  sources = [
    "virtual_audio.h",
    "virtual_audio_bus.cc",
    "virtual_audio_control_impl.cc",
    "virtual_audio_control_impl.h",
    "virtual_audio_device_impl.cc",
    "virtual_audio_device_impl.h",
    "virtual_audio_stream.cc",
    "virtual_audio_stream.h",
    "virtual_audio_stream_in.h",
    "virtual_audio_stream_out.h",
  ]

  deps = [
    ":virtual_audio_bind",
    "//sdk/fidl/fuchsia.virtualaudio",
    "//sdk/fidl/fuchsia.virtualaudio:fuchsia.virtualaudio_c",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/media/audio/lib/simple-audio-stream",
    "//zircon/public/lib/affine",
    "//zircon/public/lib/async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/audio-driver-proto",
    "//zircon/system/ulib/closure-queue",
    "//zircon/system/ulib/fzl",
  ]
}

fuchsia_driver_component("virtual_audio_driver") {
  component_name = "virtual_audio_driver"
  deps = [ ":virtual_audio_driver-driver" ]
}

kernel_cmdline("kernel-logging") {
  args = [ "driver.virtual_audio.log=trace" ]
}
