# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

source_set("ffmpeg") {
  public = [
    "av_codec_context.h",
    "av_format_context.h",
    "av_frame.h",
    "av_io_context.h",
    "av_packet.h",
    "ffmpeg_init.h",
    "metadata.h",
  ]

  sources = [
    "av_codec_context.cc",
    "av_io_context.cc",
    "ffmpeg_init.cc",
    "metadata.cc",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.audiovideo",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/media/lib/ffmpeg",
    "//src/media/vnext/lib/formats",
    "//src/media/vnext/lib/reader",
    "//zircon/system/ulib/async",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  public_configs = [
    "//build/config:Wno-conversion",
    ":ffmpeg_defines",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":ffmpeg_unittests" ]
}

test("ffmpeg_unittests_bin") {
  testonly = true

  output_name = "ffmpeg_unittests"

  sources = [
    "test/av_codec_context_unittests.cc",
    "test/av_io_context_unittests.cc",
  ]

  deps = [
    ":ffmpeg",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_unittest_package("ffmpeg_unittests") {
  package_name = "ffmpeg_unittests"
  deps = [ ":ffmpeg_unittests_bin" ]
}

config("ffmpeg_defines") {
  defines = [
    "FF_API_PIX_FMT_DESC=0",
    "FF_API_OLD_DECODE_AUDIO=0",
    "FF_API_DESTRUCT_PACKET=0",
    "FF_API_GET_BUFFER=0",
    "FF_API_CONVERGENCE_DURATION=0",
    "__STDC_CONSTANT_MACROS",
  ]
}
