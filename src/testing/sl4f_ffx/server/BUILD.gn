# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  name = "sl4f_ffx_proxy_server"
  with_unit_tests = true
  edition = "2021"

  deps = [
    "//sdk/fidl/fuchsia.developer.ffx:fuchsia.developer.ffx_rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-hyper",
    "//src/testing/sl4f_ffx/fidl:fuchsia.sl4f.ffx_rust",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:hyper",
  ]

  test_deps = [
    "//sdk/fidl/fuchsia.developer.ffx:fuchsia.developer.ffx_rust",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component-test",
  ]

  sources = [ "src/main.rs" ]
}

fuchsia_component("sl4f_ffx_proxy_server_component") {
  component_name = "sl4f_proxy_server"
  manifest = "meta/server.cml"
  deps = [ ":bin" ]
}

# The package URL will be:
# fuchsia-pkg://fuchsia.com/sl4f-ffx-proxy-server#meta/sl4f_proxy_server.cm
fuchsia_package("sl4f-ffx-proxy-server") {
  deps = [ ":sl4f_ffx_proxy_server_component" ]
}

# The test will be fuchsia-pkg://fuchsia.com/sl4f-ffx-tests#meta/sl4f-ffx-tests.cm
# This template will generate a component target based on its target, which means that no other
# target can be `sl4f-ffx-tests_component`. However, the component name will be `sl4f-ffx-tests`
fuchsia_unittest_package("sl4f-ffx-tests") {
  deps = [ ":bin_test" ]
}

group("tests") {
  testonly = true
  deps = [ ":sl4f-ffx-tests" ]
}
