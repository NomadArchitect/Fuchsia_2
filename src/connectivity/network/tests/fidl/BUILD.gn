# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import(
    "//src/connectivity/network/testing/expectation/fuchsia_test_with_expectations_package.gni")

group("tests") {
  testonly = true
  deps = [
    ":netstack-error-fidl-tests",
    ":netstack-fidl-tests",
    ":netstack-interfaces-admin-fidl-tests",
  ]
}

tests = [
  "debug",
  "interfaces",
  "neighbor",
  "routes",
]

tests_with_expectations = [ "interfaces-admin" ]

foreach(test, tests + tests_with_expectations) {
  name = "netstack-${test}-fidl-test"
  fuchsia_test_component(name) {
    manifest = "meta/${name}.cml"
    deps = [ test ]
  }
}

filter_tests = [
  "nat",
  "tcp",
  "udp",
]

foreach(test, filter_tests) {
  name = "netstack-${test}-filter-fidl-test"
  fuchsia_test_component(name) {
    manifest = "meta/netstack-${test}-filter-fidl-test.cml"
    deps = [ "filter/${test}" ]
  }
}

_common_test_package_deps = [
  # netemul-sandbox-v2 is used to create hermetic test realms.
  "//src/connectivity/network/testing/netemul/service:netemul-sandbox",

  # netstack is under test.
  "//src/connectivity/network/netstack:component-debug",

  # netstack3 is under test.
  "//src/connectivity/network/netstack3:component-debug",
]

foreach(test, tests_with_expectations) {
  fuchsia_test_with_expectations_package("netstack-${test}-fidl-tests") {
    test_components = [ ":netstack-${test}-fidl-test" ]
    deps = _common_test_package_deps

    expectations = "expectations/${test}.json5"

    # TODO(https://fxbug.dev/65359): Figure out how to allow high-severity logs
    # in expected-to-fail tests.
    test_specs = {
      log_settings = {
        max_severity = "ERROR"
      }
    }
  }
}

fuchsia_test_package("netstack-fidl-tests") {
  test_components = []
  foreach(test, tests) {
    test_components += [ ":netstack-${test}-fidl-test" ]
  }
  foreach(test, filter_tests) {
    test_components += [ ":netstack-${test}-filter-fidl-test" ]
  }

  deps = _common_test_package_deps
}

fuchsia_test_component("netstack-interfaces-error-fidl-test") {
  manifest = "meta/netstack-interfaces-error-fidl-test.cml"
  deps = [ "interfaces:error" ]
}

# TODO(https://fxbug.dev/65359): Merge this with the above package once it is
# supported to configure acceptable error log lines.
fuchsia_test_package("netstack-error-fidl-tests") {
  test_components = [ ":netstack-interfaces-error-fidl-test" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  deps = _common_test_package_deps
}
