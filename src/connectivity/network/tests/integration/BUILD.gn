# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/config.gni")

group("tests") {
  testonly = true
  deps = [
    ":netstack-error-integration-tests",
    ":netstack-integration-tests",
    "ip_forward:netemul-ip-forward-tests",
  ]
}

tests = [
  "dhcp",
  "dns",
  "fidl",
  "filter",
  "inspect",
  "ipv4",
  "ipv6",
  "management",
  "neighbor",
  "reachability",
  "routes",
  "socket",
  "sys",
]

foreach(test, tests) {
  name = "netstack-${test}-integration-test"
  fuchsia_test_component(name) {
    manifest = "meta/${name}.cml"
    deps = [ test ]
  }
}

fuchsia_test_package("netstack-integration-tests") {
  test_components = []
  foreach(test, tests) {
    test_components += [ ":netstack-${test}-integration-test" ]
  }

  deps = [
    # netemul-sandbox-v2 is used to create hermetic test realms.
    "//src/connectivity/network/testing/netemul-v2/service:netemul-sandbox",

    # netstack is under test.
    "//src/connectivity/network/netstack:component-debug",

    # netstack3 is under test.
    "//src/connectivity/network/netstack3:component",

    # reachability monitor is under test.
    "//src/connectivity/management/reachability:component-v2",

    # sampler config is tested as part of inspect integration tests.
    ":sampler-config",

    # stash_secure is used by dhcpd and netstack.
    "//src/sys/stash:stash_secure_v2",

    # dhcpd is used to test DHCP address acquisition.
    "//src/connectivity/network/dhcp:component",

    # DHCPv6 client is under test.
    "//src/connectivity/network/dhcpv6/client:component",

    # dns_resolver integration with netstack is tested.
    "//src/connectivity/network/dns:component",

    # netcfg integration with netstack is tested.
    ":netcfg-netemul-config",
    "//src/connectivity/network/netcfg:component",
  ]
}

fuchsia_test_component("netstack-fidl-error-integration-test") {
  manifest = "meta/netstack-fidl-error-integration-test.cml"
  deps = [ "fidl:error" ]
}

# TODO(https://fxbug.dev/74868): Merge this with the above package once it is
# supported to configure acceptable error log lines.
fuchsia_test_package("netstack-error-integration-tests") {
  test_components = [ ":netstack-fidl-error-integration-test" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  deps = [
    # netemul-sandbox-v2 is used to create hermetic test realms.
    "//src/connectivity/network/testing/netemul-v2/service:netemul-sandbox",

    # netstack is under test.
    "//src/connectivity/network/netstack:component-debug",

    # netstack3 is under test.
    "//src/connectivity/network/netstack3:component",
  ]
}

resource("netcfg-netemul-config") {
  testonly = true
  sources = [ "//src/connectivity/network/netcfg/config/empty.json" ]
  outputs = [ "netcfg/empty.json" ]
}

resource("sampler-config") {
  sources = [ "//src/diagnostics/config/sampler/netstack.json" ]
  outputs = [ "data/sampler-config/netstack.json" ]
}
