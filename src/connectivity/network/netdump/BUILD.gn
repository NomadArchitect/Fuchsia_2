# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

executable("bin") {
  output_name = "netdump"
  sources = [
    "filter.cc",
    "netdump.cc",
    "parser.cc",
    "tokens.cc",
  ]
  deps = [
    ":common",
    "//sdk/fidl/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_c_client",
    "//zircon/system/ulib/pretty",
  ]
}

source_set("common") {
  visibility = [ ":*" ]
  public_deps = [
    "//sdk/lib/fdio",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
  ]
}

test("unit_tests") {
  output_name = "netdump_unit_tests"
  sources = [
    "filter.cc",
    "parser.cc",
    "test/filter_builder_test.cc",
    "test/filter_test.cc",
    "test/parser_test.cc",
    "test/tokens_test.cc",
    "tokens.cc",
  ]
  include_dirs = [ "." ]
  deps = [
    ":common",
    "//zircon/system/ulib/mock-function",
    "//zircon/system/ulib/zxtest",
  ]
}

group("tests") {
  testonly = true

  deps = [
    ":netdump",
    ":netdump-unit-tests",
    "integration:tests",
  ]
}

fuchsia_component("component") {
  component_name = "netdump"
  deps = [ ":bin" ]
  manifest = "meta/netdump.cmx"
}

fuchsia_shell_package("netdump") {
  deps = [
    ":bin",
    ":component",
  ]
}

fuchsia_unittest_package("netdump-unit-tests") {
  deps = [ ":unit_tests" ]
  manifest = "meta/netdump_unit_tests.cmx"
}
