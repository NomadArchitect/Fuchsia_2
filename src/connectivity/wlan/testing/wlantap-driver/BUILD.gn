# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")

bind_rules("wlantapctl_bind") {
  rules = "wlantapctl.bind"
  output = "wlantapctl_bind.h"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.test" ]
}

group("tests") {
  testonly = true
  deps = [ ":wlantapctl_bind_test" ]
}

driver_module("wlantap_driver") {
  output_name = "wlantap"

  sources = [
    "utils.cc",
    "utils.h",
    "wlantap-ctl.cc",
    "wlantap-mac.cc",
    "wlantap-mac.h",
    "wlantap-phy.cc",
    "wlantap-phy.h",
  ]

  deps = [
    ":wlantapctl_bind",
    "//garnet/lib/wlan/protocol",
    "//sdk/banjo/ddk.hw.wlan.wlaninfo",
    "//sdk/banjo/fuchsia.hardware.wlanphyimpl:fuchsia.hardware.wlanphyimpl_banjo_cpp",
    "//sdk/fidl/fuchsia.wlan.tap:fuchsia.wlan.tap",
    "//sdk/fidl/fuchsia.wlan.tap:fuchsia.wlan.tap_llcpp",
    "//src/connectivity/wlan/lib/common/cpp:common",
    "//src/devices/lib/driver",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

fuchsia_system_package("wlantap") {
  deps = [ ":wlantap_driver" ]
  allowed_in_extra_deps = true
}
