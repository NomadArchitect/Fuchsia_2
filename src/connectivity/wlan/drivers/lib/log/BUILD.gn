# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

config("wlan_drivers_log_config") {
  include_dirs = [ "cpp/include" ]
}

source_set("cpp") {
  public_configs = [ ":wlan_drivers_log_config" ]
  sources = [ "cpp/log.cc" ]
  public_deps = [ "//src/lib/ddk" ]
}

test("wlan_drivers_log_test") {
  sources = [
    "cpp/log_test.cc",
    "cpp/throttle_counter_test.cc",
    "cpp/token_bucket_test.cc",
  ]
  deps = [
    "//src/connectivity/wlan/drivers/lib/log:cpp",
    "//src/devices/testing/no_ddk",
    "//src/lib/fxl/test:gtest_main",
  ]
}

fuchsia_unittest_package("wlan-drivers-log-tests") {
  deps = [ ":wlan_drivers_log_test" ]
}

group("tests") {
  testonly = true
  deps = [ ":wlan-drivers-log-tests" ]
}
