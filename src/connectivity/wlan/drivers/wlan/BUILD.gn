# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//src/sys/build/components.gni")

config("wlan_config") {
  include_dirs = [ "." ]
}

bind_rules("wlan_bind") {
  rules = "wlan.bind"
  output = "wlan_bind.h"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.wlan" ]
}

driver_module("wlan_driver") {
  output_name = "wlan"
  sources = [
    "device.cc",
    "device.h",
    "driver.cc",
    "minstrel.cc",
    "minstrel.h",
    "probe_sequence.cc",
    "probe_sequence.h",
  ]

  deps = [
    ":wlan_bind",
    "//sdk/banjo/ddk.hw.wlan.wlaninfo",
    "//sdk/banjo/fuchsia.hardware.ethernet",
    "//src/connectivity/wlan/lib/mlme/cpp:mlme",
    "//src/devices/lib/dev-operation",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
  ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
}

test("wlan_minstrel_unittest") {
  sources = [
    "minstrel.cc",
    "minstrel.h",
    "probe_sequence.cc",
    "probe_sequence.h",
    "tests/minstrel_unittest.cc",
    "tests/probe_sequence_unittest.cc",
  ]

  deps = [
    "//garnet/lib/wlan/protocol",
    "//src/connectivity/wlan/lib/common/cpp:common",
    "//src/connectivity/wlan/lib/mlme/cpp:mlme",
    "//src/connectivity/wlan/lib/mlme/cpp/tests:test_main",
    "//src/lib/timekeeper:testing",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/fbl",
  ]

  configs +=
      [ "//src/connectivity/wlan/lib/mlme/cpp/tests:wlan_mlme_tests_config" ]
}

fuchsia_unittest_package("wlan_drivers_tests") {
  deps = [ ":wlan_minstrel_unittest" ]
  manifest = "meta/wlan_minstrel_unittest.cmx"
}

fuchsia_system_package("wlan") {
  package_name = "wlan-system"

  deps = [ ":wlan_driver" ]

  allowed_in_extra_deps = true
}

group("tests") {
  testonly = true
  deps = [
    ":wlan_bind_test",
    ":wlan_drivers_tests",
  ]
}
