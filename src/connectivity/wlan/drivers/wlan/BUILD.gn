# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

config("wlan_config") {
  include_dirs = [ "." ]
}

driver_bind_rules("wlan_bind") {
  rules = "wlan.bind"
  header_output = "wlan_bind.h"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.wlan" ]
}

wlan_convert_sources = [
  "convert.cc",
  "convert.h",
]

wlan_convert_deps = [
  "//sdk/banjo/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_banjo_cpp",
  "//sdk/banjo/fuchsia.hardware.wlan.softmac:fuchsia.hardware.wlan.softmac_banjo_c",
  "//sdk/fidl/fuchsia.wlan.common:fuchsia.wlan.common_banjo_c",
  "//sdk/fidl/fuchsia.wlan.internal:fuchsia.wlan.internal_banjo_c",
  "//sdk/fidl/fuchsia.wlan.softmac:fuchsia.wlan.softmac_cpp_driver",
  "//src/connectivity/wlan/lib/mlme/cpp:mlme",
  "//zircon/system/ulib/fbl",
  "//zircon/system/ulib/zx",
]

source_set("wlan_convert") {
  sources = wlan_convert_sources
  public_deps = wlan_convert_deps
}

fuchsia_driver("wlan_driver-driver") {
  output_name = "wlan"
  sources = [
    "device.cc",
    "device.h",
    "driver.cc",
    "probe_sequence.cc",
    "probe_sequence.h",
  ]
  deps = [
    ":wlan_bind",
    ":wlan_convert",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//src/connectivity/wlan/lib/mlme/cpp:mlme",
    "//src/devices/lib/dev-operation",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/lib/ddk",
    "//src/lib/ddktl",
  ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("wlan_driver") {
  component_name = "wlan"
  deps = [ ":wlan_driver-driver" ]
  info = "wlan_driver-info.json"
  colocate = true
}

fuchsia_driver_package("wlan") {
  package_name = "wlan-system"
  driver_components = [ ":wlan_driver" ]
}

test("wlan_convert_unittest") {
  testonly = true
  output_name = "wlan_convert_unittest"
  sources = wlan_convert_sources + [ "tests/convert_unittest.cc" ]

  deps = wlan_convert_deps + [
           "//src/lib/fxl/test:gtest_main",
           "//third_party/googletest:gtest",
         ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_unittest_component("wlan_convert_unittest_component") {
  deps = [ ":wlan_convert_unittest" ]
}

fuchsia_test_package("wlan_unittests") {
  test_components = [ ":wlan_convert_unittest_component" ]
}

group("tests") {
  testonly = true
  deps = [
    ":wlan_bind_test",
    ":wlan_unittests",
  ]
}
