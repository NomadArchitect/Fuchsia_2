# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [ ":netdevice-migration-tests" ]
}

driver_bind_rules("netdevice-migration-bind") {
  rules = "netdevice_migration.bind"
  header_output = "netdevice_migration_bind.h"
  tests = "bind-tests.json"
  deps = [ "//src/devices/bind/fuchsia.ethernet" ]
}

source_set("src") {
  sources = [
    "netdevice_migration.cc",
    "netdevice_migration.h",
  ]
  public_deps = [
    ":netdevice-migration-bind",
    "//sdk/banjo/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.network.device:fuchsia.hardware.network.device_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_llcpp",
    "//sdk/fidl/fuchsia.hardware.network:fuchsia.hardware.network_llcpp",
    "//src/connectivity/network/drivers/network-device/device:shared_lock",
    "//src/lib/ddktl",
    "//src/lib/vmo_store",
    "//zircon/system/public",
    "//zircon/system/ulib/fbl",
  ]
}

driver_module("netdevice-migration-driver") {
  output_name = "netdevice-migration"
  deps = [
    ":netdevice-migration-bind",
    ":src",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
  ]
}

fuchsia_driver_component("netdevice-migration") {
  component_name = "netdevice-migration"
  deps = [ ":netdevice-migration-driver" ]
}

fuchsia_system_package("pkg") {
  package_name = "netdevice-migration"
  deps = [ ":netdevice-migration" ]

  # TODO(https://fxbug.dev/78293): Change the line below, as needed, once there exists a
  # compile-time mechanism for binding the migration driver.
  allowed_in_extra_deps = true
}

test("unit-tests") {
  output_name = "netdevice-migration-unit-tests"
  sources = [ "unit-tests.cc" ]
  deps = [
    ":netdevice-migration-bind",
    ":src",
    "//src/devices/testing/fake-bti",
    "//src/devices/testing/mock-ddk",
    "//src/lib/testing/predicates",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//third_party/googletest:gtest_main",
  ]
}

fuchsia_unittest_package("netdevice-migration-tests") {
  deps = [ ":unit-tests" ]
}
