# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")

bind_rules("rndis_function_bind") {
  rules = "rndis_function.bind"
  output = "rndis_function_bind.h"
  tests = "bind-tests.json"
  deps = [
    "//src/devices/bind/fuchsia.usb",
    "//src/devices/bind/fuchsia.usb.misc",
  ]
}

source_set("rndis-function-lib") {
  configs += [ "//build/config:all_source" ]
  sources = [
    "rndis_function.cc",
    "rndis_function.h",
  ]
  public_deps = [
    ":rndis_function_bind",
    "//sdk/banjo/fuchsia.hardware.ethernet",
    "//sdk/banjo/fuchsia.hardware.usb",
    "//sdk/banjo/fuchsia.hardware.usb.composite",
    "//sdk/banjo/fuchsia.hardware.usb.function",
    "//sdk/banjo/fuchsia.hardware.usb.request",
    "//src/connectivity/ethernet/lib/rndis",
    "//src/devices/usb/lib/usb",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/async",
    "//zircon/public/lib/async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

driver_module("rndis-function") {
  # visibility = [ ":*" ]
  output_name = "rndis-function"

  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  deps = [
    ":rndis-function-lib",
    "//src/devices/lib/driver",
  ]
}

test("rndis-function-test") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    testonly = true
  }
  sources = [ "rndis_function_test.cc" ]
  deps = [
    ":rndis-function-lib",
    "//src/devices/testing/fake_ddk",
    "//zircon/public/lib/zxtest",
  ]
}

fuchsia_unittest_package("rndis-function-test-package") {
  package_name = "rndis-function-test"
  deps = [ ":rndis-function-test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":rndis-function-test-package",
    ":rndis_function_bind_test",
  ]
}
