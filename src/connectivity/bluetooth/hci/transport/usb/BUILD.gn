# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")

bind_rules("bt_transport_usb_bind") {
  rules = "bt_transport_usb.bind"
  output = "bt_transport_usb_bind.h"
  tests = "bind_tests.json"
  deps = [
    "//src/devices/bind/fuchsia.usb",
    "//src/devices/bind/fuchsia.usb.wireless",
  ]
}

driver_module("bt-transport-usb") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = []
  deps = [ ":common" ]
}

source_set("common") {
  sources = [ "bt-transport-usb.c" ]
  public_deps = [
    ":bt_transport_usb_bind",
    "//sdk/banjo/fuchsia.hardware.bt.hci",
    "//sdk/banjo/fuchsia.hardware.usb",
    "//sdk/banjo/fuchsia.hardware.usb.composite",
    "//sdk/banjo/fuchsia.hardware.usb.request",
    "//src/devices/lib/driver",
    "//src/devices/usb/lib/usb",
    "//src/lib/listnode",
    "//zircon/public/lib/sync",
  ]
}

test("bt-transport-usb-test") {
  sources = [ "bt-transport-usb-test.cc" ]
  deps = [
    ":common",
    "//sdk/lib/fdio",
    "//src/devices/testing/fake_ddk",
    "//src/devices/usb/testing/descriptor-builder",
    "//zircon/public/lib/zxtest",
  ]
}

fuchsia_unittest_package("bt-transport-usb-package") {
  deps = [ ":bt-transport-usb-test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":bt-transport-usb-package",
    ":bt_transport_usb_bind_test",
  ]
}
