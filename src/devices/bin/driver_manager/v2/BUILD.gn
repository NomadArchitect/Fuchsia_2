# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

source_set("driver_runner") {
  sources = [
    "composite_assembler.cc",
    "composite_assembler.h",
    "composite_manager.cc",
    "composite_manager.h",
    "device_group_v2.cc",
    "device_group_v2.h",
    "driver_component.cc",
    "driver_component.h",
    "driver_host.cc",
    "driver_host.h",
    "driver_runner.cc",
    "driver_runner.h",
    "node.cc",
    "node.h",
    "parent_set_collector.cc",
    "parent_set_collector.h",
  ]

  deps = [
    "//sdk/lib/driver2:cpp",
    "//sdk/lib/sys/component/cpp",
    "//src/devices/bin/driver_manager:binding",
    "//src/devices/bin/driver_manager/device_group:lib",
    "//src/devices/lib/log",
    "//src/lib/fxl",
    "//zircon/system/ulib/zircon-internal",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component_cpp_wire",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_cpp",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_cpp_wire",
    "//sdk/fidl/fuchsia.component.runner:fuchsia.component.runner_cpp_wire",
    "//sdk/fidl/fuchsia.device.composite:fuchsia.device.composite_cpp",
    "//sdk/fidl/fuchsia.device.manager:fuchsia.device.manager_cpp",
    "//sdk/fidl/fuchsia.driver.development:fuchsia.driver.development_cpp_wire",
    "//sdk/fidl/fuchsia.driver.host:fuchsia.driver.host_cpp_wire",
    "//sdk/fidl/fuchsia.driver.index:fuchsia.driver.index_cpp_wire",
    "//sdk/lib/inspect/component/cpp:inspect_component_cpp",
    "//src/lib/ddk",
    "//src/lib/storage/vfs/cpp",
  ]

  # TODO(fxbug.dev/94768): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]
}

executable("driver_runner_test") {
  testonly = true
  sources = [ "driver_runner_test.cc" ]

  deps = [
    ":driver_runner",
    "//sdk/fidl/fuchsia.component:fuchsia.component_hlcpp",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_hlcpp",
    "//sdk/fidl/fuchsia.driver.host:fuchsia.driver.host_hlcpp",
    "//sdk/lib/driver2:cpp",
    "//sdk/lib/inspect/testing/cpp",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/zircon-internal",
  ]

  # TODO(fxbug.dev/94768): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]
}

fuchsia_unittest_component("driver-runner-test") {
  deps = [ ":driver_runner_test" ]
}

executable("composite_assembler_test") {
  testonly = true
  sources = [ "composite_assembler_test.cc" ]

  deps = [
    ":driver_runner",
    "//sdk/lib/inspect/testing/cpp",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/lib/ddk",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//zircon/system/ulib/zircon-internal",
  ]
}

fuchsia_unittest_component("composite-assembler-test") {
  deps = [ ":composite_assembler_test" ]
}

fuchsia_test_package("driver-runner-tests") {
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }

  test_components = [
    ":driver-runner-test",
    ":composite-assembler-test",
  ]
}
