# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/drivers/driver_compat_test.gni")
import("//build/fidl/fidl.gni")
import("//build/test.gni")

fidl("fuchsia.device.instancelifecycle.test") {
  testonly = true
  sources = [ "instancelifecycle.test.fidl" ]
  visibility = [ ":*" ]
  public_deps = [ "//zircon/vdso/zx" ]
}

driver_bind_rules("test-lifecycle-bind") {
  rules = "test-lifecycle.bind"
  header_output = "test-lifecycle-bind.h"
  deps = [ "//src/devices/bind/fuchsia.test.platform" ]
}

fuchsia_driver("fuchsia_driver") {
  output_name = "ddk-instance-lifecycle-test-driver"

  testonly = true
  sources = [
    "test-driver-child.cc",
    "test-driver.cc",
  ]
  deps = [
    ":fuchsia.device.instancelifecycle.test_cpp_wire",
    ":test-lifecycle-bind",
    "//sdk/lib/fdio",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/sync",
  ]
  visibility = [
    ":*",
    "//src/devices/bundles:drivers",
  ]
}

fuchsia_driver_component("driver") {
  testonly = true
  component_name = "ddk-instance-lifecycle-test-driver"
  deps = [ ":fuchsia_driver" ]
  info = "driver-info.json"
  colocate = true
}

driver_compat_test("ddk-instance-lifecycle-test") {
  testonly = true
  sources = [ "test.cc" ]
  deps = [
    ":fuchsia.device.instancelifecycle.test_cpp_wire",
    "//sdk/lib/fdio",
    "//src/devices/testing/driver-integration-test-shim",
    "//src/lib/ddk",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_component("ddk-instance-lifecycle-test-dfv1") {
  deps = [ ":ddk-instance-lifecycle-test_dfv1" ]
}

fuchsia_unittest_component("ddk-instance-lifecycle-test-dfv2") {
  deps = [ ":ddk-instance-lifecycle-test_dfv2" ]
}

fuchsia_test_package("ddk-instance-lifecycle-test-package") {
  test_components = [
    ":ddk-instance-lifecycle-test-dfv1",
    ":ddk-instance-lifecycle-test-dfv2",
  ]
  deps = [
    ":driver",
    "//src/devices/board/drivers/integration-test",
    "//src/devices/bus/drivers/platform",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":ddk-instance-lifecycle-test-package" ]
}
