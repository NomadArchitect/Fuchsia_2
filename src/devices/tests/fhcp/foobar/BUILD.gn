# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/drivers/fhcp_test_package.gni")
import("//build/test.gni")

test("foobar-test-bin") {
  output_name = "fhcp-foobar-test"
  sources = [ "foobar-test.cc" ]
  deps = [ "//zircon/system/ulib/zxtest" ]
}

fuchsia_component("fhcp-foobar-test-cmp") {
  testonly = true
  manifest = "meta/foobar-test.cml"
  component_name = "fhcp-foobar-test"
  deps = [ ":foobar-test-bin" ]
}

fhcp_test_package("fhcp-foobar-touchpad-test") {
  test_types = [
    "functional",
    "performance",
  ]
  device_categories = [
    {
      category = "input"
      subcategory = "touchpad"
    },
    {
      category = "bluetooth"
      subcategory = ""
    },
  ]
  test_components = [ ":fhcp-foobar-test-cmp" ]
  test_specs = {
    environments = [
      {
        dimensions = {
          device_type = "AEMU"
        }
        tags = [ "fhcp-automated" ]
      },
    ]
  }
}

fhcp_test_package("fhcp-foobar-camera-test") {
  test_types = [ "functional" ]
  device_categories = [
    {
      category = "imaging"
      subcategory = "camera"
    },
  ]
  test_components = [ ":fhcp-foobar-test-cmp" ]
  test_specs = {
    environments = [
      {
        dimensions = {
          device_type = "AEMU"
        }
        tags = [ "fhcp-automated" ]
      },
      {
        dimensions = {
          device_type = "Intel NUC Kit NUC7i5DNHE"
        }
        tags = [ "fhcp-automated" ]
      },
    ]
  }
}

group("tests") {
  testonly = true
  deps = [
    ":fhcp-foobar-camera-test",
    ":fhcp-foobar-touchpad-test",
  ]
}
