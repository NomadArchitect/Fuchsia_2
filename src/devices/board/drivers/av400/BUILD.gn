# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")

driver_bind_rules("av400-emmc-bind") {
  rules = "av400-emmc.bind"
  header_output = "av400-emmc-bind.h"
  bind_output = "av400-emmc-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.a5",
    "//src/devices/bind/fuchsia.amlogic.platform.meson",
    "//src/devices/bind/fuchsia.clock",
    "//src/devices/bind/fuchsia.gpio",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("av400-sdio-bind") {
  rules = "av400-sdio.bind"
  header_output = "av400-sdio-bind.h"
  bind_output = "av400-sdio-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.a5",
    "//src/devices/bind/fuchsia.gpio",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("pwm_init_bind") {
  rules = "pwm_init.bind"
  header_output = "pwm_init_bind.h"
  bind_output = "pwm_init_bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform.a5",
    "//src/devices/bind/fuchsia.amlogic.platform.meson",
    "//src/devices/bind/fuchsia.clock",
    "//src/devices/bind/fuchsia.gpio",
    "//src/devices/bind/fuchsia.pwm",
  ]
}

driver_bind_rules("av400-bind") {
  rules = "av400.bind"
  header_output = "av400-bind.h"
  tests = "bind-tests.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("spi_1_bind") {
  rules = "bind/spi_1.bind"
  header_output = "spi_1_bind.h"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.a5",
    "//src/devices/bind/fuchsia.gpio",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("tdm-i2s-bind") {
  rules = "bind/tdm-i2s.bind"
  header_output = "tdm-i2s-bind.h"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform.a5",
    "//src/devices/bind/fuchsia.codec",
    "//src/devices/bind/fuchsia.gpio",
    "//src/devices/bind/fuchsia.ti.platform",
  ]
}

driver_bind_rules("audio-tas5707-stereo-bind") {
  rules = "bind/audio-tas5707-stereo.bind"
  header_output = "audio-tas5707-stereo-bind.h"
  deps = [ "//src/devices/bind/fuchsia.i2c" ]
}

fuchsia_driver("av400-driver") {
  output_name = "av400"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "av400-audio.cc",
    "av400-clk.cc",
    "av400-emmc.cc",
    "av400-eth.cc",
    "av400-gpio.cc",
    "av400-i2c.cc",
    "av400-pwm.cc",
    "av400-registers.cc",
    "av400-rtc.cc",
    "av400-sdio.cc",
    "av400-spi.cc",
    "av400-sysmem.cc",
    "av400-thermal.cc",
    "av400-usb.cc",
    "av400.cc",
  ]
  deps = [
    ":audio-tas5707-stereo-bind_header",
    ":av400-bind",
    ":av400-emmc-bind_header",
    ":av400-sdio-bind_header",
    ":pwm_init_bind_header",
    ":spi_1_bind_header",
    ":tdm-i2s-bind_header",
    "//sdk/banjo/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.gpioimpl:fuchsia.hardware.gpioimpl_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.iommu:fuchsia.hardware.iommu_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.serial:fuchsia.hardware.serial_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.modeswitch:fuchsia.hardware.usb.modeswitch_banjo_cpp",
    "//sdk/banjo/fuchsia.sysmem:fuchsia.sysmem_banjo_c",
    "//sdk/fidl/fuchsia.hardware.serial:fuchsia.hardware.serial_c",
    "//sdk/fidl/fuchsia.hardware.thermal:fuchsia.hardware.thermal_c",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_c",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/driver",
    "//src/devices/lib/fidl-metadata",
    "//src/devices/lib/metadata:llcpp",
    "//src/devices/lib/mmio",
    "//src/devices/lib/ti",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",

    # TODO(fxb/38132): Migrate to the new bind rules and delete the below
    "//src/lib/ddk:ddk-deprecated-binding-headers",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
  ]

  # TODO(fxbug.dev/95833): This target uses the deprecated C bindings.
  # Consider switching to the C++ bindings. See linked bug for details.
  configs += [ "//build/c:fidl-deprecated-c-bindings" ]
}

fuchsia_driver_component("av400") {
  component_name = "av400"
  deps = [ ":av400-driver" ]
  info = "av400-info.json"
  colocate = true
}

group("tests") {
  testonly = true
  deps = [ ":av400-bind_test" ]
}
