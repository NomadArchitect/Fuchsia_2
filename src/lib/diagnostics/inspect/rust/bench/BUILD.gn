# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("reader-bin") {
  name = "inspect_rust_reader_benchmarks"
  edition = "2018"

  source_root = "src/reader.rs"

  deps = [
    "//sdk/fidl/fuchsia.inspect:fuchsia.inspect-rustc",
    "//src/developer/fuchsia-criterion",
    "//src/lib/diagnostics/inspect/runtime/rust",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
  ]

  sources = [
    "src/reader.rs",
    "src/utils.rs",
  ]
}

rustc_binary("writer-bin") {
  name = "inspect_rust_writer_benchmarks"
  edition = "2018"

  source_root = "src/writer.rs"

  deps = [
    "//sdk/fidl/fuchsia.inspect:fuchsia.inspect-rustc",
    "//src/developer/fuchsia-criterion",
    "//src/lib/diagnostics/inspect/runtime/rust",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/mapped-vmo",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:num",
    "//third_party/rust_crates:paste",
    "//third_party/rust_crates:rand",
  ]

  sources = [
    "src/utils.rs",
    "src/writer.rs",
  ]
}

fuchsia_component("reader-component") {
  component_name = "reader"
  manifest = "meta/reader.cml"
  deps = [ ":reader-bin" ]
}

fuchsia_component("writer-component") {
  component_name = "writer"
  manifest = "meta/writer.cml"
  deps = [ ":writer-bin" ]
}

fuchsia_package("rust-inspect-benchmarks-v2") {
  deps = [
    ":reader-component",
    ":writer-component",
  ]
}

fuchsia_package_with_single_component("rust-inspect-benchmarks") {
  manifest = "meta/rust_inspect_benchmarks.cmx"
  deps = [ ":bin" ]
  testonly = true
}

rustc_binary("bin") {
  name = "rust_inspect_benchmarks"
  edition = "2018"
  with_unit_tests = true

  deps = [
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics-rustc",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/diagnostics/selectors",
    "//src/lib/fuchsia",
    "//src/lib/trace/rust:trace",
    "//src/lib/trace/rust:trace-provider",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:lazy_static",
    "//third_party/rust_crates:paste",
    "//third_party/rust_crates:rand",
  ]

  test_deps = [ "//third_party/rust_crates:pretty_assertions" ]

  sources = [
    "src/bench_utils.rs",
    "src/main.rs",
  ]
}

fuchsia_unittest_package("rust-inspect-benchmarks-utils-test") {
  deps = [ ":bin_test" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("bench") {
  testonly = true
  deps = [
    ":rust-inspect-benchmarks",
    ":rust-inspect-benchmarks-v2",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":rust-inspect-benchmarks-utils-test" ]
}
