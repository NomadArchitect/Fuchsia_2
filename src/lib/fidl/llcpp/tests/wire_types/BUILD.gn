# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fidl/fidl.gni")
import("//build/test.gni")
import("//src/sys/build/components.gni")

group("wire_types") {
  testonly = true
  deps = [
    ":fidl_llcpp_builder_tests",
    ":fidl_llcpp_builder_tests_bin($host_toolchain)",
    ":fidl_llcpp_wire_type_api_tests",
  ]
}

fidl("fidl.llcpp.types.test") {
  sources = [ "types.test.fidl" ]
  public_deps = [ "//zircon/vdso/zx" ]
  host_llcpp = true
}

# TODO(fxbug.dev/71459): This test is Fuchsia-exclusive for now, because
# the referenced FIDL library does not compile under host.
test("fidl_llcpp_wire_type_api_tests_bin") {
  output_name = "fidl_llcpp_wire_type_api_tests"
  sources = [
    "bits_test.cc",
    "enum_test.cc",
    "table_test.cc",
    "traits_test.cc",
    "xunion_test.cc",
  ]
  deps = [
    ":fidl.llcpp.types.test_llcpp",
    "//src/lib/fidl/llcpp/tests:types_test_utils",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
  ]
}

fuchsia_unittest_package("fidl_llcpp_wire_type_api_tests") {
  # Use `fuchsia.process.Launcher` for death tests
  manifest = "meta/wire_type_api_tests.cmx"
  deps = [ ":fidl_llcpp_wire_type_api_tests_bin" ]
}

test("fidl_llcpp_builder_tests_bin") {
  output_name = "fidl_llcpp_builder_tests"
  sources = [
    "aligned_test.cc",
    "unowned_ptr_test.cc",
    "vector_view_test.cc",
  ]
  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/fidl",
    "//zircon/public/lib/fidl-llcpp",
  ]

  if (is_fuchsia) {
    sources += [
      # These tests use Zircon handles.
      "fidl_allocator_test.cc",

      # These tests use fbl.
      "memory_test.cc",
    ]
    deps += [ "//src/lib/fidl/llcpp/tests:types_test_utils" ]
  }
}

fuchsia_unittest_package("fidl_llcpp_builder_tests") {
  deps = [ ":fidl_llcpp_builder_tests_bin" ]
}
