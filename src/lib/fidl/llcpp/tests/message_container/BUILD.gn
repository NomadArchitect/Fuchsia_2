# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/test.gni")

group("message_container") {
  testonly = true
  deps = [
    ":fidl_llcpp_message_container_tests",
    ":fidl_llcpp_result_types_tests",
    ":fidl_llcpp_result_types_tests_bin($host_toolchain)",
  ]
}

fidl("fidl.llcpp.linearized.test") {
  sources = [ "linearized.test.fidl" ]
}

test("fidl_llcpp_message_container_tests_bin") {
  output_name = "fidl_llcpp_message_container_tests"
  sources = [
    "encoded_test.cc",
    "incoming_message_test.cc",
    "outgoing_message_test.cc",
    "outgoing_to_incoming_message_test.cc",
  ]
  deps = [
    ":fidl.llcpp.linearized.test_llcpp",
    "//sdk/testing/fidl:fidl_test_llcpp",
    "//src/lib/fidl/llcpp/tests:types_test_utils",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
  ]
}

fuchsia_unittest_package("fidl_llcpp_message_container_tests") {
  # TODO(fxbug.dev/74490): Required for death-tests. We could remove the
  # manifests by switching to zxtest.
  manifest = "meta/fidl_llcpp_message_container_tests.cmx"
  deps = [ ":fidl_llcpp_message_container_tests_bin" ]
}

test("fidl_llcpp_result_types_tests_bin") {
  output_name = "fidl_llcpp_result_types_tests"
  sources = [ "result_description_test.cc" ]
  deps = [
    "//zircon/system/ulib/fidl:fidl-llcpp",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("fidl_llcpp_result_types_tests") {
  deps = [ ":fidl_llcpp_result_types_tests_bin" ]
}
