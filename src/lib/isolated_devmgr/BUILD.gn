# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")
import("//src/lib/isolated_devmgr/isolated_devmgr.gni")

source_set("src") {
  testonly = true
  sources = [
    "isolated_devmgr.cc",
    "isolated_devmgr.h",
  ]

  public_deps = [ "//zircon/system/ulib/devmgr-integration-test" ]

  deps = [
    "//sdk/lib/fdio",
    "//sdk/lib/fit",
    "//sdk/lib/sys/cpp",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/fxl",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/inspector",
  ]
}

executable("isolated_devmgr") {
  testonly = true
  output_name = "isolated_devmgr"

  sources = [ "main.cc" ]

  deps = [
    ":src",
    "//sdk/lib/fdio",
    "//sdk/lib/fit",
    "//sdk/lib/sys/cpp",
    "//src/lib/ddk",
    "//src/lib/fxl",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/fbl",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/devmgr-integration-test",
  ]

  # These binaries are used by isolated_devmgr at runtime and should be
  # present in the same package.
  data_deps = [
    "//src/devices/bin/driver_host",
    "//src/devices/bin/driver_manager",
    "//src/storage/fshost",
  ]

  metadata = {
    # Tests that use isolated_devmgr require the following capabilities
    test_component_manifest_cmx = [
      {
        sandbox = {
          services = [
            "fuchsia.exception.Handler",
            "fuchsia.process.Launcher",
          ]
        }
      },
    ]
  }
}

static_library("usb-virtual-bus") {
  testonly = true
  sources = [
    "usb-virtual-bus-helper.cc",
    "usb-virtual-bus-helper.h",
    "usb-virtual-bus.cc",
    "usb-virtual-bus.h",
  ]
  public_deps = [
    "//garnet/public/lib/gtest",
    "//sdk/fidl/fuchsia.hardware.usb.peripheral:fuchsia.hardware.usb.peripheral_llcpp",
    "//sdk/fidl/fuchsia.hardware.usb.virtual.bus:fuchsia.hardware.usb.virtual.bus_llcpp",
    "//zircon/system/ulib/devmgr-integration-test",
  ]
  deps = [
    "//sdk/fidl/fuchsia.sys",
    "//sdk/lib/fdio",
    "//sdk/lib/fit",
    "//sdk/lib/sys/cpp",
    "//src/lib/ddk",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
  ]
  configs += [
    "//build/config/fuchsia:enable_zircon_asserts",
    "//build/config/fuchsia:static_cpp_standard_library",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

executable("test_bin") {
  testonly = true

  output_name = "isolated_devmgr_tests"

  sources = [ "isolated_devmgr_tests.cc" ]

  deps = [
    ":src",
    "//garnet/public/lib/gtest",
    "//sdk/fidl/fuchsia.hardware.ethertap",
    "//sdk/fidl/fuchsia.io:fuchsia.io",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/fidl/fuchsia.virtualaudio",
    "//sdk/lib/fdio",
    "//sdk/lib/fit",
    "//sdk/lib/sys/cpp",
    "//src/lib/ddk",
    "//src/lib/fxl/test:gtest_main",
    "//zircon/public/lib/async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/devmgr-integration-test",
  ]
}

isolated_devmgr_component("ethertap-devmgr") {
  args = [
    "--sys_device=/boot/driver/test-parent-sys.so",
    "--svc_name=fuchsia.example.IsolatedDevmgr",
    "--wait_for=sys/test/tapctl",
  ]

  deps = [
    "//src/connectivity/ethernet/drivers/ethernet",
    "//src/connectivity/ethernet/drivers/ethertap",
    "//src/devices/misc/drivers/test-parent",
  ]
}

isolated_devmgr_component("virtual-audio-devmgr") {
  args = [
    "--sys_device=/boot/driver/platform-bus.so",
    "--svc_name=fuchsia.example.IsolatedDevmgr",
    "--wait_for=sys/platform/00:00:2f/virtual_audio",
  ]

  deps = [ "//src/media/audio/drivers/virtual_audio:virtual_audio_driver" ]
}

driver_bind_rules("crash-device-bind") {
  rules = "crash-device.bind"
  header_output = "crash-device-bind.h"
  deps = [ "//src/devices/bind/test.platform" ]
}

# A device that causes a crash when it's operated on
driver_module("crash-device-driver") {
  output_name = "crash-device"
  sources = [ "crash_device.cc" ]
  deps = [
    ":crash-device-bind",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fidl",
    "//zircon/system/ulib/fzl",
  ]
}

fuchsia_driver_component("crash-device") {
  component_name = "crash-device"
  deps = [ ":crash-device-driver" ]
}

fuchsia_component("isolated-devmgr-tests") {
  testonly = true
  manifest = "isolated-devmgr-tests.cmx"
  deps = [
    ":crash-device",
    ":ethertap-devmgr",
    ":test_bin",
    ":virtual-audio-devmgr",
    "//src/connectivity/ethernet/drivers/ethernet",
    "//src/connectivity/ethernet/drivers/ethertap",
    "//src/devices/board/drivers/integration-test",
    "//src/devices/bus/drivers/platform",
    "//src/devices/rtc/drivers/fallback",
  ]
}

fuchsia_test_package("isolated-devmgr-tests-package") {
  test_components = [ ":isolated-devmgr-tests" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  data_deps = [ ":isolated-devmgr-tests-package" ]
}
