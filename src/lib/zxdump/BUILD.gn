# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import("//build/zircon/migrated_targets.gni")

zx_library("zxdump") {
  sdk = "source"
  sdk_headers = [
    "lib/zxdump/fd-writer.h",
    "lib/zxdump/types.h",
  ]

  public_deps = [
    # <lib/zxdump/types.h> has #include <zircon/types.h>.
    "//zircon/system/public",

    # <lib/zxdump/fd-writer.h> has #include <lib/fitx/result.h>.
    "//zircon/system/ulib/zxc",

    # <lib/zxdump/fd-writer.h> has #include <fbl/unique_fd.h>.
    "//zircon/system/ulib/fbl",
  ]

  sources = [
    "error.cc",
    "fd-writer.cc",
  ]

  deps = []
  if (is_fuchsia) {
    deps += [ "//src/zircon/lib/zircon" ]
  }
}

test("zxdump-unittests") {
  sources = [
    "error-tests.cc",
    "fd-writer-tests.cc",
  ]
  deps = [
    ".",
    "//sdk/lib/fit",
    "//src/lib/fxl/test:gtest_main",
  ]
}

fuchsia_unittest_package("zxdump-tests") {
  deps = [ ":zxdump-unittests" ]
  manifest = "meta/zxdump-unittests.cml"
}

group("tests") {
  testonly = true

  deps = [
    ":zxdump-tests",
    ":zxdump-unittests($host_toolchain)",
  ]
}
