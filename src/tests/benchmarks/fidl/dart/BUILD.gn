# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/dart/dart_test_component.gni")
import("//build/fidl/fidl.gni")
import("//src/dart/testing/dart_test_wrapper_component.gni")
import("//src/tests/benchmarks/fidl/benchmark_suite/benchmark_suite.gni")
import("//tools/fidl/gidl/gidl.gni")

# GIDL Benchmark

benchmark_dir = "$target_gen_dir/benchmark"
benchmark_file = "benchmark.dart"
benchmark_path = "$benchmark_dir/test/$benchmark_file"

gidl("benchmark_suite") {
  type = "benchmark"
  language = "dart"
  inputs = benchmark_suite_gidl_files
  fidl = benchmark_suite_fidl_target
  output = benchmark_path
}

copy("copy_benchmark_component_analysis_options") {
  sources = [ "analysis_options.yaml" ]
  outputs = [ "${benchmark_dir}/test/analysis_options.yaml" ]
}

dart_test_component("benchmark-component") {
  component_name = "dart-fidl-benchmarks"
  manifest = "meta/dart-fidl-benchmarks.cml"
  package_root = benchmark_dir
  sources = [ benchmark_file ]
  deps = [
    ":copy_benchmark_component_analysis_options",
    "//sdk/dart/fidl",
    "//sdk/dart/fuchsia",
    "//sdk/dart/zircon",
    "//sdk/testing/sl4f/client",
    "//src/lib/fidl/dart/gidl",
    "//third_party/dart-pkg/pub/args",
    "//third_party/dart-pkg/pub/test",
    benchmark_suite_fidl_target,
  ]
  non_dart_deps = [ ":benchmark_suite" ]

  pubspec = "pubspec.yaml"
  options_file = "${benchmark_dir}/test/analysis_options.yaml"
}

dart_test_wrapper_component("dart-fidl-benchmarks-wrapper") {
  # Note that build/rust/rustc_binary.gni converts "-" to "_" in binary
  # names, so use underscores in the wrapper_binary name for searchability,
  # so that this matches the name in the .cml file.
  wrapper_binary = "dart_fidl_benchmarks_wrapper"
  manifest = "meta/dart-fidl-benchmarks-wrapper.cml"
  dart_test_component_name = "dart-fidl-benchmarks"
}

fuchsia_test_package("dart-fidl-benchmarks") {
  test_components = [ ":dart-fidl-benchmarks-wrapper" ]
  deps = [ ":benchmark-component" ]
}

group("dart") {
  testonly = true
  deps = [ ":dart-fidl-benchmarks" ]
}
