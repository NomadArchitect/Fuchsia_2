# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/compiled_action.gni")
import("//build/components/fuchsia_package.gni")
import("//build/components/fuchsia_test_component.gni")
import("//build/config.gni")
import("//build/dist/resource.gni")
import("//build/images/assemble_system.gni")
import("//build/images/assembly/update_package.gni")
import("//build/rust/rustc_binary.gni")
import("//src/security/pkg_test/assemblies/build_info.gni")

# Construct a system assembly for use in product security tests.
#
# Parameters:
#
#   board_name (required):
#     [string] board_name forwarded to assemble_system(target_name).
#
#   base_packages (required):
#     [list of labels] base_packages forwarded to assemble_system(target_name).
#
#   system_version_file (required):
#     [string] The file used to designate system version in the assembly's
#     update package and build-info package.
#
#   packaged_assembly_directory (required):
#     [string] The subdirectory in data/assemblies where the
#     packaged-in-a-fuchsia-package copy of assembled artifacts will be stored.
#
#   test_config (required):
#     [string] Path to shared test configuration file that is used by host tools
#     to generate consistent inputs. For example, host tools need to access the
#     domain that should be used for package URLs in the update package.
#
# Public targets:
#
#   ${target_name}:
#     The assemble_system(target_name) { ... } used as a basis for other
#     targets.
#
#   ${target_name}_update_package:
#     The update package for the assembled system.
#
#   ${target_name}_system_resources:
#     The resources required for serving the assembled system as installed on
#     device. Resources reside in package's
#     data/assemblies/${packaged_assembly_directory} directory.
#
#   ${target_name}_update_resources:
#     The resources required for serving the update package associated with the
#     assembled system. Resources reside in package's
#     data/assemblies/${packaged_assembly_directory} directory.

template("assemble_security_pkg_test_system") {
  assert(defined(invoker.board_name),
         "board_name must be defined for $target_name")
  assert(defined(invoker.base_packages),
         "base_packages must be defined for $target_name")
  assert(defined(invoker.system_version_file),
         "system_version_file must be defined for $target_name")
  assert(defined(invoker.packaged_assembly_directory),
         "packaged_assembly_directory must be defined for $target_name")
  assert(defined(invoker.test_config),
         "test_config must be defined for $target_name")

  assembly_name = target_name
  if (current_toolchain == target_toolchain) {
    packaged_assembly_directory = invoker.packaged_assembly_directory
  } else {
    not_needed(invoker, [ "packaged_assembly_directory" ])
  }

  labels = {
    assembly = assembly_name
    assembly_image_assembler = "${assembly_name}_image_assembler"
    assembly_partitions_config = "${assembly_name}_partitions_config"
    empty_pkgfs = "${assembly_name}_empty_pkgfs"
    build_info = "${assembly_name}_build_info"
    repository_config = "${assembly_name}_repository_config"
    packages_json = "${assembly_name}_packages_json"
    update_package = "${assembly_name}_update_package"
    system_resources = "${assembly_name}_system_resources"
    update_resources_group = "${assembly_name}_update_resources_group"
    update_resources_package = "${assembly_name}_update_resources_package"
    verify_update_package_resources =
        "${assembly_name}_verify_update_package_resources"
    update_resources = "${assembly_name}_update_resources"
  }

  directories = {
    assembly_out = get_label_info(":${labels.assembly}", "target_out_dir") +
                   "/${assembly_name}"
    build_info_out = get_label_info(":${labels.assembly}", "target_out_dir") +
                     "/${assembly_name}/build_info"
    update_out = get_label_info(":${labels.assembly}", "target_out_dir") +
                 "/${labels.update_package}"
    update_resources_package_out =
        get_label_info(":${labels.update_resources_package}",
                       "target_out_dir") + "/${labels.update_resources_package}"
  }
  if (current_toolchain == target_toolchain) {
    directories.update_gen = "${directories.update_out}/gen"
  }

  files = {
    assembly_out_packages_json = "${directories.assembly_out}/packages.json"
    packages_json =
        "${directories.assembly_out}/security_pkg_test_packages.json"
    partitions_json = "${directories.assembly_out}_partitions.json"
    images_json = "${directories.assembly_out}/images.json"
    package_manifest_json =
        "${directories.update_resources_package_out}/package_manifest.json"
    update_package_manifest_json =
        "${directories.update_out}/update_package_manifest.json"
    verify_update_package_resources =
        "${target_out_dir}/${labels.verify_update_package_resources}.ok"
    build_info_version = invoker.system_version_file
    test_config = invoker.test_config

    # TODO(fxbug.dev/88453): These should be specified `invoker` to match the
    # configured product.
    build_info_product = build_info_files.product
    build_info_jiri_snapshot = build_info_files.jiri_snapshot

    build_info_board = "${directories.build_info_out}/board"
    build_info_version = invoker.system_version_file
    build_info_latest_commit_date =
        "${directories.build_info_out}/latest-commit-date.txt"
    build_info_minimum_utc_stamp =
        "${directories.build_info_out}/minimum-utc-stamp.txt"
  }
  if (current_toolchain == target_toolchain) {
    files.update_board = "${directories.update_gen}/board"
  }

  # Resource files with both `local` and `packaged` paths.
  if (current_toolchain == target_toolchain) {
    fvm_blk_resource = {
      local = "${directories.assembly_out}/fvm.blk"
      packaged = "data/assemblies/${packaged_assembly_directory}/fvm.blk"
    }
    base_far_resource = {
      local = "${directories.assembly_out}/base.far"
      packaged = "data/assemblies/${packaged_assembly_directory}/base.far"
    }
    zbi_resource = {
      local = "${directories.assembly_out}/${assembly_name}.zbi"
      packaged = "data/assemblies/${packaged_assembly_directory}/update/zbi"
    }
    update_far_resource = {
      local = "${directories.update_out}/update.far"
      packaged =
          "data/assemblies/${packaged_assembly_directory}/update/update.far"
    }
    version_resource = {
      local = files.build_info_version
      packaged = "data/assemblies/${packaged_assembly_directory}/update/version"
    }
    board_resource = {
      local = files.update_board
      packaged = "data/assemblies/${packaged_assembly_directory}/update/board"
    }
    packages_json_resource = {
      local = "${directories.update_gen}/packages.json"
      packaged =
          "data/assemblies/${packaged_assembly_directory}/update/packages.json"
    }
    epoch_json_resource = {
      local = "${directories.update_gen}/epoch.json"
      packaged =
          "data/assemblies/${packaged_assembly_directory}/update/epoch.json"
    }
    if (use_vbmeta) {
      vbmeta_resource = {
        local = "${directories.assembly_out}/${assembly_name}.vbmeta"
        packaged = "data/assemblies/${packaged_assembly_directory}/update/fuchsia.vbmeta"
      }
    }
    firmware_resources = []
    foreach(firmware, firmware_prebuilts) {
      assert(defined(firmware.path), "Firmware path must be specified")
      _firmware_path = "${firmware.path}${firmware_prebuilts_path_suffix}"
      _firmware_file = get_path_info(_firmware_path, "file")
      firmware_resources += [
        {
          local = _firmware_path
          packaged = "data/assemblies/${packaged_assembly_directory}/update/${_firmware_file}"
        },
      ]
    }
  }

  # `assemble_system(...) { base_packages }` requires a package with the name
  # "pkgfs".
  fuchsia_package(labels.empty_pkgfs) {
    testonly = true
    package_name = "pkgfs"
  }

  # Build info to be included in base packages.
  write_file(files.build_info_board, invoker.board_name)
  build_info(labels.build_info) {
    testonly = true
    product = files.build_info_product
    board = files.build_info_board
    version = files.build_info_version
    jiri_snapshot = files.build_info_jiri_snapshot
    latest_commit_date = files.build_info_latest_commit_date
    minimum_utc_stamp = files.build_info_minimum_utc_stamp
  }

  # Main system assembly.
  assemble_system(labels.assembly) {
    testonly = true
    board_name = invoker.board_name
    bootfs_labels = []
    base_packages = [ ":${labels.build_info}" ] + invoker.base_packages
    pkgfs_package_label = ":${labels.empty_pkgfs}"
  }

  # Process packages.json to use a custom domain name. This mirrors the domain
  # binding strategy used in production.
  compiled_action(labels.packages_json) {
    testonly = true
    tool = "//src/security/pkg_test/rewrite_packages_json_domain"
    deps = [ ":${labels.assembly}" ]
    inputs = [
      files.assembly_out_packages_json,
      files.test_config,
    ]
    outputs = [ files.packages_json ]
    args = [
      "--input",
      rebase_path(files.assembly_out_packages_json, root_build_dir),
      "--output",
      rebase_path(files.packages_json, root_build_dir),
      "--test-config",
      rebase_path(files.test_config, root_build_dir),
      "--in-domain",
      "fuchsia.com",
    ]
  }

  # Construct an update package for the system assembly.
  update_package(labels.update_package) {
    testonly = true
    deps = [
      ":${labels.assembly_image_assembler}",
      ":${labels.assembly_partitions_config}",
      ":${labels.packages_json}",
    ]
    partitions = files.partitions_json
    packages = files.packages_json
    system_a = files.images_json
    board_name = board_name
    version_file = invoker.system_version_file
    epoch = "1"
  }

  # TODO(fxbug.dev/88453): Verify that contents of `files.build_info_board` and
  # `files.update_board` are identical.

  # `resource_group` targets should be built on the `target_toolchain`.
  # Other toolchains should depend on the `target_toolchain` outputs.
  if (current_toolchain == target_toolchain) {
    # Resources needed to host a system image.
    resource_group(labels.system_resources) {
      testonly = true
      deps = [ ":${labels.assembly}" ]
      files = [
        {
          source = fvm_blk_resource.local
          dest = fvm_blk_resource.packaged
        },
        {
          source = base_far_resource.local
          dest = base_far_resource.packaged
        },
      ]
    }

    # Resources needed for update package. The bespoke subset of files from
    # system assembly that is included here is checked below.
    resource_group(labels.update_resources_group) {
      testonly = true
      deps = [
        ":${labels.update_package}",

        # Image assembler step in `assemble_system(...)` produces
        # `packages.json`.
        ":${labels.assembly_image_assembler}",
      ]
      files = [
        {
          source = zbi_resource.local
          dest = zbi_resource.packaged
        },
        {
          source = board_resource.local
          dest = board_resource.packaged
        },
        {
          source = packages_json_resource.local
          dest = packages_json_resource.packaged
        },
        {
          source = update_far_resource.local
          dest = update_far_resource.packaged
        },
        {
          source = epoch_json_resource.local
          dest = epoch_json_resource.packaged
        },
        {
          source = version_resource.local
          dest = version_resource.packaged
        },
      ]
      if (use_vbmeta) {
        files += [
          {
            source = vbmeta_resource.local
            dest = vbmeta_resource.packaged
          },
        ]
      }
      foreach(firmware, firmware_resources) {
        files += [
          {
            source = firmware.local
            dest = firmware.packaged
          },
        ]
      }
    }
  } else {
    group(labels.system_resources) {
      testonly = true
      public_deps = [ ":${labels.system_resources}($target_toolchain)" ]
    }

    group(labels.update_resources_group) {
      testonly = true
      public_deps = [ ":${labels.update_resources_group}($target_toolchain)" ]
    }
  }

  # Check update package resources: Step 1:
  # Construct a package that contains update resources.
  fuchsia_package(labels.update_resources_package) {
    testonly = true
    deps = [ ":${labels.update_resources_group}" ]
  }

  # Check update package resources: Step 2:
  # Run script that compares blob hashes in the update resources package
  # (directly above), and the actual update package. The script succeeds when
  # update resources package contains *at least* the blobs in the update
  # package.
  action(labels.verify_update_package_resources) {
    testonly = true
    script =
        "//src/security/pkg_test/assemblies/verify_update_package_resources.py"
    sources = [
      files.package_manifest_json,
      files.update_package_manifest_json,
    ]
    deps = [
      ":${labels.update_package}",
      ":${labels.update_resources_package}",
    ]
    outputs = [ files.verify_update_package_resources ]
    args = [
      "--package-manifest-path",
      rebase_path(files.package_manifest_json, root_build_dir),
      "--update-package-manifest-path",
      rebase_path(files.update_package_manifest_json, root_build_dir),
      "--output",
      rebase_path(files.verify_update_package_resources, root_build_dir),
    ]
  }

  # Public update resources gn target: depends on the update resources and their
  # verification step.
  group(labels.update_resources) {
    testonly = true
    public_deps = [ ":${labels.update_resources_group}" ]
    deps = [ ":${labels.verify_update_package_resources}" ]
  }
}
