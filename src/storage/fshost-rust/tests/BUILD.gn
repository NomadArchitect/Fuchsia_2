# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")
import("//src/storage/fshost/generated_fshost_config.gni")
import("//src/storage/testing/driver_test_realm.gni")
import("//tools/cmc/build/cmc.gni")

# Generates an fshost integration test component.
#
#   fshost_target (required)
#     [string] The build target of the component to test against. The name of this target is also
#     used to determine the component manifest path in the resulting package, so make sure the
#     target doesn't rename the component.
#
#   data_filesystem_format (required)
#     [string] The format of the data partition fshost exports. One of "fxfs", "minfs", or "f2fs".
#
template("fshost_integration_test") {
  assert(defined(invoker.fshost_target),
         "fshost component target not specified")
  assert(defined(invoker.data_filesystem_format),
         "data filesystem format not specified")

  _fshost_component_name = get_label_info(invoker.fshost_target, "name")
  _test_bin = string_replace(target_name, "-", "_")
  _test_bin = "${_test_bin}_bin"
  rustc_test(_test_bin) {
    edition = "2021"
    source_root = "src/main.rs"
    rustenv = [
      "FSHOST_COMPONENT_NAME=${_fshost_component_name}",
      "DATA_FILESYSTEM_FORMAT=${invoker.data_filesystem_format}",
    ]
    sources = [
      "src/main.rs",
      "src/mocks.rs",
    ]
    deps = [
      "//sdk/fidl/fuchsia.boot:fuchsia.boot_rust",
      "//sdk/fidl/fuchsia.fshost:fuchsia.fshost_rust",
      "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
      "//sdk/fidl/fuchsia.logger:fuchsia.logger_rust",
      "//sdk/fidl/fuchsia.process:fuchsia.process_rust",
      "//sdk/fidl/fuchsia.sys:fuchsia.sys_rust",
      "//sdk/lib/device-watcher/rust",
      "//src/lib/fdio/rust:fdio",
      "//src/lib/fidl/rust/fidl",
      "//src/lib/fuchsia",
      "//src/lib/fuchsia-component",
      "//src/lib/fuchsia-component-test",
      "//src/lib/storage/fs_management/rust:fs_management",
      "//src/lib/storage/key-bag",
      "//src/lib/storage/ramdevice_client/rust",
      "//src/lib/storage/vfs/rust:vfs",
      "//src/lib/zircon/rust:fuchsia-zircon",
      "//src/storage/bin/blobfs:blobfs-component",
      "//src/storage/fs_test:test_crypt_service",
      "//src/storage/fxfs:fuchsia.fxfs_rust",
      "//src/storage/fxfs:fxfs_component",
      "//src/storage/fxfs:test_fxfs_component_manifest_shard",
      "//src/storage/fxfs-crypt",
      "//src/storage/testing/rust:storage-isolated-driver-manager",
      "//src/sys/lib/component-events",
      "//third_party/rust_crates:anyhow",
      "//third_party/rust_crates:futures",
      "//third_party/rust_crates:uuid",
    ]
  }

  _shard_target = "${target_name}-shard"
  generated_file(_shard_target) {
    testonly = true
    contents = {
      program = {
        binary = "bin/${_test_bin}"
      }
    }
    outputs = [ "${target_out_dir}/${_shard_target}.json" ]
    output_conversion = "json"
    visibility = [ ":*" ]
  }
  _shard = get_target_outputs(":$_shard_target")
  _shard = _shard[0]

  _manifest_target = "${target_name}-manifest.cml"
  cmc_merge("$_manifest_target") {
    testonly = true
    sources = [
      "meta/fshost_integration_test.cml",
      _shard,
    ]
    deps = [ ":$_shard_target" ]
  }
  _manifest = get_target_outputs(":$_manifest_target")
  _manifest = _manifest[0]

  fuchsia_test_component(target_name) {
    manifest = _manifest
    deps = [
      ":$_manifest_target",
      ":$_test_bin",
      invoker.fshost_target,
    ]
  }
}

cmc_merge("test-fshost-rust.cml") {
  sources = [
    "//src/storage/fshost/meta/base_fshost.cml",
    "//src/storage/fshost/meta/config.cml",
    "//src/storage/fshost/meta/core_fshost.cml",
    "//src/storage/fshost/meta/fxfs_fshost.cml",
    "//src/storage/fshost/meta/rust_fshost.cml",
  ]
}

fuchsia_component("test-fshost-rust") {
  testonly = true
  manifest_outputs = get_target_outputs(":test-fshost-rust.cml")
  manifest = manifest_outputs[0]
  manifest_deps = [ ":test-fshost-rust.cml" ]
  deps = [
    ":config",
    "..:fshost-rust",
    "//src/storage/fxfs:fxfs_component",
    "//src/storage/fxfs-crypt",
  ]
}

fshost_integration_test("fshost-cpp-integration-test-fxfs") {
  fshost_target = "//src/storage/fshost/testing:test-fshost-fxfs"
  data_filesystem_format = "fxfs"
}

fshost_integration_test("fshost-cpp-integration-test-minfs") {
  fshost_target = "//src/storage/fshost/testing:test-fshost"
  data_filesystem_format = "minfs"
}

fshost_integration_test("fshost-rust-integration-test") {
  fshost_target = ":test-fshost-rust"
  data_filesystem_format = "fxfs"
}

fuchsia_test_package("fshost_integration_tests") {
  # Currently required because the data_formatted test causes Fxfs to log:
  #   ERROR: ... handle_start failed error=Journal replay failed
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  test_components = [
    ":fshost-cpp-integration-test-fxfs",
    ":fshost-cpp-integration-test-minfs",
    ":fshost-rust-integration-test",
  ]
  deps = [ "//src/storage/testing:storage_driver_test_realm" ]
}

generated_fshost_config("config") {
  component_name = "test-fshost-rust"
}

group("tests") {
  testonly = true
  deps = [ ":fshost_integration_tests" ]
}
