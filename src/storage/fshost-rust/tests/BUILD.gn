# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")
import("//src/storage/fshost/generated_fshost_config.gni")
import("//src/storage/testing/driver_test_realm.gni")
import("//tools/cmc/build/cmc.gni")

fshost_integration_test_deps = [
  "//sdk/fidl/fuchsia.boot:fuchsia.boot_rust",
  "//sdk/fidl/fuchsia.fshost:fuchsia.fshost_rust",
  "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
  "//sdk/fidl/fuchsia.logger:fuchsia.logger_rust",
  "//sdk/fidl/fuchsia.process:fuchsia.process_rust",
  "//sdk/fidl/fuchsia.sys:fuchsia.sys_rust",
  "//sdk/lib/device-watcher/rust",
  "//src/lib/fdio/rust:fdio",
  "//src/lib/fidl/rust/fidl",
  "//src/lib/fuchsia",
  "//src/lib/fuchsia-component",
  "//src/lib/fuchsia-component-test",
  "//src/lib/fuchsia-fs",
  "//src/lib/storage/fs_management/rust:fs_management",
  "//src/lib/storage/key-bag",
  "//src/lib/storage/ramdevice_client/rust",
  "//src/lib/storage/vfs/rust:vfs",
  "//src/lib/zircon/rust:fuchsia-zircon",
  "//src/storage/bin/blobfs-component:blobfs_rust_fs_management",
  "//src/storage/fs_test:test_crypt_service",
  "//src/storage/fxfs:fuchsia.fxfs_rust",
  "//src/storage/fxfs:fxfs_component",
  "//src/storage/fxfs:test_fxfs_component_manifest_shard",
  "//src/storage/fxfs-crypt",
  "//src/storage/testing/rust:storage-isolated-driver-manager",
  "//src/sys/lib/component-events",
  "//third_party/rust_crates:anyhow",
  "//third_party/rust_crates:futures",
  "//third_party/rust_crates:uuid",
]

fshost_integration_test_sources = [
  "src/main.rs",
  "src/mocks.rs",
]

rustc_test("fshost_cpp_integration_test") {
  edition = "2021"
  source_root = "src/main.rs"
  features = [ "fshost_cpp" ]
  sources = fshost_integration_test_sources
  deps = fshost_integration_test_deps
}

rustc_test("fshost_rust_integration_test") {
  edition = "2021"
  source_root = "src/main.rs"
  features = [ "fshost_rust" ]
  sources = fshost_integration_test_sources
  deps = fshost_integration_test_deps
}

cmc_merge("fshost_cpp_integration_test.cml") {
  sources = [
    "meta/fshost_cpp_program.cml",
    "meta/fshost_integration_test.cml",
  ]
}

cmc_merge("fshost_rust_integration_test.cml") {
  sources = [
    "meta/fshost_integration_test.cml",
    "meta/fshost_rust_program.cml",
  ]
}

fuchsia_test_component("fshost_cpp_integration_test_component") {
  component_name = "fshost_cpp_integration_test_component"
  manifest_outputs = get_target_outputs(":fshost_cpp_integration_test.cml")
  manifest = manifest_outputs[0]
  deps = [ ":fshost_cpp_integration_test" ]
  manifest_deps = [ ":fshost_cpp_integration_test.cml" ]
}

storage_driver_test_realm_v2_component(
    "fshost_rust_integration_test_component") {
  component_name = "fshost_rust_integration_test_component"
  manifest_outputs = get_target_outputs(":fshost_rust_integration_test.cml")
  manifest = manifest_outputs[0]
  deps = [ ":fshost_rust_integration_test" ]
  manifest_deps = [ ":fshost_rust_integration_test.cml" ]
}

cmc_merge("test-fshost-rust.cml") {
  sources = [
    "//src/storage/fshost/meta/base_fshost.cml",
    "//src/storage/fshost/meta/config.cml",
    "//src/storage/fshost/meta/core_fshost.cml",
    "//src/storage/fshost/meta/fxfs_fshost.cml",
    "//src/storage/fshost/meta/rust_fshost.cml",
  ]
}

fuchsia_component("test-fshost-rust") {
  testonly = true
  manifest_outputs = get_target_outputs(":test-fshost-rust.cml")
  manifest = manifest_outputs[0]
  manifest_deps = [ ":test-fshost-rust.cml" ]
  deps = [
    ":config",
    "..:fshost-rust",
    "//src/storage/fxfs:fxfs_component",
    "//src/storage/fxfs-crypt",
  ]
}

fuchsia_test_package("fshost_integration_tests") {
  # Currently required because the data_formatted test causes Fxfs to log:
  #   ERROR: ... handle_start failed error=Journal replay failed
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  test_components = [
    ":fshost_cpp_integration_test_component",
    ":fshost_rust_integration_test_component",
  ]
  deps = [
    ":fshost_cpp_integration_test_component",
    ":fshost_rust_integration_test_component",
    ":test-fshost-rust",
    "//src/storage/fshost/testing:test-fshost-fxfs",
    "//src/storage/testing:storage_driver_test_realm",
  ]
}

generated_fshost_config("config") {
  component_name = "test-fshost-rust"
}

group("tests") {
  testonly = true
  deps = [ ":fshost_integration_tests" ]
}
