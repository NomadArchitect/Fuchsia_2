# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("fshost-rust") {
  output_name = "fshost_rust"
  edition = "2021"
  deps = [
    "//sdk/fidl/fuchsia.device:fuchsia.device_rust",
    "//sdk/fidl/fuchsia.fshost:fuchsia.fshost_rust",
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_rust",
    "//sdk/fidl/fuchsia.hardware.block.volume:fuchsia.hardware.block.volume_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-runtime",
    "//src/lib/storage/fs_management/rust:fs_management",
    "//src/lib/storage/fuchsia-watch",
    "//src/lib/storage/vfs/rust:vfs",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/storage/fshost:fshost_config_rust",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
  ]
  test_deps = [
    "//sdk/lib/device-watcher/rust",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/storage/ramdevice_client/rust",
    "//src/storage/testing/rust:storage-isolated-driver-manager",
    "//third_party/rust_crates:uuid",
  ]
  sources = [
    "src/config.rs",
    "src/device.rs",
    "src/device/constants.rs",
    "src/environment.rs",
    "src/main.rs",
    "src/manager.rs",
    "src/matcher.rs",
    "src/service.rs",
    "src/watcher.rs",
  ]

  with_unit_tests = true
}

fuchsia_unittest_component("fshost_rust_tests_component") {
  component_name = "fshost_rust_tests"
  deps = [ ":fshost-rust_test" ]
}

fuchsia_test_package("fshost_rust_tests") {
  test_components = [ ":fshost_rust_tests_component" ]
}

group("tests") {
  testonly = true
  public_deps = [
    ":fshost_rust_tests",
    "tests:tests",
  ]
}
