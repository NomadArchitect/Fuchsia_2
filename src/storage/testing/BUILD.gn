# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//src/lib/isolated_devmgr/v2_component/isolated_devmgr.gni")

group("storage_drivers") {
  testonly = true
  deps = [
    "//src/devices/block/drivers/core",
    "//src/devices/block/drivers/ftl",
    "//src/devices/block/drivers/gpt",
    "//src/devices/block/drivers/ramdisk:ramdisk",
    "//src/devices/block/drivers/zxcrypt",
    "//src/devices/nand/drivers/ram-nand",
    "//src/storage/fvm/driver",
  ]
}

isolated_devmgr_v2_component("isolated-devmgr-component") {
}

fuchsia_package("storage-isolated-devmgr") {
  testonly = true
  deps = [
    ":isolated-devmgr-component",
    ":storage_drivers",
  ]
}

source_set("storage_driver_test_support") {
  testonly = true
  sources = [
    "fvm.cc",
    "fvm.h",
    "ram_disk.cc",
    "ram_disk.h",
  ]
  deps = [
    "//sdk/fidl/fuchsia.device",
    "//sdk/fidl/fuchsia.device:fuchsia.device_llcpp",
    "//sdk/fidl/fuchsia.sys2",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//zircon/public/lib/sync",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/fs-management",
  ]
  public_deps = [
    "//src/lib/isolated_devmgr/v2_component:client",
    "//zircon/public/lib/zxc",
    "//zircon/system/ulib/ramdevice-client",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

group("isolated-devmgr") {
  testonly = true
  deps = [ ":storage-isolated-devmgr" ]
}
