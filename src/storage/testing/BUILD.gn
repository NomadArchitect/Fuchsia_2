# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//src/lib/isolated_devmgr/v2_component/isolated_devmgr.gni")

group("storage_drivers") {
  testonly = true
  deps = [
    "//src/devices/block/drivers/core",
    "//src/devices/block/drivers/ftl",
    "//src/devices/block/drivers/gpt",
    "//src/devices/block/drivers/ramdisk:ramdisk",
    "//src/devices/block/drivers/zxcrypt",
    "//src/devices/nand/drivers/ram-nand",
    "//src/storage/fvm/driver",
  ]
}

isolated_devmgr_v2_component("isolated-devmgr-component") {
  deps = [
    ":storage_drivers",
    "//src/devices/bus/drivers/platform",
  ]
}

fuchsia_package("storage-isolated-devmgr") {
  testonly = true
  deps = [ ":isolated-devmgr-component" ]
}

group("driver_test_realm") {
  testonly = true
  deps = [
    ":storage_drivers",
    "//sdk/lib/driver_test_realm/platform",
  ]
}

fuchsia_component("component") {
  component_name = "storage_driver_test_realm"
  manifest = "meta/storage_driver_test_realm.cml"
}

fuchsia_package("storage_driver_test_realm") {
  testonly = true
  deps = [
    ":component",
    ":driver_test_realm",
  ]
}

source_set("storage_driver_test_support") {
  testonly = true
  sources = [
    "fvm.cc",
    "fvm.h",
    "ram_disk.cc",
    "ram_disk.h",
    "zxcrypt.cc",
    "zxcrypt.h",
  ]
  deps = [
    "//sdk/fidl/fuchsia.device",
    "//sdk/fidl/fuchsia.device:fuchsia.device_llcpp",
    "//sdk/fidl/fuchsia.sys2",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/security/zxcrypt:zxcrypt-client",
    "//src/storage/lib/utils:topological_path",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/fs-management",
    "//zircon/system/ulib/sync",
  ]
  public_deps = [
    "//zircon/system/ulib/ramdevice-client",
    "//zircon/system/ulib/zxc",
  ]
}

group("isolated-devmgr") {
  testonly = true
  deps = [
    ":storage-isolated-devmgr",
    ":storage_driver_test_realm",
  ]
}
