# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import("//src/lib/isolated_devmgr/v2_component/isolated_devmgr.gni")

group("test") {
  testonly = true
  deps = [ ":gpt-tests" ]
}

test("gpt-unit-test") {
  sources = [
    "gpt-tests.cc",
    "main.cc",
  ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_c",
    "//sdk/lib/fdio",
    "//src/storage/gpt",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",

    # TODO(https://fxbug.dev/74373) replace once `RecursiveWaitForFile` is
    # extracted to a library.
    "//zircon/system/ulib/devmgr-integration-test",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/mbr",
    "//zircon/system/ulib/ramdevice-client",
    "//zircon/third_party/ulib/cksum",
  ]
}

isolated_devmgr_v2_component("ramdisk-devmgr") {
  deps = [
    "//src/devices/block/drivers/core",
    "//src/devices/block/drivers/ramdisk",
    "//src/devices/bus/drivers/platform",
  ]
}

fuchsia_test_component("gpt-unit-test-component") {
  component_name = "gpt-unit-test"
  manifest = "meta/gpt-unit-test.cml"
  deps = [ ":gpt-unit-test" ]
}

fuchsia_test_package("gpt-tests") {
  deps = [ ":ramdisk-devmgr" ]
  test_components = [ ":gpt-unit-test-component" ]
}
