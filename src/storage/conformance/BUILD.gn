# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")

# Full list of io1 conformance tests should be included here. Add deps to the
# test packages for additional filesystem conformance tests and test harnesses.
group("tests") {
  testonly = true
  deps = [
    # Disabled for now until io.test.fidl changes.
    #":fdio_test",
    ":io_conformance_util_tests",
    "//src/storage/conformance/conformance_harness/cppvfs:cppvfs-io1-conformance-test",
    "//src/storage/conformance/conformance_harness/minfs:minfs-io1-conformance-test",
    "//src/storage/conformance/conformance_harness/rustvfs:rustvfs-io1-conformance-test",
    "//src/storage/conformance/conformance_harness/sdkcpp:sdkcpp-io1-conformance-test",
  ]
}

rustc_library("io_conformance_util") {
  name = "io_conformance_util"
  with_unit_tests = true
  edition = "2021"
  testonly = true
  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component-rustc",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl-rustc",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.io.test:fuchsia.io.test-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
  ]

  sources = [
    "src/flags.rs",
    "src/lib.rs",
    "src/test_harness.rs",
  ]
}

fuchsia_unittest_package("io_conformance_util_tests") {
  deps = [ ":io_conformance_util_test" ]
}

rustc_test("io1_conformance_test") {
  name = "io1_conformance_test"
  edition = "2021"
  source_root = "io1_tests.rs"
  deps = [
    ":io_conformance_util",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.io.test:fuchsia.io.test-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:libc",
  ]

  sources = [ "io1_tests.rs" ]
}
