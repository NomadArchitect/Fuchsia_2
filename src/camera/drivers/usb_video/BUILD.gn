# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/driver_package.gni")
import("//src/sys/build/components.gni")

group("tests") {
  testonly = true
  deps = [
    ":usb_video_bind_test",
    ":uvc_format_unittest",
  ]
}

bind_rules("usb_video_bind") {
  rules = "usb_video.bind"
  output = "bind.h"
  tests = "bind_tests.json"
  deps = [
    "//src/devices/bind/fuchsia.usb",
    "//src/devices/bind/fuchsia.usb.video",
  ]
}

source_set("lib") {
  sources = [
    "camera_control_impl.cc",
    "camera_control_impl.h",
    "usb_video.cc",
    "usb_video.h",
    "usb_video_stream.cc",
    "usb_video_stream.h",
    "uvc_format.cc",
    "uvc_format.h",
    "video_util.c",
    "video_util.h",
  ]
  deps = [
    ":usb_video_bind",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/syslog/cpp",
    "//sdk/lib/syslog/cpp:backend_legacy",
    "//src/devices/lib/driver",
    "//src/lib/listnode",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/safemath",
    "//zircon/public/lib/zircon-internal",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
  public_deps = [
    "//sdk/banjo/fuchsia.hardware.usb",
    "//sdk/fidl/fuchsia.camera",
    "//sdk/fidl/fuchsia.hardware.camera:fuchsia.hardware.camera_c",
    "//src/devices/usb/lib/usb",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/fzl",
  ]
}

driver_module("usb_video_driver") {
  output_name = "usb_video"
  deps = [
    ":lib",
    "//sdk/lib/syslog/cpp:backend_legacy",
  ]
}

driver_package("usb_video") {
  deps = [ ":usb_video_driver" ]

  drivers = [
    {
      name = "usb_video.so"
    },
  ]
}

executable("uvc_format_unittest_bin") {
  testonly = true
  sources = [ "uvc_format_unittest.cc" ]
  deps = [
    ":lib",
    "//src/devices/testing/fake_ddk",
    "//zircon/public/lib/zxtest",
  ]
}

fuchsia_unittest_package("uvc_format_unittest") {
  deps = [ ":uvc_format_unittest_bin" ]
}
