# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/images/vboot/vboot.gni")
import("//zircon/kernel/phys/efi/efi_executable.gni")

source_set("lib") {
  sources = [
    "device_path.cc",
    "device_path.h",
    "gpt.cc",
    "gpt.h",
    "utils.cc",
    "utils.h",
  ]

  public_deps = [
    "//sdk/lib/fit",
    "//zircon/kernel/lib/efi",
    "//zircon/kernel/phys/efi:main",
    "//zircon/kernel/phys/efi:protocol",
  ]

  if (is_kernel) {
    public_deps += [
      "//zircon/kernel/lib/ktl",
      "//zircon/kernel/lib/libc",
    ]
  }
}

efi_executable("gigaboot") {
  sources = [
    "main.cc",
    "sysdeps.cc",
    "zircon_boot_ops.cc",
    "zircon_boot_ops.h",
  ]

  deps = [
    ":lib",
    "//src/firmware/lib/zircon_boot:in_tree",
    "//src/lib/zbitl",
    "//zircon/kernel/lib/ktl",
    "//zircon/kernel/lib/libc",
    "//zircon/third_party/ulib/cksum",
  ]

  cflags = [ "-DAVB_COMPILATION" ]
}

# Create a bootable gigaboot esp image.
esp("esp") {
  output_name = "gigaboot_cpp"
  gigaboot_target = ":gigaboot"
  gigaboot_bin =
      get_label_info(
          ":gigaboot(//zircon/kernel/phys/efi:kernel.efi_$current_cpu)",
          "root_out_dir") + "/gigaboot.efi"
}
