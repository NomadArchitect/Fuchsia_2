# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/go/go_binary.gni")
import("//build/go/go_library.gni")
import("//build/go/go_test.gni")
import("//build/host.gni")
import("//tools/size_checker/size_checker_input.gni")

group("cmd") {
  testonly = true
  public_deps = [
    ":host",
    ":tests",
  ]
}

generated_file("size_checker_json") {
  outputs = [ "$root_build_dir/size_checker.json" ]
  contents = size_checker_input
  output_conversion = "json"
}

go_library("size_checker_lib") {
  sources = [
    "size_checker.go",
    "size_checker_test.go",
  ]
}

go_binary("size_checker") {
  gopackage = "go.fuchsia.dev/fuchsia/tools/size_checker/cmd"

  deps = [ ":size_checker_lib" ]

  non_go_deps = [ ":size_checker_json" ]
}

if (is_host) {
  go_test("size_checker_tests") {
    gopackages = [ "go.fuchsia.dev/fuchsia/tools/size_checker/cmd" ]
    deps = [ ":size_checker_lib" ]
  }
}

install_host_tools("host") {
  deps = [
    ":size_checker",
    "//src/storage/tools/blobfs-compression",
  ]
  outputs = [
    "size_checker",
    "blobfs-compression",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":size_checker_tests($host_toolchain)",

    # To be able to run `fx test`.
    "//tools/symbolizer($host_toolchain)",
  ]
}
