# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/host.gni")
import("//build/rust/rustc_binary.gni")
import("//build/sdk/sdk_host_tool.gni")
rustc_binary("jq5") {
  with_unit_tests = true
  deps = [
    "//src/lib/fuchsia-async",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:json5format",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:serde_json5",
    "//third_party/rust_crates:structopt",
  ]
  sources = [
    "src/main.rs",
    "src/reader.rs",
    "src/traverser.rs",
  ]
  if (is_host) {
    test_deps = [ ":jq_for_test($host_toolchain)" ]
  }
}
group("tests") {
  testonly = true

  # TODO(73269) Find a way to rewrite so that tests pass on all variants,
  # rather than simply skipping tests on asan variants.
  if (select_variant + [ "host_asan-ubsan" ] - [ "host_asan-ubsan" ] ==
      select_variant) {
    deps = [ ":jq5_test" ]
  }
}
install_host_tools("install") {
  deps = [ ":jq5" ]
  outputs = [ "jq5" ]
}

if (is_host) {
  host_test_data("jq_for_test") {
    sources = [ "//prebuilt/third_party/jq/${host_platform}/bin/jq" ]
    outputs = [ "${root_build_dir}/jq5_tests/jq" ]
    visibility = [ ":*" ]
  }
}
