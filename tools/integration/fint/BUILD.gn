# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_library.gni")
import("//build/go/go_test.gni")
import("//build/go/golden_go_proto.gni")

golden_go_proto("context") {
  sources = [ "proto/context.proto" ]
}
golden_go_proto("set_artifacts") {
  sources = [ "proto/set_artifacts.proto" ]
}
golden_go_proto("static") {
  sources = [ "proto/static.proto" ]
  generate_descriptor_set = true
}

go_library("proto_lib") {
  source_dir = "proto"
  sources = [
    "context.pb.go",
    "set_artifacts.pb.go",
    "static.pb.go",
  ]
  deps = [
    # TODO(https://fxbug.dev/70570): Remove this when we're on protoc 3.14.0 or higher.
    "//third_party/golibs:github.com/golang/protobuf",
    "//third_party/golibs:google.golang.org/protobuf",
  ]
  non_go_deps = [
    ":context",
    ":set_artifacts",
    ":static",
  ]
}

go_library("filetype_lib") {
  sources = [
    "filetype/filetype.go",
    "filetype/filetype_test.go",
  ]
}

go_test("filetype_tests") {
  gopackages = [ "go.fuchsia.dev/fuchsia/tools/integration/fint/filetype" ]
  deps = [ ":filetype_lib" ]
}

go_library("fint_lib") {
  sources = [
    "build.go",
    "build_test.go",
    "common.go",
    "parse.go",
    "parse_test.go",
    "set.go",
    "set_test.go",
    "should_build.go",
    "should_build_test.go",
  ]

  deps = [
    ":filetype_lib",
    ":proto_lib",
    "//third_party/golibs:github.com/google/go-cmp",
    "//third_party/golibs:github.com/google/subcommands",
    "//third_party/golibs:google.golang.org/protobuf",
    "//tools/build",
    "//tools/lib/color",
    "//tools/lib/command",
    "//tools/lib/hostplatform",
    "//tools/lib/isatty",
    "//tools/lib/jsonutil",
    "//tools/lib/logger",
    "//tools/lib/osmisc",
    "//tools/lib/runner",
  ]
}

go_test("fint_lib_tests") {
  gopackages = [ "go.fuchsia.dev/fuchsia/tools/integration/fint" ]
  deps = [ ":fint_lib" ]
}

group("tests") {
  testonly = true
  deps = [
    ":filetype_tests",
    ":fint_lib_tests",
  ]
}
