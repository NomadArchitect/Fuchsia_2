# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/compiled_action.gni")
import("//build/host.gni")

# Compile a configuration value file.
#
# Parameters
#
#   cm (required)
#     Compiled manifest for which the value file should be compiled.
#     Type: path
#
#   value_file (required)
#     A JSON5 file containing the configuration values to be compiled.
#     Type: path
#
#   deps (optional)
#   metadata (optional)
#   output_name (optional)
#   testonly (optional)
#   visibility (optional)
#     Standard GN meaning.
template("cvf") {
  input_manifest = invoker.cm
  value_file = invoker.value_file

  compiled_action(target_name) {
    forward_variables_from(invoker,
                           [
                             "deps",
                             "metadata",
                             "output_name",
                             "testonly",
                             "visibility",
                           ])

    if (!defined(output_name)) {
      output_name = target_name
    }

    tool = "//tools/configc"
    tool_output_name = "configc"

    compiled_output = "${target_out_dir}/${output_name}.cvf"
    inputs = [
      input_manifest,
      value_file,
    ]
    outputs = [ compiled_output ]

    args = [
      "cvf",
      "--cm",
      rebase_path(input_manifest, root_build_dir),
      "--values",
      rebase_path(value_file, root_build_dir),
      "--output",
      rebase_path(compiled_output, root_build_dir),
    ]

    if (!defined(deps)) {
      deps = []
    }

    deps += [ "//tools/cmc/build/restricted_features:structured_config" ]
  }
}

rust_dir = "//prebuilt/third_party/rust/$host_platform"

# Generate the FIDL client library source files for a configuration declaration.
#
# Parameters
#
#   compiled_manifest (required)
#     Compiled manifest for which the source files should be generated.
#     Type: path
#
#   name (optional)
#     Name for the generated FIDL library. If not specified, the target name is used.
#     Type: string
#
#   deps (optional)
#   metadata (optional)
#   testonly (optional)
#   visibility (optional)
#     Standard GN meaning.
template("fidl_config_client_lib_source") {
  assert(defined(invoker.compiled_manifest),
         "the path to a compiled component manifest must be specified")
  compiled_manifest = invoker.compiled_manifest

  compiled_action(target_name) {
    forward_variables_from(invoker,
                           [
                             "deps",
                             "metadata",
                             "name",
                             "testonly",
                             "visibility",
                           ])

    if (!defined(name)) {
      name = target_name
    }

    tool = "//tools/configc"
    tool_output_name = "configc"

    fidl_format_tool_label = "//tools/fidl/fidlc:fidl-format($host_toolchain)"
    fidl_format_tool_path =
        get_label_info(fidl_format_tool_label, "root_out_dir") + "/fidl-format"

    inputs = [
      compiled_manifest,
      fidl_format_tool_path,
    ]

    source_file_path = "${target_out_dir}/${name}.fidl"

    args = [
      "fidl",
      "--cm",
      rebase_path(compiled_manifest, root_build_dir),
      "--output",
      rebase_path(source_file_path, root_build_dir),
      "--fidl-format",
      rebase_path(fidl_format_tool_path, root_build_dir),
    ]

    if (!defined(deps)) {
      deps = []
    }

    deps += [
      "//tools/cmc/build/restricted_features:structured_config",
      "//tools/fidl/fidlc:fidl-format($host_toolchain)",
    ]

    outputs = [ source_file_path ]
  }
}

# Generate the Rust client library source files for a configuration declaration.
#
# Parameters
#
#   compiled_manifest (required)
#     Compiled manifest for which the source files should be generated.
#     Type: path
#
#   name (optional)
#     Name for the generated Rust library. If not specified, the target name is used.
#     Type: string
#
#   deps (optional)
#   metadata (optional)
#   output_name (optional)
#   testonly (optional)
#   visibility (optional)
#     Standard GN meaning.
template("rust_config_client_lib_source") {
  assert(defined(invoker.compiled_manifest),
         "the path to a compiled component manifest must be specified")
  compiled_manifest = invoker.compiled_manifest

  compiled_action(target_name) {
    forward_variables_from(invoker,
                           [
                             "deps",
                             "metadata",
                             "name",
                             "testonly",
                             "visibility",
                           ])

    if (!defined(name)) {
      name = target_name
    }

    tool = "//tools/configc"
    tool_output_name = "configc"

    inputs = [
      compiled_manifest,
      "//rustfmt.toml",
      "$rust_dir/bin/rustfmt",

      # Ensure bindings are regenerated after Rust toolchain rolls.
      "$rust_dir/.versions/rust.cipd_version",
    ]

    source_file_path = "${target_out_dir}/${name}.rs"

    args = [
      "rust",
      "--cm",
      rebase_path(compiled_manifest, root_build_dir),
      "--output",
      rebase_path(source_file_path, root_build_dir),
      "--rustfmt",
      rebase_path("$rust_dir/bin/rustfmt", root_build_dir),
      "--rustfmt-config",
      rebase_path("//rustfmt.toml", root_build_dir),
    ]

    if (!defined(deps)) {
      deps = []
    }

    deps += [ "//tools/cmc/build/restricted_features:structured_config" ]

    outputs = [ source_file_path ]
  }
}
