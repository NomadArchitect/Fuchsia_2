# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/dart/config.gni")
import("//build/flutter/config.gni")

# This has a group corresponding to each product name.  It gives the
# additional base packages that should be included in the build done by
# a bot to run tests and build-time checks to cover that product's code.

group("bringup") {
  testonly = true
  deps = [ "//bundles/bringup:tests" ]
}

# TODO(fxbug.dev/105827): Remove group when all bringup tests pass with DFv2 is
# enabled and all builder bots with DFv2 do not include this group into their
# base package set.
group("bringup_dfv2") {
  testonly = true
  deps = [ "//bundles/bringup:tests_dfv2" ]
}

# This group contains tests that don't depend on any known e2e test libraries.
# This happens to exclude unit tests for these e2e libraries, so this group is
# not meant to be used directly. It serves as an intermediate group for
# constructing `core_no_e2e` below.
group("no_e2e_dep") {
  testonly = true
  assert_no_deps = e2e_test_libs

  deps = [
    "//build:tests",
    "//bundles:tools",
    "//bundles/packages/tests:all",
    "//examples",
    "//examples:tests",
    "//scripts:tests",
    "//scripts/sdk/gn:tests",
    "//src/diagnostics:tests",
    "//src/media/audio/bundles:virtual_audio_driver",
    "//src/media/bundles:examples",
    "//src/media/bundles:services",
    "//src/media/bundles:tools",
    "//third_party/go:tests",
    "//tools",

    # The following groups have known e2e tests explicitly excluded.
    "//bundles/fidl:tests_no_e2e",
    "//sdk:tests_no_e2e",
    "//src:src_no_e2e",
    "//tools:tests_no_e2e",
  ]
}

# This group contains no e2e tests. Tests in this group:
#
# * Either doesn't transitively depend on any known e2e test libraries;
# * Or are unit tests for known e2e test libraries.
group("core_no_e2e") {
  testonly = true
  deps = [ ":no_e2e_dep" ] + e2e_lib_unit_tests
}

group("core") {
  testonly = true
  deps = [
    ":core_no_e2e",

    # Add in e2e tests excluded from `no_e2e`.
    "//bundles/fidl:tests",
    "//sdk:tests",
    "//src",
    "//tools",
  ]

  # Ensure paths to known e2e test libs are always up-to-date.
  deps += e2e_test_libs
}

# WARNING: Known to infrastructure.
group("terminal") {
  testonly = true
}

# Host-only tests.
workstation_host_tests = [
  "//src/experiences/tests/e2e:test($host_toolchain)",
  "//src/experiences/tests/performance:test($host_toolchain)",
]

# Host-only tests that only pass in DFv1 and not DFv2.
# TODO (fxbug.dev/105182): Remove once it is empty.
workstation_host_tests_dfv1 = [
  # TODO(fxbug.dev/105182): Move this test to `workstation_tests` when it is
  # passing with DFv2 enabled.
  "//src/experiences/tests/chrome:test($host_toolchain)",
]

group("workstation_host_tests") {
  testonly = true
  deps = workstation_host_tests

  # TODO(fxbug.dev/105182): Remove when `workstation_host_tests_dfv1` is empty.
  deps += workstation_host_tests_dfv1
}

# TODO(fxbug.dev/105182): Remove when `workstation_host_tests_dfv1` is empty and
# all builder bots with DFv2 include `bundles/buildbot:workstation_host` instead
# of `bundles/buildbot:workstation_host_dfv2` into their host package set.
group("workstation_host_tests_dfv2") {
  testonly = true
  deps = workstation_host_tests
}

workstation_tests = [
  # Only for products in which Flutter is supported.
  "//src/tests/intl/timezone:tests-for-flutter",

  # CFv2 test framework.
  "//src/sys/run_test_suite",

  # Used for Chromium tests
  "//src/testing/fidl/intl_property_manager",

  # E2E tests w/ default toolchain that do not produce Fuchsia packages, only host bins.
  "//src/tests/end_to_end/sl4f:test",
  "//src/tests/end_to_end/time_sync:test",

  # Other e2e dep bundles
  "//src/experiences/tests/e2e:end_to_end_deps",
  "//src/experiences/tests/performance:performance_deps",
  "//src/tests/end_to_end/bundles:end_to_end_deps",

  # Integration tests
  "//src/ui/tests:workstation_tests",

  ### Host-only deps below here ###
  "//src/experiences/tests/e2e:test($host_toolchain)",
  "//src/experiences/tests/performance:test($host_toolchain)",

  # TODO(fxbug.dev/102651): Remove these tests when Subpackages is made
  # generally available. Until then, subpackage tests will only run in CQ if
  # added to the workstation_eng packages, here:
  "//examples/components/routing/with_subpackages:tests",
  "//examples/components/subpackages:tests",
]

# TODO(https://fxbug.dev/94919) split this into target/host groups and move to //products
group("workstation") {
  testonly = true
  deps = workstation_tests

  # Remove when all builder bots that use the workstation_eng product explicitly
  # include workstation_host into their host labels within their fint
  # parameters.
  deps += [ ":workstation_host_tests" ]
}

# TODO(fxbug.dev/105182): Remove when `workstation_host_dfv2` is no longer a
# dependency and all builder bots with DFv2 enabled include `workstation`
# instead of `workstation_dfv2` into their base package set.
group("workstation_dfv2") {
  testonly = true
  deps = workstation_tests

  # Remove when all builder bots that use the workstation_eng_dfv2 product
  # explicitly include workstation_host_dfv2 into their host labels within their
  # fint parameters.
  deps += [ ":workstation_host_tests_dfv2" ]
}

# "host-tests" targets are for tests which depend on package artifacts such as
# the FVM. They should be included using `fx set --with-host`.

# This group contains no e2e tests. Tests in this group:
#
# * Either doesn't transitively depend on any known e2e test libraries;
# * Or are unit tests for known e2e test libraries.
# TODO(https://fxbug.dev/93763) use an underscore to match source code layout guidelines
group("host-tests_no_e2e") {
  testonly = true
  assert_no_deps = e2e_test_libs
  deps = [
    "//build/assembly:host_tests($host_toolchain)",
    "//build/python:host_tests($host_toolchain)",
    "//src/lib/assembly:host_tests($host_toolchain)",
    "//src/security:host_tests($host_toolchain)",
    "//src/tests/assembly:host_tests($host_toolchain)",
  ]
}

# TODO(https://fxbug.dev/93763) use an underscore to match source code layout guidelines
group("host-tests") {
  testonly = true
  deps = [ ":host-tests_no_e2e" ]

  if (is_linux) {
    deps += [
      "//src/tests/installer:tests",
      "//tools/fvdl/e2e:tests",
    ]
  }
}
