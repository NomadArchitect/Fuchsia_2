# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import("//build/zircon/zx_library.gni")

syslog_headers = [
  "lib/syslog/global.h",
  "lib/syslog/logger.h",
  "lib/syslog/wire_format.h",
]

zx_library("syslog") {
  sdk = "shared"
  sdk_publishable = true
  sdk_headers = syslog_headers

  sources = [
    "fx_logger.cc",
    "global.cc",
    "legacy.cc",
    "logger.cc",
  ]
  deps = [
    ":helpers",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_llcpp",
    "//sdk/lib/syslog:client_includes",
    "//sdk/lib/syslog/cpp:cpp-macros",
    "//sdk/lib/syslog/structured_backend",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/service:service-llcpp",
    "//zircon/system/ulib/zx",
  ]
  runtime_deps = [
    "//sdk/lib/fdio:fdio_sdk",
    "//zircon/system/ulib/trace-engine:trace-engine_sdk",
  ]
}

zx_library("syslog-compat") {
  sdk = "shared"
  sdk_publishable = true
  sdk_headers = syslog_headers

  sources = [
    "compat.cc",
    "fx_logger.cc",
    "global.cc",
    "logger.cc",
  ]
  deps = [
    ":helpers",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_llcpp",
    "//sdk/lib/syslog:client_includes",
    "//sdk/lib/syslog/cpp:backend_fuchsia_compat",
    "//sdk/lib/syslog/cpp:cpp-macros",
    "//sdk/lib/syslog/structured_backend",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/service:service-llcpp",
    "//zircon/system/ulib/zx",
  ]
}

# This static library should have no shared library dependencies,
# except libzircon, so it may be used from magma client driver libraries
# such as the Vulkan ICD.
# This library cannot connect to the logging service, so a console_fd
# or log_service_channel must be provided at initialization.
zx_library("syslog-static") {
  sdk = "static"
  sdk_headers = syslog_headers
  sources = [
    "fx_logger.cc",
    "global.cc",
    "logger.cc",
  ]
  deps = [
    ":helpers",
    "//sdk/lib/syslog:client_includes",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
  ]
  defines = [ "SYSLOG_STATIC" ]
}

zx_library("syslog-headers") {
  sdk = "static"
  sdk_publishable = true
  sdk_headers = syslog_headers
}

source_set("helpers") {
  sources = [
    "helpers.cc",
    "helpers.h",
  ]
  deps = [ ":syslog-headers" ]
}

_test_sources = [
  "syslog_socket_tests.cc",
  "syslog_tests.cc",
]

_test_deps = [
  ":helpers",
  "//sdk/fidl/fuchsia.logger:fuchsia.logger_llcpp",
  "//zircon/system/ulib/fbl",
  "//zircon/system/ulib/zx",
  "//zircon/system/ulib/zxtest",
]

test("syslog-test") {
  sources = _test_sources
  deps = _test_deps + [ ":syslog" ]
}

test("syslog-static-test") {
  sources = _test_sources
  deps = _test_deps + [ ":syslog-static" ]
  defines = [ "SYSLOG_STATIC" ]
}

fuchsia_unittest_component("c-syslog-test") {
  deps = [ ":syslog-test" ]
}

fuchsia_unittest_component("c-syslog-static-test") {
  deps = [ ":syslog-static-test" ]
}

fuchsia_test_package("c-syslog-tests") {
  test_components = [
    ":c-syslog-test",
    ":c-syslog-static-test",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":c-syslog-tests" ]
}
