# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [
    ":zxtest-integration($host_toolchain)",
    ":zxtest-integration-test-pkg",
  ]
}

test("zxtest-integration") {
  output_name = "zxtest-integration-test"
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "assertions_test.c",
    "assertions_test.cc",
    "helper.cc",
    "main.cc",
    "print_test.c",
    "print_test.cc",
    "printvalue_test.cc",
    "register_test.c",
    "register_test.cc",
  ]
  deps = [
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zxtest",
  ]
  if (is_fuchsia) {
    deps += [
      "//sdk/lib/fdio",
      "//zircon/public/lib/zx",
    ]
  }
}

fuchsia_unittest_package("zxtest-integration-test-pkg") {
  package_name = "zxtest-integration-test"
  deps = [ ":zxtest-integration" ]
}
