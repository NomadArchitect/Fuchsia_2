# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/compiled_action.gni")
import("//build/config/clang/clang.gni")
import("//build/cpp/library_headers.gni")
import("//build/go/go_library.gni")

# TODO(fxbug.dev/91102, fxbug.dev/93393): "cpp", and "rust".
supported_zither_backends = [
  "asm",  # Assembly data layout bindings.
  "c",  # C data layout bindings.
  "go",  # Go data layout bindings.
]

_clang_format = {
  script = "$clang_prefix/clang-format"
  inputs = [ "//.clang-format" ]
  args = [ "--style=file:" + rebase_path(inputs[0], root_build_dir) ]
}

# Information on supported backends, accessible via
# `supported_zither_backend_info[ "$backend" ]`.
#
# Each backend scope contains the following:
#
#  * output_subdir
#    - Required: Describes the output subdirectory of the output directory
#      passed to zither in which the backend artifacts will be written. This
#      subdirectory has backend-specific significance as an C include path,
#      Go package name, etc. Given a FIDL library name `library_name` and
#      referring to this scope as `output_subdir_info`, one can reconstruct
#      this path as follows:
#      ```
#      output_subdir_info = {
#        prefix = ""
#        suffix = ""
#        name_separator = "."
#        forward_variables_from(backend_info["output_subdir"], "*")
#        path = prefix
#        path += string_replace(library_name, ".", name_separator)
#        path += suffix
#      }
#      output_subdir = output_subdir_info.path
#      ```
#    - Type: scope
#
#    The scope contains the following:
#      * name_separator
#        - Optional: The separator with which the '.'-separated tokens of the
#          FIDL library name should be joined in the output subdirectory
#          namespace. A value of "." will just use the library name unchanged
#          as path token.
#        - Type: string
#        - Default: "."
#
#      * prefix
#        - Optional: The prefix to attach to output subdirectory.
#        - Type: string
#        - Default: ""
#
#      * suffix
#        - Optional: The sufffix to attach to output subdirectory.
#        - Type: string
#        - Default: ""
#
#  * formatter
#    - Optional: A formatting specification for Zither outputs. The shape and
#      semantics of this parameter are identical to the `formatter` parameter
#      of `golden_files()`. While `formatter.extensions` is not consumed by
#      Zither - it makes sure to only format the appropriate files - it is
#      consumed in zither_golden_files() for the formatting of goldens outside
#      of Zither.
#    - Type: scope
#
supported_zither_backend_info = {
  c = {
    output_subdir = {
      prefix = "lib/"
      suffix = "/c"
    }
    formatter = _clang_format

    _library_template = "_zither_c_family_library"
  }
  asm = {
    output_subdir = {
      prefix = "lib/"
      suffix = "/asm"
    }
    formatter = _clang_format

    _library_template = "_zither_c_family_library"
  }
  go = {
    output_subdir = {
      name_separator = "/"
    }
    formatter = {
      script = "//prebuilt/third_party/go/$host_platform/bin/gofmt"

      # The go backend emits both go and text files; restrict the use of the
      # formatter to the go files.
      extensions = [ "go" ]
    }

    _library_template = "_zither_go_library"
  }
}

# Internal Zither invocation helper template used by `zither_library()`.
#
# Parameters:
#
#  * backend
#    - Required: The Zither backend to invoke.
#    - Type: string
#
#  * fidl_ir
#    - Required: A FIDL IR JSON file.
#    - Type: path
#
#  * output_dir
#    - Required: The directory for Zither outputs.
#    - Type: path
#
#  * generated_files
#    - Required: The expected set of Zither outputs, which necessarily must
#      start with "$output_dir/".
#    - Type: list(path)
#
#  * formatter
#    - Optional: See `supported_zither_backend_info`.
#    - Type: scope.
#
#  * testonly, visibility, deps
#    - Usual GN meanings. `deps` is just expected to contain whatever produced
#      the FIDL IR file.
#
template("_zither") {
  if (current_toolchain == default_toolchain) {
    forward_variables_from(invoker,
                           [
                             "generated_files",
                             "backend",
                             "fidl_ir",
                             "output_dir",
                             "formatter",
                           ])

    main_target = target_name

    # Internal subtarget used to check that a given backend's outputs were
    # fully specified; used for testing.
    output_check_target = "$target_name.check"

    output_manifest = "$output_dir/outputs.json"

    compiled_action(main_target) {
      forward_variables_from(invoker,
                             [
                               "testonly",
                               "deps",
                             ])
      visibility = [ ":*" ]

      tool = "//zircon/tools/zither"

      inputs = [ fidl_ir ]
      outputs = generated_files + [ output_manifest ]
      args = [
        "-ir",
        rebase_path(fidl_ir, root_build_dir),
        "-backend",
        backend,
        "-output-manifest",
        rebase_path(output_manifest, root_build_dir),
        "-output-dir",
        rebase_path(output_dir, root_build_dir),
      ]

      if (defined(formatter)) {
        inputs += [ formatter.script ]
        args += [
          "-formatter",
          rebase_path(formatter.script, root_build_dir),
        ]
        if (defined(formatter.args)) {
          args += [ "-formatter-args" ] + formatter.args
        }
        if (defined(formatter.inputs)) {
          inputs += formatter.inputs
        }
      }
    }

    # Ensures that the outputs were fully specified above.
    action(output_check_target) {
      forward_variables_from(invoker,
                             [
                               "testonly",
                               "visibility",
                             ])

      script = "//zircon/tools/zither/testing/check-outputs.py"

      # Stamp file.
      outputs = [ "$target_gen_dir/$output_check_target.stamp" ]

      inputs = generated_files + [ output_manifest ]
      args = [
               "--stamp",
               rebase_path(outputs[0], root_build_dir),
               "--manifest",
               rebase_path(output_manifest, root_build_dir),
             ] + rebase_path(generated_files, root_build_dir)
      deps = [ ":$main_target" ]
    }
  } else {
    group(target_name) {
      forward_variables_from(invoker, [ "testonly" ])
      visibility = [ ":*" ]
      deps = [ ":$target_name($default_toolchain)" ]
    }
    not_needed(invoker, "*", [ "testonly" ])
  }
}

#
# Internal language library helper templates used by `zither_library()`.
#
# Parameters:
#
#  * source_names
#    - Required: The list of the basenames (i.e., stripped of .fidl and
#      .test.fidl extensions) of the source FIDL files.
#
#  * output_subdir
#    - Required: The subdirectory of `output_dir` that has the Zither outputs
#      for entries.
#    - Type: relative path
#
#  * fidl_ir, output_dir, formatter
#    - Same as `_zither()`.
#
#  * testonly, visibility, deps
#    - Usual GN meanings.
#

template("_zither_c_family_library") {
  main_target = target_name
  zither_target = "$target_name.gen"

  forward_variables_from(invoker,
                         [
                           "source_names",
                           "output_dir",
                           "output_subdir",
                         ])

  generated_files = []
  foreach(name, source_names) {
    generated_files += [ "${output_dir}/${output_subdir}/${name}.h" ]
  }

  _zither(zither_target) {
    forward_variables_from(invoker,
                           [
                             "testonly",
                             "visibility",
                             "deps",
                             "fidl_ir",
                             "generated_files",
                             "backend",
                             "formatter",
                           ])
  }

  library_headers(main_target) {
    forward_variables_from(invoker,
                           [
                             "testonly",
                             "visibility",
                             "generated_files",
                           ])
    include_dir = output_dir
    headers = rebase_path(generated_files, include_dir)
    deps = [
      ":${zither_target}($default_toolchain)",
      ":${zither_target}.check($default_toolchain)",
    ]
  }
}

template("_zither_go_library") {
  assert(invoker.backend == "go")

  main_target = target_name
  zither_target = "$target_name.gen"

  forward_variables_from(invoker,
                         [
                           "source_names",
                           "output_dir",
                           "output_subdir",
                         ])

  generated_files = [ "${output_dir}/${output_subdir}/pkg_name.txt" ]
  foreach(name, source_names) {
    generated_files += [ "${output_dir}/${output_subdir}/${name}.go" ]
  }

  _zither(zither_target) {
    forward_variables_from(invoker,
                           [
                             "testonly",
                             "visibility",
                             "deps",
                             "fidl_ir",
                             "generated_files",
                             "formatter",
                           ])
    backend = "go"
  }

  go_library(main_target) {
    forward_variables_from(invoker,
                           [
                             "visibility",
                             "testonly",
                             "generated_files",
                           ])
    name = output_subdir
    source_dir = "${output_dir}/${output_subdir}"
    sources = rebase_path(generated_files, source_dir)
    non_go_deps = [
      ":${zither_target}($default_toolchain)",
      ":${zither_target}.check($default_toolchain)",
    ]
  }
}

# Define a full set of per-backend targets for a Zither library.
#
# `zither_library()` consumes FIDL source and defines the relevant language
# library targets that collect the bindings of the various supported Zither
# backends. These backends are listed in `supported_zither_backends` and the
# details of their bindings can be found in //zircon/tools/zither/README.md;
# The nature associated backend library subtargets are as follows where
# `${output_subdir}` is as described above in `supported_zither_backend_info`:
#
# Subtargets:
#
#   * C backend: ${target_name}.c
#     Public: a C library target (e.g., a source set or static library), whose
#     generated headers are of the form `<${output_subdir}/${filename}>` for each
#     basename of the associated FIDL source files.
#
#   * Go backend: ${target_name}.go
#     Public: a `go_library()` with a package name of ${output_subdir} (which
#     is written to pkg_name.txt alongside the bindings).
#
#   * Assembly backend: ${target_name}.asm
#     Public: an assembly library target (e.g., a source set), whose generated
#     headers are of the form `<${output_subdir}/${filename}>` for each
#     basename of the associated FIDL source files.
#
# Parameters:
#
#  * sources
#    - Required: The input FIDL sources, comprising one library necessarily of the
#      name $target_name.
#    - Type: list(path)
#
template("zither_library") {
  assert(defined(invoker.sources),
         "zither_library(\"$target_name\") requires `sources`")

  fidlc_target = "_zither_library.${target_name}.fidlc"

  default_target_gen_dir =
      get_label_info(":$target_name($default_toolchain)", "target_gen_dir")
  fidl_ir = "${default_target_gen_dir}/${target_name}.fidl.json"

  library_name = target_name
  if (current_toolchain == default_toolchain) {
    # Generate the IR.
    compiled_action(fidlc_target) {
      visibility = [ ":*" ]
      forward_variables_from(invoker,
                             [
                               "sources",
                               "testonly",
                             ])

      tool = "//tools/fidl/fidlc"

      outputs = [ fidl_ir ]

      args = [
               "--name",
               library_name,
               "--json",
               rebase_path(outputs[0], root_build_dir),
               "--files",
             ] + rebase_path(sources, root_build_dir)
    }
  } else {
    group(fidlc_target) {
      forward_variables_from(invoker, [ "testonly" ])
      visibility = [ ":*" ]
      deps = [ ":$fidlc_target($default_toolchain)" ]
    }
    not_needed(invoker, "*", [ "testonly" ])
  }

  foreach(backend, supported_zither_backends) {
    backend_info = {
    }  # Clear from previous iteration.
    backend_info = supported_zither_backend_info[backend]

    target(backend_info._library_template, "${target_name}.${backend}") {
      forward_variables_from(invoker,
                             [
                               "testonly",
                               "visibility",
                               "fidl_ir",
                               "backend",
                             ])
      forward_variables_from(backend_info, [ "formatter" ])

      # Zither will only be invoked in the default toolchain.
      output_dir = "$default_target_gen_dir/${target_name}-${backend}"

      output_subdir_info = {
        prefix = ""
        suffix = ""
        name_separator = "."
        forward_variables_from(backend_info["output_subdir"], "*")
        path = prefix
        path += string_replace(library_name, ".", name_separator)
        path += suffix
      }
      output_subdir = output_subdir_info.path

      source_names = []
      foreach(source, invoker.sources) {
        # Strip any .fidl or .test.fidl extensions.
        name = get_path_info(source, "name")
        if (get_path_info(name, "extension") == "test") {
          name = get_path_info(name, "name")
        }
        source_names += [ name ]
      }

      deps = [ ":$fidlc_target" ]
    }
  }
}
