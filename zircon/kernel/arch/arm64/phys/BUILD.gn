# Copyright 2020 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("//build/toolchain/toolchain_environment.gni")
import("//build/toolchain/variant.gni")

# This has to be variant() rather than config() to be used in the toolchain
# configs list.
variant("strict-align") {
  cflags = [ "-mstrict-align" ]
}

if (toolchain_environment == "kernel.phys") {
  source_set("address-space") {
    sources = [ "address-space.cc" ]
    include_dirs = [ "//." ]
    deps = [
      "//zircon/kernel/lib/arch",
      "//zircon/kernel/lib/boot-options",
      "//zircon/kernel/phys:allocation",
      "//zircon/kernel/phys:page-table",
      "//zircon/kernel/phys/lib/memalloc",
      "//zircon/kernel/phys/lib/page-table",
      "//zircon/system/ulib/zbitl",
    ]

    # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
    configs += [ "//build/config:Wno-conversion" ]
  }

  source_set("phys") {
    sources = [
      "setup.cc",
      "shadow-call-stack.S",
      "start.S",
    ]
    include_dirs = [ "//zircon/.." ]
    deps = [
      "//zircon/kernel/lib/arch",
      "//zircon/kernel/phys:main",
      "//zircon/system/ulib/static-pie",
    ]
  }

  config("qemu.config") {
    # Like ZBI executables, direct QEMU phys executables can be loaded
    # anywhere, so they are linked at 0.
    configs = [ "//zircon/kernel/phys:load-pic" ]
  }

  source_set("linuxboot") {
    sources = [ "linuxboot-header.S" ]
    deps = [ "//zircon/kernel/lib/arch" ]
    libs = [ "./linuxboot.ld" ]
  }
}

group("tests") {
  testonly = true
  deps = []
}
