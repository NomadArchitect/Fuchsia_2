# Copyright 2019 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("//build/tracer/tracer.gni")
import("//build/zircon/migrated_targets.gni")
import("version_string.gni")

# The version string depends solely on the source and doesn't vary across
# machines or toolchains, so generate it only once.
gen_dir = get_label_info(".($default_toolchain)", "target_gen_dir")
if (current_toolchain == default_toolchain) {
  assert(gen_dir == target_gen_dir)

  action("version-string.bin") {
    visibility = [
      ":*",
      "//zircon/kernel:*",
    ]
    script = "git-version-string.sh"
    inputs = [
      # Regenerate version if HEAD moves
      "//.git/HEAD",

      # Add "-dirty" suffix as needed
      "//.git/index",
    ]
    outputs = [ kernel_version_string_file ]

    args = rebase_path([
                         kernel_version_string_file,
                         "//.git",
                       ],
                       root_build_dir)
  }

  action("version-string.h") {
    visibility = [ ":*" ]
    script = "gen-version-string-header.sh"
    outputs = [ "$gen_dir/lib/version/version-string.h" ]
    deps = [ ":version-string.bin" ]
    sources = [ kernel_version_string_file ]
    args = rebase_path(sources + outputs, root_build_dir)
  }
} else {
  zx_library("version") {
    sources = [ "version.cc" ]
    defines = [ "ARCH=\"$current_cpu\"" ]
    public_deps = [ ":version-string.h($default_toolchain)" ]
    public_configs = [ ":gen.config" ]
    deps = [
      "//zircon/kernel/lib/console",
      "//zircon/kernel/lib/init",
    ]
  }

  config("gen.config") {
    visibility = [ ":*" ]
    include_dirs = [ gen_dir ]
  }
}
