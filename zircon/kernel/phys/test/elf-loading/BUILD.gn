# Copyright 2022 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("//build/dist/resource.gni")
import("//build/testing/zbi_test.gni")
import("//zircon/kernel/kernel_package.gni")
import("//zircon/kernel/phys/zbi_executable.gni")

zbi_executable("zbi-basic-elf-loading-test.executable") {
  visibility = [ ":*" ]
  testonly = true

  sources = [ "basic-test.cc" ]

  deps = [
    "..:zbi-test-main",
    "//src/lib/elfldltl",
    "//src/lib/zbitl",
    "//zircon/kernel/lib/ktl",
    "//zircon/kernel/lib/libc",
    "//zircon/kernel/phys:elf-image",
    "//zircon/kernel/phys:kernel-package",
    "//zircon/kernel/phys:zbi-memory",
    "//zircon/kernel/phys:zbitl-allocation",
  ]
}

loadable_module("get-int") {
  visibility = [ ":*" ]
  testonly = true

  sources = [ "get-int.cc" ]
  ldflags = [ "-Wl,-e,GetInt" ]

  metadata = {
    # This is the same metadata as a resource() target would have.
    distribution_entries = [
      {
        source = rebase_path("$root_out_dir/$target_name.so", root_build_dir)
        destination = target_name
        label = get_label_info(":$target_name", "label_with_toolchain")
      },
    ]
  }
}

kernel_package("loadables") {
  visibility = [ ":*" ]
  testonly = true

  deps = [ ":get-int" ]
}

zbi_test("zbi-basic-elf-loading-test") {
  args = []
  deps = [
    ":loadables",
    ":zbi-basic-elf-loading-test.executable",
  ]
}

group("elf-loading") {
  testonly = true

  deps = [ ":zbi-basic-elf-loading-test" ]
}
