# Copyright 2022 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("//build/toolchain/toolchain_environment.gni")

_is_efi = toolchain_environment == "kernel.efi"

_efi_toolchain = "//zircon/kernel/phys/efi:kernel.efi_$current_cpu"

# This is the UEFI-specified "machine type short-name", as used in
# the canonical file name "\efi\boot\boot${efi_cpu}.efi".
if (current_cpu == "arm64") {
  efi_cpu = "aa64"
} else if (current_cpu == "x86") {
  efi_cpu = "ia32"
} else {
  efi_cpu = current_cpu
}

# Build an executable for the kernel.efi environment.
#
# This just defines an executable() target, but it's always in the toolchain
# for the kernel.efi environment for $current_cpu.
#
# Parameters
#
#   * install_path
#     - Optional: If set, this specifies in the destination in FAT filesystem
#       when aggregated as a dependency of `fat_filesystem()` - at which this
#       executable should be installed
#     - Type: relative path
#
#   * output_extension
#     - Optional: See executable(), but defaults to "efi".
#     - Type: string
#     - Default: "efi"
#
# See executable() for other parameters.
template("efi_executable") {
  if (!_is_efi) {
    group(target_name) {
      forward_variables_from(invoker,
                             [
                               "testonly",
                               "visibility",
                             ])
      public_deps = [ ":$target_name($_efi_toolchain)" ]
    }
    not_needed(invoker, "*")
  } else {
    executable(target_name) {
      forward_variables_from(invoker,
                             "*",
                             [
                               "install_path",
                               "metadata",
                             ])
      if (defined(visibility)) {
        # Make sure it's visible to the redirector group above.
        visibility += [ ":$target_name" ]
      }
      if (!defined(output_extension)) {
        output_extension = "efi"
      }
      if (!defined(output_name)) {
        output_name = target_name
      }

      metadata = {
        if (defined(invoker.metadata)) {
          forward_variables_from(invoker.metadata, "*")
        }

        if (defined(invoker.install_path)) {
          if (!defined(distribution_entries)) {
            distribution_entries = []
          }
          distribution_entries += [
            {
              source = rebase_path(
                      "${root_out_dir}/${output_name}.${output_extension}",
                      root_build_dir)
              destination = invoker.install_path
              label = get_label_info(":$target_name", "label_with_toolchain")
            },
          ]
        }
      }
    }
  }
}

if (_is_efi) {
  set_defaults("efi_executable") {
    configs = default_executable_configs
  }
}
