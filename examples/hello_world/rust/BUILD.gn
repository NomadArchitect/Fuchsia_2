# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# NOTE: The comments that have [START/END ...] in them are used to identify code snippets that
# appear in the documentation.  Please be aware that changes in these blocks will affect the
# documentation on fuchsia.dev.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

group("tests") {
  testonly = true

  deps = [ ":hello-world-rust-tests" ]
}

# [START rustc_tests]
rustc_binary("bin") {
  name = "hello_world_rust"

  # Generates the "bin_test" build target
  with_unit_tests = true
  edition = "2018"

  deps = []
  test_deps = [ "//src/lib/fuchsia-async" ]

  sources = [ "src/main.rs" ]
}

# [END rustc_tests]

fuchsia_component("hello-world-rust-component") {
  deps = [ ":bin" ]
  component_name = "hello-world-rust"
  manifest = "meta/hello_world_rust.cml"
}

fuchsia_component("hello-world-rust-tests-component") {
  testonly = true
  deps = [ ":bin_test" ]
  manifest = "meta/hello_world_rust_bin_test.cml"
  component_name = "hello-world-rust-tests"
}

fuchsia_test_package("hello-world-rust-tests") {
  test_components = [ ":hello-world-rust-tests-component" ]
}
