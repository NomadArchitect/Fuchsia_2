# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")
import("//build/security.gni")

# TODO(fxbug.dev/102652): Remove security.gni when Subpackages are generally available

group("tests") {
  testonly = true

  # This Subpackages example will only work if subpackage resolution is enabled
  # in the full-resolver.
  if (full_resolver_enable_subpackages) {
    deps = [ ":package-with-subs" ]
  }
}

rustc_test("bin") {
  name = "parent_realm"
  edition = "2021"

  deps = [
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component-test",
    "//src/sys/lib/component-events",
    "//third_party/rust_crates:tracing",
  ]

  sources = [ "src/lib.rs" ]
}

fuchsia_component("parent-component") {
  testonly = true
  manifest = "meta/parent_realm.cml"
  component_name = "parent-realm"
  deps = [ ":bin" ]
}

fuchsia_package_with_default_component("echo_client") {
  manifest = "meta/echo_client.cml"
  deps = [ "//examples/components/routing/rust/echo_client" ]
}

fuchsia_package_with_default_component("echo_server") {
  manifest = "meta/echo_server.cml"
  deps = [ "//examples/components/routing/rust/echo_server" ]
}

# Run with `fx test subpackage-examples`.
fuchsia_test_package("package-with-subs") {
  package_name = "subpackage-examples"
  test_components = [ ":parent-component" ]

  subpackages = [
    {
      package = ":echo_client"
    },
    {
      # TODO(fxbug.dev/100060): It should be possible to rename the subpackage, by
      # uncommenting the next line, but full-resolver needs to be updated first,
      # to resolve packages only by package hash (blob id). Until then, the subpackage
      # name is used to fill in the package name, so they must match.
      #
      #     name = "my-echo-server"
      package = ":echo_server"
    },
  ]
}
