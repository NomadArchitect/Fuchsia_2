# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")

# [START example_snippet]
rustc_test("bin") {
  name = "echo_integration_test_rust"
  edition = "2021"

  deps = [
    "//examples/components/routing/fidl:echo_rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
  ]

  sources = [ "src/lib.rs" ]
}

fuchsia_component("echo_integration_test_component") {
  testonly = true
  component_name = "echo_integration_test"
  manifest = "meta/echo_integration_test.cml"
  deps = [ ":bin" ]
}

fuchsia_test_package("echo_integration_test_rust") {
  test_components = [ ":echo_integration_test_component" ]
  deps =
      [ "//examples/components/routing/rust/echo_server:echo_server_component" ]
}

# [END example_snippet]

fuchsia_test_package_with_default_component(
    "subpackaged_echo_integration_test_rust") {
  manifest = "meta/subpackaged_echo_integration_test.cml"
  deps = [ ":bin" ]
  renameable_subpackages = [
    # TODO(fxbug.dev/113190): When packages included only as subpackages can
    # have names that are not required to be globally unique, rename the
    # `package_name` of the `:echo_server_package` to `"echo_server"` and change
    # this `renameable_subpackages` list to the simpler form:
    # `subpackages = [ "...:echo_server_package" ]`.
    {
      package =
          "//examples/components/routing/rust/echo_server:echo_server_package"
      name = "echo_server"
    },
  ]
}
