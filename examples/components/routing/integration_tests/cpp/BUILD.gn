# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

# [START example_snippet]
executable("bin") {
  output_name = "echo_integration_test_cpp"
  sources = [ "echo_integration_test.cc" ]
  deps = [
    "//examples/components/routing/fidl:echo_hlcpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
  testonly = true
}

fuchsia_component("echo_integration_test_component_cpp") {
  testonly = true
  component_name = "echo_integration_test"
  manifest = "meta/echo_integration_test.cml"
  deps = [ ":bin" ]
}

fuchsia_test_package("echo_integration_test_cpp") {
  test_components = [ ":echo_integration_test_component_cpp" ]
  deps =
      [ "//examples/components/routing/cpp/echo_server:echo_server_component" ]
}

# [END example_snippet]

fuchsia_test_package_with_default_component(
    "subpackaged_echo_integration_test_cpp") {
  manifest = "meta/subpackaged_echo_integration_test.cml"
  deps = [ ":bin" ]
  renameable_subpackages = [
    # TODO(fxbug.dev/113190): When packages included only as subpackages can
    # have names that are not required to be globally unique, rename the
    # `package_name` of the `:echo_server_package` to `"echo_server"` and change
    # this `renameable_subpackages` list to the simpler form:
    # `subpackages = [ "...:echo_server_package" ]`.
    {
      package =
          "//examples/components/routing/cpp/echo_server:echo_server_package"
      name = "echo_server"
    },
  ]
}
