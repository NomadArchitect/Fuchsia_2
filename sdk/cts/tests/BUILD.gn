# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//sdk/cts/build/cts.gni")

group("prebuilts") {
  testonly = true
  deps = [
    "fidl:prebuilts",
    "pkg:prebuilts",
    "//sdk/cts/examples:prebuilts",

    # TODO(kjharland): Include fidl cpp tests.
    # TODO(kjharland): Include tools tests.
  ]
}

group("tests_no_e2e") {
  testonly = true
  assert_no_deps = e2e_test_libs

  deps = [
    "fidl:tests",
    "pkg:tests",
  ]

  # TODO(jcecil): add back tools tests once we're able to execute them OOT.
  path = get_label_info(":${target_name}", "dir")
  is_cts_tot = string_replace(path, "//sdk/cts", "") != path
  if (is_cts_tot) {
    deps += [ "tools:tests_no_e2e" ]
  }
}

group("e2e_tests") {
  testonly = true
  deps = []

  # TODO(jcecil): add back tools tests once we're able to execute them OOT.
  path = get_label_info(":${target_name}", "dir")
  is_cts_tot = string_replace(path, "//sdk/cts", "") != path
  if (is_cts_tot) {
    deps += [ "tools:e2e_tests" ]
  }
}

group("tests") {
  testonly = true
  deps = [
    ":e2e_tests",
    ":tests_no_e2e",
  ]
}

cts_copy_to_sdk("build") {
  sources = [ "BUILD.gn" ]
  testonly = true
}

sdk_molecule("test_sdks") {
  deps = [
    ":build_sdk",
    "fidl:test_sdks",
    "pkg:test_sdks",

    # TODO(johnshamoon): Enable this when our Dart toolchain is sorted.
    # "tools:test_sdks",
  ]
  testonly = true
}
