# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//sdk/cts/build/cts_copy_to_sdk.gni")
import("//sdk/cts/build/cts_element.gni")
import("//sdk/cts/build/write_cts_file.gni")

# A Fuchsia component that can be used in CTS.
#
# An equivalent to fuchsia_component, but ensures dependencies are allowed in CTS.
# Creates an SDK atom so that this target can be released in the CTS SDK.
#
# Example:
# ```
# cts_fuchsia_component("my-test-component") {
#   suite_name = "my_test_suite"
#   deps = [ ":my-test" ]
#   manifest = "meta/my-test.cmx"
#   testonly = true
# }
# ```
#
#   suite_name (required)
#     Name of the test suite this executable belongs to.
#     The sources for this target will be placed in tests/$suite_name
#     Type: string
template("cts_fuchsia_component") {
  assert(defined(invoker.suite_name), "Suite name must be defined.")
  if (defined(invoker.deps) && invoker.deps != []) {
    cts_element("${target_name}_verify_deps") {
      deps = invoker.deps
      invoker_label = get_label_info(invoker.target_name, "label_no_toolchain")
    }
  }

  fuchsia_component(target_name) {
    forward_variables_from(invoker, "*", [ "suite_name" ])
    if (defined(deps) && deps != []) {
      deps += [
        ":${target_name}_cts_file",
        ":${target_name}_verify_deps",
      ]
    } else {
      deps = [ ":${target_name}_cts_file" ]
    }
  }

  cts_copy_to_sdk(target_name) {
    forward_variables_from(invoker,
                           [
                             "manifest",
                             "suite_name",
                             "testonly",
                           ])
    sources = [ manifest ]
  }

  write_cts_file(target_name) {
  }
}
