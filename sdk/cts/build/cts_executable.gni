# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//sdk/cts/build/cts_element.gni")
import("//sdk/cts/build/cts_source_library.gni")
import("//sdk/cts/build/write_cts_file.gni")

# An executable that can be used in CTS.
#
# An equivalent to the built-in executable, but ensures dependencies are allowed
# in CTS. Creates an SDK atom so that this target can be released in the CTS SDK.
#
# Example:
# ```
# cts_executable("my-test") {
#   suite_name = "my_test_suite"
#   deps = [ "//zircon/public/lib/zxtest" ]
#   sources = [ "//sdk/cts/tests/my_test/my_test.cc" ]
#   testonly = true
# }
# ```
#
# Parameters
#   suite_name (required)
#     Name of the test suite this executable belongs to.
#     The sources for this target will be placed in tests/$suite_name
#     Type: string
template("cts_executable") {
  assert(defined(invoker.suite_name), "Suite name must be defined.")

  invoker_deps = []
  if (defined(invoker.deps)) {
    invoker_deps += invoker.deps
  }
  if (defined(invoker.public_deps)) {
    invoker_deps += invoker.public_deps
  }

  if (invoker_deps != []) {
    verify_target = target_name + "_verify_deps"
  }
  write_file_target = target_name + "_cts_file"

  if (defined(verify_target)) {
    cts_element(verify_target) {
      deps = invoker_deps
      invoker_label = get_label_info(invoker.target_name, "label_no_toolchain")
    }
  }

  write_cts_file(write_file_target) {
  }

  executable(target_name) {
    forward_variables_from(invoker,
                           "*",
                           [
                             "non_cts_deps",
                             "suite_name",
                           ])
    if (!defined(deps)) {
      deps = []
    }

    if (defined(verify_target)) {
      deps += [ ":$verify_target" ]
    }
    deps += [ ":$write_file_target" ]
  }

  # TODO(75000): Remove non_cts_deps
  # non_cts_deps should only be used as a stopgap to moving existing CTS tests
  # into the CTS archive.
  cts_source_library(target_name) {
    forward_variables_from(invoker,
                           [
                             "deps",
                             "non_cts_deps",
                             "public",
                             "public_deps",
                             "sources",
                             "suite_name",
                             "testonly",
                           ])
  }
}

set_defaults("cts_executable") {
  configs = default_executable_configs
}
