# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file

import("//build/compiled_action.gni")
import("//build/fidl/fidl_summary.gni")
import("//build/sdk/plasa/config.gni")
import("//build/sdk/plasa/plasa_fragment_fidl.gni")

group("plasa") {
  testonly = true
  deps = [
    ":tests",
    "//sdk/ctf/plasa:filter_compdb",
    "//sdk/ctf/plasa/clang_doc_filter:host",
    "//sdk/ctf/plasa/model",
    "//sdk/ctf/plasa/plasa_test_coverage_report:host",
  ]
  if (current_toolchain == toolchain_variant.base) {
    deps += [ "//sdk/ctf/plasa:compdb" ]

    if (generate_plasa_artifacts) {
      deps += [ ":api_coverage_report" ]
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    "//sdk/ctf/plasa/clang_doc_filter:tests",
    "//sdk/ctf/plasa/model:tests",
    "//sdk/ctf/plasa/plasa_test_coverage_report:tests",
  ]
}

if (current_toolchain == toolchain_variant.base) {
  group("api_coverage_report") {
    testonly = true
    deps = [ "//sdk/ctf/plasa:api_coverage_report" ]
  }

  plasa_fragment_fidl("vdso_summary") {
    testonly = true
    dest = "vdso"
    fidl_ir = "${root_gen_dir}/zircon/vdso/zx.fidl.json"
    output_fragment_file = "${target_gen_dir}/zx.api_summary.json"
    deps = [ "//zircon/vdso" ]
  }
}
