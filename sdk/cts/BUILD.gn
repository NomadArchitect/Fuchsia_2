# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/build_api_module.gni")
import("//sdk/cts/build/cts.gni")
import("//sdk/cts/build/verify_sdk_compatibility.gni")

build_api_module("cts_artifacts") {
  testonly = true
  data_keys = [ "cts_artifacts" ]
  deps = [ "//sdk/cts" ]
}

generated_file("generated_test_manifest") {
  testonly = true
  outputs = [ "$root_out_dir/cts/test_manifest.json" ]
  output_conversion = "json"
  data_keys = [ "test_manifest" ]
  deps = [
    "examples:tests",
    "tests",
  ]

  metadata = {
    cts_artifacts = [ "cts/test_manifest.json" ]
  }
}

generated_file("generated_host_test_manifest") {
  testonly = true
  outputs = [ "$root_out_dir/cts/host_test_manifest.json" ]
  output_conversion = "json"
  data_keys = [ "host_test_manifest" ]
  deps = [ "examples/host_tool:tests($host_toolchain)" ]

  metadata = {
    cts_artifacts = [ "cts/host_test_manifest.json" ]
  }
}

group("test_manifest") {
  testonly = true
  deps = [
    ":generated_host_test_manifest",
    ":generated_test_manifest",
  ]
}

group("cts_no_e2e") {
  testonly = true
  assert_no_deps = e2e_test_libs

  deps = [
    "build/scripts:tests",
    "examples:tests_no_e2e",
    "tests:tests_no_e2e",
  ]
}

group("cts") {
  testonly = true
  deps = [
    ":core_sdk_layout",
    ":cts_no_e2e",
    ":plasa",
    ":test_manifest",
    "examples:tests",
    "tests",
  ]
}

# A manifest of the plasa elements that are vended in CTS.
generated_file("plasa_manifest") {
  testonly = true
  data_keys = [ "plasa" ]
  outputs = [ "$root_build_dir/manifest.cts.plasa.json" ]
  output_conversion = "json"
  deps = [ "//sdk/fidl:plasa" ]
}

group("plasa") {
  testonly = true
  deps = [
    ":plasa_manifest_copy",
    "//sdk/fidl:plasa",
  ]
}

copy("plasa_manifest_copy") {
  testonly = true
  sources = get_target_outputs(":plasa_manifest")
  outputs = [ "$root_build_dir/cts/{{source_file_part}}" ]
  deps = [ ":plasa_manifest" ]
}

# Generate SDK directory layout golden file.
generate_sdk_layout_golden_file("core_sdk_layout") {
  sdk_archive = "$root_out_dir/sdk/archive/core.tar.gz"
  deps = [ "//sdk:core_archive" ]
}

# Ensures the backwards compatibility of core SDK directory layout changes.
verify_sdk_compatibility("verify_sdk_compatibility") {
  sdk_archive = "$root_out_dir/sdk/archive/core.tar.gz"
  deps = [ "//sdk:core_archive" ]
}
