# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/sdk/sdk_copy_product_bundle.gni")
import("//build/sdk/sdk_molecule.gni")

# The in-tree physical device profile is generated by the
# //build/images:physical_device_manifest manifest target.

# This build file will eventually collect all available device
# profiles generated by the release builder. Details are TBD.

# Product bundles that are part of the core SDK.
bundle_name_identifiers = [
  "terminal.x64",
  "terminal.arm64",
  "terminal.qemu-x64",
  "terminal.qemu-arm64",
  "workstation.qemu-x64",
]
product_bundle_deps = []

# Collect all the product bundle metadata files that are generated
# by the //build/images:product_metadata target.
foreach(bundle_name, bundle_name_identifiers) {
  sdk_copy_product_bundle(bundle_name) {
    category = "partner"
  }
  product_bundle_deps += [ ":${bundle_name}" ]
}

sdk_molecule("devices") {
  deps = product_bundle_deps
}
