# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/sdk/sdk_molecule.gni")
import("//sdk/devices/device.gni")

devices = [
  {
    name = "generic-arm64"
    description = "A generic arm64 device"
    arch = "arm64"
  },
  {
    name = "generic-x64"
    description = "A generic x64 device"
    arch = "x64"
  },
  {
    name = "qemu-arm64"
    description = "arm64 images runnable on QEMU"
    arch = "arm64"
  },
  {
    name = "qemu-x64"
    description = "x64 images runnable on QEMU"
    arch = "x64"
  },
]

all_devices = []

foreach(device, devices) {
  device(device.name) {
    arch = device.arch
    description = device.description
  }
  all_devices += [ ":${device.name}" ]
}

sdk_molecule("devices") {
  deps = all_devices
}
