# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import("//src/lib/fostr/build/fostr_fidl.gni")

group("tests") {
  testonly = true

  deps = [
    ":component_cpp_outgoing_directory_tests",
    ":component_cpp_service_client_tests",
    ":component_cpp_testing_realm_builder_tests",
  ]
}

source_set("test_utils") {
  testonly = true

  sources = [
    "utils.cc",
    "utils.h",
  ]

  deps = [
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_hlcpp",
    "//sdk/fidl/fuchsia.component.test:fuchsia.component.test_hlcpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_hlcpp",
    "//sdk/lib/sys/component/cpp/testing:cpp",
  ]
}

executable("echo_server_bin") {
  testonly = true

  sources = [ "echo_server.cc" ]

  deps = [
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl",
    "//src/testing/fidl:placeholders_hlcpp",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

executable("echo_server_sc_bin") {
  testonly = true

  sources = [ "echo_server_sc.cc" ]

  deps = [
    ":echo_server_config_lib",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl",
    "//src/testing/fidl:placeholders_hlcpp",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

executable("echo_service_server_bin") {
  testonly = true

  sources = [ "echo_service_server.cc" ]

  deps = [
    "//examples/fidl/fuchsia.examples:fuchsia.examples_cpp_wire",
    "//sdk/lib/fidl",
    "//sdk/lib/sys/component/cpp",
    "//sdk/lib/syslog/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

executable("realm_builder_test_bin") {
  testonly = true

  sources = [ "realm_builder_test.cc" ]

  deps = [
    ":test_utils",
    "//examples/fidl/fuchsia.examples:fuchsia.examples_hlcpp",
    "//sdk/fidl/fuchsia.component:fuchsia.component_hlcpp",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_hlcpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_hlcpp",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_hlcpp",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2_hlcpp",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/component/cpp/testing:cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fostr/fidl/fuchsia.component.decl",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/testing/fidl:placeholders_hlcpp",
  ]
}

executable("internal_test_bin") {
  testonly = true

  sources = [ "internal_test.cc" ]

  deps = [
    ":test_utils",
    "//sdk/fidl/fuchsia.component:fuchsia.component_hlcpp",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_hlcpp",
    "//sdk/fidl/fuchsia.component.test:fuchsia.component.test_hlcpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_hlcpp",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/component/cpp/testing:cpp",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/testing/fidl:placeholders_hlcpp",
  ]
}

fuchsia_component("echo_server") {
  deps = [ ":echo_server_bin" ]
  manifest = "meta/echo_server.cml"
  testonly = true
}

fuchsia_component_manifest("echo_server_sc_manifest") {
  testonly = true
  component_name = "echo_server_sc"
  manifest = "meta/echo_server_sc.cml"
}

fuchsia_component("echo_server_sc") {
  testonly = true
  cm_label = ":echo_server_sc_manifest"
  deps = [ ":echo_server_sc_bin" ]
}

fuchsia_structured_config_cpp_elf_lib("echo_server_config_lib") {
  testonly = true
  cm_label = ":echo_server_sc_manifest"
}

fuchsia_structured_config_values("echo_server_default_config") {
  testonly = true
  cm_label = ":echo_server_sc_manifest"
  values = {
    my_flag = true
    my_uint8 = 255
    my_uint16 = 65535
    my_uint32 = 4000000000
    my_uint64 = 8000000000
    my_int8 = -127
    my_int16 = -32766
    my_int32 = -2000000000
    my_int64 = -4000000000
    my_string = "hello"
    my_vector_of_flag = [
      true,
      false,
    ]
    my_vector_of_uint8 = [
      1,
      2,
    ]
    my_vector_of_uint16 = [
      2,
      3,
    ]
    my_vector_of_uint32 = [
      3,
      4,
    ]
    my_vector_of_uint64 = [
      4,
      5,
    ]
    my_vector_of_int8 = [
      -1,
      -2,
    ]
    my_vector_of_int16 = [
      -2,
      -3,
    ]
    my_vector_of_int32 = [
      -3,
      -4,
    ]
    my_vector_of_int64 = [
      -4,
      -5,
    ]
    my_vector_of_string = [
      "foo",
      "bar",
    ]
  }
}

fuchsia_component("echo_service_server") {
  testonly = true
  deps = [ ":echo_service_server_bin" ]
  manifest = "meta/echo_service_server.cml"
}

fuchsia_component("realm_builder_test") {
  testonly = true
  deps = [ ":realm_builder_test_bin" ]
  manifest = "meta/realm_builder_test.cml"
}

fuchsia_component("pre_populated_realm") {
  testonly = true
  manifest = "meta/pre_populated_realm.cml"
}

fuchsia_unittest_component("internal_test") {
  deps = [ ":internal_test_bin" ]
}

fuchsia_test_package("component_cpp_testing_realm_builder_tests") {
  test_components = [
    ":realm_builder_test",
    ":internal_test",
  ]
  deps = [
    ":echo_server",
    ":echo_server_default_config",
    ":echo_server_sc",
    ":echo_service_server",
    ":pre_populated_realm",
  ]
}

executable("cpp_outgoing_directory_test_bin") {
  testonly = true

  sources = [ "outgoing_directory_test.cc" ]

  deps = [
    "//examples/fidl/fuchsia.examples:fuchsia.examples_cpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_cpp_wire",
    "//sdk/lib/sys/component/cpp",
    "//src/lib/fidl/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/storage/vfs/cpp",
    "//src/lib/testing/loop_fixture",
    "//third_party/googletest:gmock",
  ]
}

executable("cpp_outgoing_directory_integration_test_bin") {
  testonly = true

  sources = [ "outgoing_directory_integration_test.cc" ]

  deps = [
    "//examples/fidl/fuchsia.examples:fuchsia.examples_hlcpp",
    "//sdk/fidl/fuchsia.component:fuchsia.component_hlcpp",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_hlcpp",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/component/cpp/testing:cpp",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/storage/vfs/cpp",
    "//src/lib/testing/loop_fixture",
  ]
}

fuchsia_component("cpp_echo_service_server") {
  testonly = true
  deps = [ ":echo_service_server_bin" ]
  manifest = "meta/cpp_echo_service_server.cml"
}

fuchsia_component("outgoing_directory_test") {
  testonly = true
  deps = [ ":cpp_outgoing_directory_test_bin" ]
  manifest = "meta/outgoing_directory_test.cml"
}

fuchsia_component("outgoing_directory_integration_test") {
  testonly = true
  deps = [ ":cpp_outgoing_directory_integration_test_bin" ]
  manifest = "meta/outgoing_directory_integration_test.cml"
}

fuchsia_test_package("component_cpp_outgoing_directory_tests") {
  test_components = [
    ":outgoing_directory_test",
    ":outgoing_directory_integration_test",
  ]
  deps = [ ":cpp_echo_service_server" ]
}

fidl("fidl.service.test") {
  testonly = true
  visibility = [ ":*" ]
  sources = [ "test.test.fidl" ]
}

test("service_client_test") {
  sources = [
    "client_test.cc",
    "server_test.cc",
  ]
  deps = [
    ":fidl.service.test_cpp_wire",
    "//sdk/lib/fdio",
    "//sdk/lib/sys/component/cpp",
    "//src/lib/storage/vfs/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fidl-async",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
    "//zircon/system/ulib/zxtest",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

fuchsia_unittest_package("component_cpp_service_client_tests") {
  deps = [ ":service_client_test" ]
}
