# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/cpp/library_headers.gni")

library_headers("headers") {
  headers = [
    "lib/zxio/cpp/vector.h",
    "lib/zxio/extensions.h",
    "lib/zxio/null.h",
    "lib/zxio/ops.h",
    "lib/zxio/types.h",
    "lib/zxio/zxio.h",
  ]
}

# This header exposes implementation details of the zxio library in order to
# facilitate moving logic from fdio into zxio. It should not be used outside of
# these two libraries.
library_headers("inception") {
  headers = [ "lib/zxio/cpp/inception.h" ]

  public_deps = [
    ":headers",

    # <lib/zxio/cpp/inception.h> has #include <fuchsia/io/llcpp/fidl.h>.
    "//sdk/fidl/fuchsia.io:fuchsia.io_llcpp",
  ]

  visibility = [
    "//sdk/lib/fdio/*",
    "//sdk/lib/zxio/*",
  ]
}

static_library("zxio") {
  sources = [
    "create.cc",
    "debuglog.cc",
    "extensions.cc",
    "get_vmo.cc",
    "inception.cc",
    "null.cc",
    "pipe.cc",
    "remote.cc",
    "remote_v2/common_utils.cc",
    "remote_v2/dirent_iterator.cc",
    "remote_v2/remote_v2.cc",
    "vmo.cc",
    "vmofile.cc",
    "zxio.cc",
  ]
  public_deps = [ ":headers" ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.pty:fuchsia.hardware.pty_llcpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_llcpp",
    "//sdk/fidl/fuchsia.io2:fuchsia.io2_llcpp",
    "//sdk/lib/zxio:inception",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
  ]
}

static_library("zxio_complete") {
  complete_static_lib = true

  deps = [ ":zxio" ]
}

group("tests") {
  testonly = true
  deps = [ "tests" ]
}
