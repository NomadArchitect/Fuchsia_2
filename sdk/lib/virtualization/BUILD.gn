# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

declare_args() {
  # Set this to false to enable new wayland bridge code with Flatland API support
  use_legacy_wayland_bridge = true
}

source_set("virtualization") {
  sources = [
    "scenic_wayland_dispatcher.cc",
    "scenic_wayland_dispatcher.h",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.sys",
    "//sdk/fidl/fuchsia.ui.app",
    "//sdk/fidl/fuchsia.virtualization",
    "//sdk/fidl/fuchsia.wayland",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl",
  ]
  defines = []
  if (use_legacy_wayland_bridge) {
    defines += [ "USE_LEGACY_WAYLAND_BRIDGE" ]
  }
}

executable("virtualization_lib_unittests") {
  visibility = [ ":*" ]
  testonly = true

  sources = [ "scenic_wayland_dispatcher_unittest.cc" ]

  deps = [
    ":virtualization",
    "//garnet/public/lib/gtest",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/lib/fxl/test:gtest_main",
  ]
  defines = []
  if (use_legacy_wayland_bridge) {
    defines += [ "USE_LEGACY_WAYLAND_BRIDGE" ]
  }
}

fuchsia_unittest_package("virtualization_lib_tests") {
  deps = [ ":virtualization_lib_unittests" ]
  manifest = "meta/virtualization_lib_unittests.cmx"
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
