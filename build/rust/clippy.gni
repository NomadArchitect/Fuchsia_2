# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/rust/config.gni")

# Defines a clippy target corresponding to a given `rustc_{library/binary/test}` target.
# This will emit json lints to the target's gen directory in a `target_name.clippy` file.
template("clippy") {
  _output = "$target_gen_dir/$target_name"

  # These generated files collect the rlibs of this targets direct and transitive dependencies
  generated_file("$target_name.depsfile") {
    forward_variables_from(invoker, [ "testonly" ])
    deps = invoker.deps
    outputs = [ "$_output.deps" ]
    data_keys = [ "rlib" ]
    walk_keys = [ "rust_barrier" ]
  }
  generated_file("$target_name.transdepsfile") {
    forward_variables_from(invoker, [ "testonly" ])
    deps = invoker.deps
    outputs = [ "$_output.transdeps" ]
    data_keys = [ "searchdir" ]
  }

  action(target_name) {
    forward_variables_from(invoker, [ "testonly" ])
    sources = invoker.sources
    deps = invoker.deps
    deps += [
      ":${invoker.target_name}.depsfile",
      ":${invoker.target_name}.transdepsfile",
    ]
    if (defined(invoker.non_rust_deps)) {
      deps += invoker.non_rust_deps
    }

    inputs = [
      "$_output.deps",
      "$_output.transdeps",
    ]
    script = "//build/rust/clippy_wrapper.sh"
    output = _output
    outputs = [ output ]

    edition = "2018"
    if (defined(invoker.edition)) {
      edition = "${invoker.edition}"
    }

    _jq = "//prebuilt/third_party/jq/${host_platform}/bin/jq"

    args = [
      "--output",
      rebase_path(_output, root_build_dir),
      "--jq",
      rebase_path(_jq, root_build_dir),
    ]
    if (clippy_cause_failure) {
      args += [ "--fail" ]
    }
    args += [
      "--",
      "$rebased_rustc_prefix/clippy-driver",
      rebase_path(invoker.crate_root, root_build_dir),
      "--target=$rust_target",
      "--edition=$edition",
      "--cfg=rust_panic=\"${rust_panic}\"",
      "--sysroot=$rebased_rust_sysroot",
      "--crate-type=${invoker.clippy_crate_type}",
      "-Ldependency=host_x64",  # Needed for proc macros
    ]
    if (clippy_force_warn) {
      args += [ "--force-warn=clippy::all" ]
    } else {
      _level = "A"
      if (clippy_warn) {
        _level = "W"
      }
      args += [
        "-${_level}clippy::all",
        "-Dclippy::correctness",
      ]
    }
    args += invoker.rustflags

    # Search the list of configs for the "proc macro test" one, and if it's present,
    # apply the same flags it would have applied because clippy doesn't have access
    # to GN configs.
    _proc_macro = "//build/config/rust:proc_macro_test"
    if (invoker.configs + [ _proc_macro ] - [ _proc_macro ] !=
        invoker.configs || invoker.clippy_crate_type == "proc-macro") {
      args += [
        "--extern",
        "proc_macro",
      ]
    }
  }
}
