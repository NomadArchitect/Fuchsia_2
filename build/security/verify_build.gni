# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/compiled_action.gni")
import("//build/dev.gni")

# Check the kernel cmdline extracted from ZBI against a golden file.
#
# Parameters
#
#   zbi
#     Required: Path to the ZBI image to extract kernel cmdline from.
#   zbi_target
#     Required: The target to build the 'zbi'.
#   goldens
#     Required: Path to a list of golden files that contain golden kernel
#       cmdline in the format of one cmdline entry per line. The actual cmdline
#       must match either one of the goldens. There should be only one golden
#       file in this list for normal case and two golden files, one for the
#       old golden file, one for the new golden file during a soft transition.
#   deps, public_deps, data_deps (optional)
#     Usual GN meaning.

template("verify_kernel_cmdline") {
  compiled_action("${target_name}") {
    assert(defined(invoker.zbi), "verify_kernel_cmdline() must specify zbi")
    assert(defined(invoker.zbi_target),
           "verify_kernel_cmdline() must specify zbi_target")
    assert(defined(invoker.goldens),
           "verify_kernel_cmdline() must specify goldens")
    forward_variables_from(invoker,
                           [
                             "testonly",
                             "deps",
                             "public_deps",
                             "data_deps",
                             "visibility",
                           ])

    tool = "//src/security/scrutiny/bin/verify_kernel_cmdline:bin"
    tool_output_name = "scrutiny_verify_kernel_cmdline"
    stamp_file = "$target_gen_dir/$target_name.verified"

    inputs = [ invoker.zbi ]
    inputs += invoker.goldens
    outputs = [ stamp_file ]

    args = [
             "--zbi",
             rebase_path(invoker.zbi, root_build_dir),
             "--stamp",
             rebase_path(stamp_file, root_build_dir),
             "--goldens",
           ] + rebase_path(invoker.goldens, root_build_dir)

    if (!defined(invoker.deps)) {
      deps = []
    }

    deps += [ invoker.zbi_target ]
  }
}

# Check the bootfs filelist extracted from ZBI against a golden file.
#
# Parameters
#
#   zbi
#     Required: Path to the ZBI image to extract bootfs from.
#   zbi_target
#     Required: The target to build the 'zbi'.
#   goldens
#     Required: Path to a list of golden files that contain golden bootFS
#       file list in the format of one file name per line. The actual bootFS
#       filelist must match either one of the goldens. There should be only one
#       golden file in this list for normal case and two golden files, one for
#       the old golden file, one for the new golden file during a soft
#       transition.
#   deps, public_deps, data_deps (optional)
#     Usual GN meaning.

template("verify_bootfs_filelist") {
  assert(defined(invoker.zbi), "verify_bootfs_filelist() must specify zbi")
  assert(defined(invoker.zbi_target),
         "verify_bootfs_filelist() must specify zbi_target")
  assert(defined(invoker.goldens),
         "verify_kernel_cmdline() must specify goldens")

  compiled_action("${target_name}") {
    forward_variables_from(invoker,
                           [
                             "testonly",
                             "deps",
                             "public_deps",
                             "data_deps",
                             "visibility",
                           ])

    tool = "//src/security/scrutiny/bin/verify_bootfs:bin"
    tool_output_name = "scrutiny_verify_bootfs"
    stamp_file = "$target_gen_dir/$target_name.verified"

    inputs = [ invoker.zbi ]
    inputs += invoker.goldens

    outputs = [ stamp_file ]

    args = [
             "--zbi",
             rebase_path(invoker.zbi, root_build_dir),
             "--stamp",
             rebase_path(stamp_file, root_build_dir),
             "--goldens",
           ] + rebase_path(invoker.goldens, root_build_dir)

    if (!defined(invoker.deps)) {
      deps = []
    }

    deps += [ invoker.zbi_target ]
  }
}

# Check the static pkgs list against a golden file.
#
# The target that generates 'zbi' needs to be added to deps.
#
# Parameters
#
#   zbi
#     Required: Path to the ZBI image.
#   zbi_target
#     Required: The target to build the 'zbi'.
#   blobfs_manifest
#     Required: Path to the blobfs manifest file.
#   blobfs_manifest_target:
#     Required: The target to build the 'blobfs_manifest'.
#   goldens
#     Required: Path to a list of golden files that contain golden static pkgs
#       list in the format of one static pkg name per line. The actual static
#       pkgs list must match either one of the goldens. There should be only one
#       golden file in this list for normal case and two golden files, one for
#       the old golden file, one for the new golden file during a soft
#       transition.
#   deps, public_deps, data_deps (optional)
#     Usual GN meaning.

template("verify_static_pkgs") {
  assert(defined(invoker.zbi), "verify_static_pkgs() must specify zbi")
  assert(defined(invoker.zbi_target),
         "verify_static_pkgs() must specify zbi_target")
  assert(defined(invoker.blobfs_manifest),
         "verify_static_pkgs() must specify blobfs_manifest")
  assert(defined(invoker.blobfs_manifest_target),
         "verify_static_pkgs() must specify blobfs_manifest_target")
  assert(defined(invoker.goldens),
         "verify_kernel_cmdline() must specify goldens")

  compiled_action("${target_name}") {
    forward_variables_from(invoker,
                           [
                             "testonly",
                             "deps",
                             "public_deps",
                             "data_deps",
                             "visibility",
                           ])

    tool = "//src/security/scrutiny/bin/verify_static_pkgs:bin"
    tool_output_name = "scrutiny_verify_static_pkgs"
    stamp_file = "$target_gen_dir/$target_name.verified"
    depfile = "$target_gen_dir/$target_name.d"

    inputs = [
               invoker.zbi,
               invoker.blobfs_manifest,
             ] + invoker.goldens

    outputs = [ stamp_file ]

    args = [
             "--zbi",
             rebase_path(invoker.zbi, root_build_dir),
             "--manifest",
             rebase_path(invoker.blobfs_manifest, root_build_dir),
             "--stamp",
             rebase_path(stamp_file, root_build_dir),
             "--depfile",
             rebase_path(depfile, root_build_dir),
             "--goldens",
           ] + rebase_path(invoker.goldens, root_build_dir)

    if (!defined(invoker.deps)) {
      deps = []
    }

    deps += [
      invoker.zbi_target,
      invoker.blobfs_manifest_target,
    ]
  }
}
