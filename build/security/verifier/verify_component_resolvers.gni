# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/compiled_action.gni")
import("//build/dev.gni")

# Validates that privileged component resolvers only resolve allowed static components,
# determined by the provided allowlist.
#
# Parameters
#   deps, public_deps, data_deps (optional)
#       Usual GN meaning.
#
#   allowlist
#       Path to the allowlist defining which component resolvers are privileged and
#       which components are allowed to be resolved by them.
#
template("verify_component_resolvers") {
  compiled_action("${target_name}") {
    forward_variables_from(invoker,
                           [
                             "testonly",
                             "deps",
                             "public_deps",
                             "data_deps",
                             "visibility",
                           ])

    depfile = "$target_out_dir/$target_name.d"
    tool = "//src/security/verifiers/verify_component_resolvers:bin"
    tool_output_name = "scrutiny_verify_component_resolvers"
    stamp_file = "$target_gen_dir/$target_name.verified"
    published_packages = "//build/images:publish"

    inputs = [ invoker.allowlist ]
    outputs = [ stamp_file ]

    args = [
      "--stamp",
      rebase_path(stamp_file, root_build_dir),
      "--depfile",
      rebase_path(depfile, root_build_dir),
      "--allowlist",
      rebase_path(invoker.allowlist, root_build_dir),
    ]

    if (!defined(invoker.deps)) {
      deps = []
    }
    deps += [ published_packages ]
  }
}
