# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Generates a list of the files included in a product that must be uploaded by
# infrastructure.
#
# Arguments
#
#   images_config (required)
#     [path] The path to the images config.
#
#   output_path (required)
#     [path] Where to write the output list.
#
#   sources (optional)
#     [list of str] Additional files or directories to list as inputs.
#
template("generated_assembly_inputs") {
  assert(defined(invoker.images_config), "Need to define images_config")
  assert(defined(invoker.output_path), "Need to define output_path")

  action(target_name) {
    forward_variables_from(invoker,
                           [
                             "deps",
                             "testonly",
                             "visibility",
                           ])
    script = "//build/images/assembly/make_assembly_inputs.py"

    outputs = [ invoker.output_path ]
    inputs = [ invoker.images_config ]

    args = [
      "--images-config",
      rebase_path(invoker.images_config, root_build_dir),
      "--output",
      rebase_path(invoker.output_path, root_build_dir),
    ]

    if (defined(invoker.sources)) {
      args += [ "--sources" ]
      foreach(source, invoker.sources) {
        args += [ rebase_path(source, root_build_dir) ]
      }
    }
  }
}
