# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/images/args.gni")
import("//build/images/assemble_system.gni")
import("//build/images/custom_signing.gni")
import("//build/images/manifest.gni")
import("//build/images/vboot/vboot.gni")

assert(current_toolchain == target_toolchain)

assemble_system("bringup") {
  testonly = true
  board_name = board_name
  output_dir = root_build_dir
  use_esp = target_cpu != "arm64" && !use_vboot

  include_version_and_epoch = true
  include_component_id_index = true
  include_shell_commands = false

  cmdline_deps = [ "//build/input:bootfs" ]
  recovery = recovery_label

  base_packages = []
  base_driver_packages = [ "//:additional_base_driver_packages" ]

  bootfs_labels = [
    "//build/images:driver-manager-base-config",
    "//build/input:bootfs",
    "//build/input:system_image",
    "//:additional_base_driver_packages",
    "//:additional_base_packages",
    "//:additional_cache_packages",
    "//:additional_universe_packages",
  ]

  # blobfs arguments
  blob_layout_format = blob_layout_format
  compress_blobs = compress_blobs

  # zbi arguments
  if (custom_signing_script != "") {
    zbi_signing_script = custom_signing_script
  } else if (use_vboot) {
    zbi_signing_script = vboot_action.script
    zbi_signing_args = vboot_action.args
  }

  metadata = {
    images = [
      {
        label = get_label_info(":$target_name", "label_with_toolchain")
        name = "zircon-a"
        path = "bringup.zbi"
        type = "zbi"

        archive = true
        bootserver_pave = []

        # Bringup builds always mexec from the paver
        bootserver_pave += [ "--boot" ]

        if (custom_signing_script == "" && !use_vboot) {
          bootserver_pave += [
            "--zircona",
            # TODO(fxbug.dev/32475): `dm reboot-recovery` boots from zircon-b instead of
            # zircon-r, so for now zedboot is being paved to this slot.
            # "--zirconb",
          ]
        }
      },
    ]

    if (use_vbmeta) {
      images += [
        {
          label = get_label_info(":$target_name", "label_with_toolchain")
          name = "zircon-a"
          path = "bringup.vbmeta"
          type = "vbmeta"

          archive = true
          bootserver_pave = [ "--vbmetaa" ]
        },
      ]
    }

    image_paths = [
      "IMAGE_ZIRCONA_ZBI=bringup.zbi",

      # TODO(mcgrathr): The complete ZBI can be used with a separate
      # kernel too, the kernel image in it will just be ignored.  So
      # just use the primary ZBI for this until all uses are
      # converted to using the ZBI alone.  Then remove this as
      # IMAGE_BOOT_RAM variable should no longer be in use.
      "IMAGE_BOOT_RAM=bringup.zbi",
    ]

    images += [
      {
        archive = true
        bootserver_netboot = [ "--boot" ]
        name = "fuchsia"
        path = "bringup.zbi"
        type = "zbi"
      },
    ]
    image_paths += [
      "IMAGE_NETBOOT_RAM=bringup.zbi",
      "IMAGE_NETBOOT_ZBI=bringup.zbi",
    ]
  }
}
