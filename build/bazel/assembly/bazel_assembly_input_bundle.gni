# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bazel/bazel_inputs.gni")

# Similar to bazel_input_resource_directory, except this rule adds a root file
# so the AIB dir can be referenced by the corresponding Bazel rule.
#
# Parameters
#
#  name
#  source_dir
#  dest_dir
#    See comment for bazel_input_resource_directory
#
#  deps
#  testonly
#  visibility
#
# NOTE: currently source_dir has to be a directory under root_build_dir,
# otherwise this template can write to the source tree.
#
template("bazel_assembly_input_bundle") {
  assert(defined(invoker.source_dir), "source_dir is required")
  assert(defined(invoker.dest_dir), "dest_dir is required")

  _root_target = "${target_name}_root"
  action(_root_target) {
    forward_variables_from(invoker,
                           [
                             "testonly",
                             "deps",
                           ])
    script = "//build/bazel/assembly/gen_aib_root.sh"
    _root_file = "${invoker.source_dir}/root"
    args = [ rebase_path(_root_file, root_build_dir) ]
    outputs = [ _root_file ]
  }

  bazel_input_resource_directory(target_name) {
    forward_variables_from(invoker,
                           [
                             "name",
                             "source_dir",
                             "dest_dir",
                             "testonly",
                             "vsibility",
                           ])
    deps = [ ":${_root_target}" ]
    if (defined(invoker.deps)) {
      deps += invoker.deps
    }
  }
}
